# -*- mode: org; coding: utf-8; lexical-binding: t -*-

#+TITLE: Modern Emacs Configuration
#+AUTHOR: YAMASHITA, Takao
#+EMAIL: tjy1965@gmail.com
#+LANGUAGE: en
#+OPTIONS: toc:3 num:t
#+STARTUP: overview
#+PROPERTY: header-args :results silent :exports code
#+PROPERTY: header-args:emacs-lisp :lexical t
#+PROPERTY: header-args:emacs-lisp+ :noweb no-export

# Tangling policy (documentation only; does not force all blocks):
# - early-init.el : Startup performance & low-level toggles (GC, file-name-handler, native-comp).
# - init.el       : Packages & leaf-based configuration.
# - user.el       : Personal, machine-specific overrides (optional).
# - README.el     : Unified output for quick test/byte-compile.
# - Makefile      : Reproducible tangle/clean/compile pipeline.

* Introduction
:PROPERTIES:
  :CUSTOM_ID: introduction
  :END:

A modern, literate Emacs configuration using Org Mode's Babel format, emphasizing performance, language server integration, AI assistance, and productivity.

** Features
:PROPERTIES:
   :CUSTOM_ID: features
   :END:

- *Performance & Native Compilation* - JIT compilation with optimized settings, intelligent GC via GCMH, and async compilation
- *Language Server Protocol* - Configurable LSP backends (Eglot/LSP-Mode) with automatic server management
- *AI Integration* - Aidermacs with vterm backend supporting GPT-4, Claude 3.5 Sonnet, and OpenRouter APIs
- *Modern UI & Editing* - Tree-Sitter syntax highlighting, ef-themes, Nerd Icons, and advanced completion
- *Productivity Tools* - GTD-style Org workflow, Org-roam networked notes, and modern styling

[[file:demo.png]]

** Installation
:PROPERTIES
   :CUSTOM_ID: installation
   :END:

*** Prerequisites
- Emacs 30.0+ with native compilation support
- Git, make, gcc (10+), libgccjit
- Optional: ripgrep, aspell, pass, Homebrew (macOS)

*** Makefile

#+begin_src text :tangle Makefile :comments no
  EMACS     ?= emacs
  ORG       ?= README.org
  LISP_DIR  ?= lisp

  .PHONY: all tangle clean

  all: tangle

  tangle:
  	@mkdir -p $(LISP_DIR)
  	$(EMACS) --batch -Q \
  	  --eval "(require 'org)" \
  	  --eval "(setq org-confirm-babel-evaluate nil)" \
  	  --eval "(org-babel-tangle-file \"$(ORG)\")"

  clean:
	rm -f *.el $(LISP_DIR)/*.el
#+end_src

*** Quick Start

1. Clone the repository:
   #+begin_src shell
   git clone --depth 1 https://github.com/ac1965/.emacs.d ~/.emacs.d
   #+end_src

2. Tangle configuration:
   #+begin_src shell
   cd ~/.emacs.d/
   EMACS=/Applications/Emacs.app/Contents/MacOS/Emacs make
   #+end_src

*** Building Emacs

Use the provided build script:
[[https://github.com/ac1965/dotfiles/blob/master/.local/bin/build-emacs.sh][build-emacs.sh]]

#+begin_src shell
build-emacs.sh --native-compilation
#+end_src

*** System Information

**** Apple Silicon (Primary)
- GNU Emacs *31.0.50*

|Property|Value|
|--------|-----|
|Commit|2954234f8ff227fe276b88d759b1e247ca811c8c|
|Branch|master|
|System|aarch64-apple-darwin24.6.0|
|Date|2025-09-11 18:03:01 (JST)|
|Patch|N/A ns-inline.patch|
|Features|ACL DBUS GLIB GNUTLS LCMS2 LIBXML2 MODULES NATIVE_COMP NOTIFY KQUEUE NS PDUMPER PNG RSVG SQLITE3 THREADS TOOLKIT_SCROLL_BARS TREE_SITTER WEBP XIM ZLIB|
|Options|--with-native-compilation --with-gnutls=ifavailable --with-json --with-modules --with-tree-sitter --with-xml2 --with-librsvg --with-mailutils --with-native-image-api --with-ns CPPFLAGS=-I/opt/homebrew/opt/llvm/include 'LDFLAGS=-L/opt/homebrew/opt/llvm/lib -L/opt/homebrew/opt/llvm/lib/c++ -Wl,-rpath,/opt/homebrew/opt/llvm/lib/c++'|

**** Intel (Secondary)
- Darwin Kernel 24.3.0, GNU Emacs 31.0.50 (master branch)
- Architecture: x86_64-apple-darwin24.4.0
- Features: NATIVE_COMP, TREE_SITTER, XWIDGETS, MODULES

* Configuration Files
:PROPERTIES:
   :CUSTOM_ID: structure
   :END:

** Early Initialization
:PROPERTIES:
   :CUSTOM_ID: performance
   :END:

Performance optimizations and environment setup executed before main initialization.

#+begin_src emacs-lisp :tangle early-init.el
  ;;; early-init.el --- Early initialization -*- lexical-binding: t; -*-
  ;;; Commentary:
  ;; Early-stage optimizations for Emacs startup performance
  ;; - GC threshold tuning
  ;; - File-name-handler optimizations
  ;; - Native compilation settings
  ;;; Code:

  ;; Compatibility Check
  ;; Guard: Emacs 30+ only (faster than parsing full version string)
  (when (< emacs-major-version 30)
    (error "This configuration requires Emacs 30+"))

  ;; Utility Functions
  (defun my:ensure-directory-exists (dir)
    "Ensure DIR exists, creating if necessary."
    (unless (file-directory-p dir)
      (condition-case err
          (make-directory dir t)
        (error (warn "Failed to create directory: %s - %s" dir err)))))

  ;; Directory Structure
  (defvar my:d (or (and load-file-name
                        (file-name-directory (file-chase-links load-file-name)))
                   user-emacs-directory)
    "Base configuration directory.")

  (defvar my:d:cache (expand-file-name ".cache/" my:d))
  (defvar my:d:etc (expand-file-name ".etc/" my:d))
  (defvar my:d:var (expand-file-name ".var/" my:d))
  (defvar my:d:elpa (expand-file-name "elpa/" my:d:cache))
  (defvar my:d:eln  (expand-file-name "eln-cache/" my:d:cache))
  (defvar my:f:custom (expand-file-name "custom.el" my:d:etc))

  (mapc #'my:ensure-directory-exists (list my:d:cache my:d:etc my:d:var my:d:elpa my:d:eln))

  ;;;; Startup performance toggles
  (defconst my--gc-cons-threshold-start (* 128 1024 1024) "128MiB during startup.")
  (defconst my--gc-cons-threshold-normal (* 64 1024 1024)  "64MiB after startup.")
  (setq gc-cons-threshold my--gc-cons-threshold-start
        gc-cons-percentage 0.6)

  ;; Save & clear to speed up startup, then robustly restore later.
  (defvar my--saved-file-name-handler-alist file-name-handler-alist)
  (setq file-name-handler-alist nil)

  ;; Improve subprocess throughput (safe on 30+): 8 MiB
  (setopt read-process-output-max (* 8 1024 1024))

  ;; Guard to make the restore routine idempotent.
  (defvar my--startup-restored-p nil
    "Non-nil once startup toggles have been restored exactly once.")

  (defun my--restore-startup-toggles ()
    "Restore GC/file-name-handler after init (idempotent)."
    (unless my--startup-restored-p
      (setq my--startup-restored-p t)
      ;; Restore safe defaults.
      (setq gc-cons-threshold  my--gc-cons-threshold-normal
            gc-cons-percentage 0.1
            file-name-handler-alist my--saved-file-name-handler-alist)
      ;; One-time message to avoid duplicate prints even if the hook runs twice.
      (message "Emacs ready in %.2fs with %d GCs"
               (float-time (time-subtract after-init-time before-init-time))
               gcs-done)))

  ;; Avoid duplicate hook entries in case early-init.el is sourced twice.
  (add-hook 'emacs-startup-hook #'my--restore-startup-toggles)

  ;; Redirect native-compilation cache before packages load.
  (let ((eln-dir my:d:eln))
    (condition-case err
        (when (and (fboundp 'startup-redirect-eln-cache)
                   (featurep 'native-compile)
                   (or (boundp 'comp-eln-load-path)
                       (boundp 'native-comp-eln-load-path)))
          (startup-redirect-eln-cache eln-dir))
      (error (message "[early-init] skip startup-redirect-eln-cache: %S" err))))

  (when (file-directory-p my:d:eln)
    (cond
     ((boundp 'comp-eln-load-path)
      (add-to-list 'comp-eln-load-path my:d:eln))
     ((boundp 'native-comp-eln-load-path)
      (add-to-list 'native-comp-eln-load-path my:d:eln))))

  (add-hook 'after-init-hook     #'my--restore-startup-toggles) ;; redundancy for safety

  ;;;; Early UI: minimal params only (full UI in init.el)
  (setq frame-inhibit-implied-resize t
        frame-resize-pixelwise t
        inhibit-startup-screen t
        inhibit-startup-message t
        inhibit-startup-echo-area-message user-login-name)

  ;; Idempotent frame params for first & subsequent GUI frames
  (dolist (kv '((menu-bar-lines . 0)
                (tool-bar-lines . 0)
                (vertical-scroll-bars . nil)
                (fullscreen . maximized)))
    (setf (alist-get (car kv) default-frame-alist) (cdr kv))
    (setf (alist-get (car kv) initial-frame-alist) (cdr kv)))

  ;; Daemon: ensure GUI client frames are maximized too
  (when (daemonp)
    (add-hook 'after-make-frame-functions
              (lambda (f)
                (when (display-graphic-p f)
                  (set-frame-parameter f 'fullscreen 'maximized)))))

  ;; Package system: disable built-in auto init & quickstart cache.
  ;; - We initialize package.el only once in init.el.
  (setopt package-enable-at-startup nil
          package-quickstart      nil)

  (provide 'early-init)
  ;;; early-init.el ends here
#+end_src

** Main Initialization
:PROPERTIES:
   :CUSTOM_ID: initial
   :END:

Minimal init.el that loads the main configuration from README.org.

#+begin_src emacs-lisp :tangle init.el
  ;;; init.el --- Main initialization -*- lexical-binding: t; -*-
  ;;; Commentary:
  ;; Delegates configuration to README.org via Org Babel
  ;;; Code:

  ;; -------------------------------------------------------------------
  ;; Fallbacks for variables/functions usually defined in early-init.el
  ;; These only take effect if the symbols are not already bound/defined.
  ;; -------------------------------------------------------------------
  (eval-and-compile
    ;; Base dir (use existing value if already set by early-init).
    (defvar my:d
      (or (and (boundp 'my:d) my:d)
          (and load-file-name (file-name-directory (file-chase-links load-file-name)))
          user-emacs-directory)
      "Base configuration directory (fallback when early-init.el not loaded).")

    ;; Subdirs
    (defvar my:d:cache (or (and (boundp 'my:d:cache) my:d:cache)
                           (expand-file-name ".cache/" my:d)))
    (defvar my:d:etc   (or (and (boundp 'my:d:etc)   my:d:etc)
                           (expand-file-name ".etc/"   my:d)))
    (defvar my:d:var   (or (and (boundp 'my:d:var)   my:d:var)
                           (expand-file-name ".var/"   my:d)))
    (defvar my:d:elpa  (or (and (boundp 'my:d:elpa)  my:d:elpa)
                           (expand-file-name "elpa/"   my:d:cache)))
    (defvar my:d:eln   (or (and (boundp 'my:d:eln)   my:d:eln)
                           (expand-file-name "eln-cache/" my:d:cache)))
    (defvar my:f:custom (or (and (boundp 'my:f:custom) my:f:custom)
                            (expand-file-name "custom.el" my:d:etc)))

    ;; Utility from early-init (define only if missing)
    (unless (fboundp 'my:ensure-directory-exists)
      (defun my:ensure-directory-exists (dir)
        "Ensure DIR exists, creating if necessary."
        (unless (file-directory-p dir)
          (condition-case err
              (make-directory dir t)
            (error (ignore-errors
                     (message "Failed to create directory: %s (%s)" dir err)))))))

    (mapc #'my:ensure-directory-exists (list my:d:cache my:d:etc my:d:var my:d:elpa my:d:eln))

    ;; Byte-compiler friendliness (silence unknown/obsolete warnings)
    (defvar display-line-numbers-type nil)
    (defvar no-littering-etc-directory (expand-file-name ".etc/" my:d))
    (defvar no-littering-var-directory (expand-file-name ".var/" my:d))
    (declare-function leaf-keywords-init "leaf-keywords")
    (declare-function exec-path-from-shell-initialize "exec-path-from-shell"))

  ;; Unbind disruptive or unused default keys (clearer API)
  (keymap-global-unset "C-z")       ; suspend-frame
  (keymap-global-unset "C-x C-z")   ; suspend-frame alias
  (keymap-global-unset "M-z")       ; zap-to-char (destructive)
  (keymap-global-unset "M-m")       ; free a handy prefix
  (keymap-global-unset "M-/")       ; prefer modern completion

  ;; Initialize package.el exactly once to avoid double-boot.
  (require 'package)
  (setopt package-archives '(("gnu" . "https://elpa.gnu.org/packages/")
                             ("melpa" . "https://melpa.org/packages/"))
  	package-gnupghome-dir (expand-file-name ".gnupg" my:d:var)
  	package-user-dir my:d:elpa
          package-vc-allow-clone t)

  (unless (bound-and-true-p package--initialized)
    (package-initialize))

  ;; Bootstrap leaf (and friends) if missing.
  (unless (package-installed-p 'leaf)
    (package-refresh-contents) ; refresh once here
    (package-install 'leaf))

  (eval-when-compile (require 'leaf))
  (leaf leaf-keywords :ensure t
    :init (leaf blackout :ensure t)
    :config (leaf-keywords-init))
  (leaf leaf-convert :ensure t)

  (leaf emacs
    :ensure nil
    :init
    ;; UI toggles are in early-init to avoid flicker.
    (pixel-scroll-precision-mode)

    ;; Basic behavior
    (setopt inhibit-startup-screen  t
            initial-scratch-message nil
            use-short-answers       t
            create-lockfiles        nil
            make-backup-files       t
            delete-old-versions     t
            version-control         t
            vc-make-backup-files    t)

    ;; Performance (fix: `idle-time` -> `idle-update-delay`)
    ;; Smaller value updates redisplay during idle more frequently.
    (setq idle-update-delay 0.2)

    ;; Electric pairs
    (electric-pair-mode 1)

    ;; Line numbers
    (setq display-line-numbers-type 'relative)
    (add-hook 'prog-mode-hook #'display-line-numbers-mode)

    ;; Auto-save
    (setq auto-save-default t
          auto-save-visited-interval 2)
    (auto-save-visited-mode 1))

  ;; No-Littering
  (leaf no-littering
    :ensure t
    :require t
    :init
    ;; Set directories before package loads things that compute paths.
    (setq no-littering-etc-directory my:d:etc
          no-littering-var-directory my:d:var))

  ;;;; macOS integration
  ;; 1) Import shell environment for GUI Emacs
  (leaf exec-path-from-shell
    :ensure t
    :if (and (memq window-system '(mac ns)) (display-graphic-p))
    :custom ((exec-path-from-shell-check-startup-files . nil)
             (exec-path-from-shell-arguments . '("-l" "-i"))
             (exec-path-from-shell-variables .
  					   '("PATH" "MANPATH" "LANG" "LC_ALL"
  					     "PASSWORD_STORE_DIR" "GPG_KEY_ID"
  					     "OPENROUTER_API_KEY" "OPENAI_API_KEY")))
    :config
    (unless noninteractive
      (exec-path-from-shell-initialize)))

  ;; 2) Fallback PATH for CLI/edge cases (was in early-init; moved here)
  (when (eq system-type 'darwin)
    (dolist (p '("/opt/homebrew/bin" "/usr/local/bin"))
      (when (and (file-directory-p p)
                 (not (member p exec-path)))
        (add-to-list 'exec-path p)
        (setenv "PATH" (concat p ":" (getenv "PATH"))))))

  ;; 3) Dired + GNU ls (gls) integration (was in early-init; moved here)
  (leaf dired
    :custom ((dired-listing-switches . "-aBhl --group-directories-first"))
    :config
    (when (and (eq system-type 'darwin) (executable-find "gls"))
      (setq insert-directory-program "gls"
            dired-use-ls-dired t)))

  ;; Garbage Collection Magic Hack
  (leaf gcmh
    :ensure t
    :hook (emacs-startup . gcmh-mode))

  (leaf hydra
    :ensure t
    :commands (defhydra))

  ;; custom-file early, but load only if present
  (setq custom-file my:f:custom)
  (when (and custom-file (file-exists-p custom-file))
    (ignore-errors (load custom-file nil 'nomessage)))

  ;; Load User-Specific Configurations
  ;; - Dynamically loads an additional configuration file specific to the current
  ;;   user (e.g., "username.el") if it exists.
  (setq user-specific-config (concat my:d user-login-name ".el"))
  (if (file-exists-p user-specific-config) (load user-specific-config))

  ;; Respect each block's :tangle; never override TARGET-FILE from code.
  (let* ((root (cond
                ((and (boundp 'my:d) (stringp my:d) (file-directory-p my:d))
                 (file-name-as-directory my:d))
                (t (file-name-as-directory user-emacs-directory))))
         (lisp-dir (expand-file-name "lisp" root))
         (org-file (expand-file-name "README.org" root))
         (el-file  (expand-file-name "README.el"  lisp-dir)))
    (when (file-exists-p org-file)
      ;; Ensure dedicated load path (avoid adding root itself).
      (unless (file-directory-p lisp-dir)
        (make-directory lisp-dir t))
      (dolist (p (list (file-name-as-directory user-emacs-directory)
                       (directory-file-name user-emacs-directory)))
        (setq load-path (delete p load-path)))
      (add-to-list 'load-path lisp-dir)

      ;; Tangle only when needed, honoring per-block :tangle destinations.
      (defvar org-confirm-babel-evaluate)
      (require 'org)
      (require 'ob-tangle)
      (let* ((org-confirm-babel-evaluate nil)
             (case-fold-search t)
             (lang-re "\\`\\(?:emacs-lisp\\|elisp\\)\\'"))
        (when (or (not (file-exists-p el-file))
                  (file-newer-than-file-p org-file el-file))
          ;; ✅ Do not pass TARGET-FILE -> respect each block's :tangle.
          (org-babel-tangle-file org-file nil lang-re)))

      ;; Load README feature idempotently.
      (unless (or (featurep 'README)
                  (require 'README nil 'noerror))
        (let ((generated el-file))
          (when (file-exists-p generated)
            (load generated nil 'nomessage)))
        (unless (featurep 'README)
          (provide 'README)))))


  (provide 'init)
  ;;; init.el ends here
#+end_src

** User Configuration

:PROPERTIES:
   :CUSTOM_ID: user-defined
   :END:

Personal and device-specific settings.

#+begin_src emacs-lisp :tangle user.el
  ;;; user.el --- Personal Configuration -*- lexical-binding: t; -*-
  ;;; Commentary:
  ;; Personal settings and device-specific configurations
  ;;; Code:

  ;; Personal Settings
  (leaf *personals
    :config
    (setq user-full-name "YAMASHITA, Takao"
          user-mail-address "tjy1965@gmail.com"
          my:font-default "JetBrains Mono NL"
          my:font-alt "Noto Sans JP"
          my:emoji-font "MesloLGS NF"
          my:font-size 16
          inhibit-compacting-font-caches t
          plstore-cache-passphrase-for-symmetric-encryption t)

    (defconst my:d:cloud "~/Documents/")
    (defconst my:d:blog (concat my:d:cloud "devel/repos/mysite/"))
    (defconst my:f:capture-blog-file (expand-file-name "all-posts.org" my:d:blog))

    (defvar my:excluded-directories '("/Users/ac1965/Library/Accounts"))

    (mapc #'my:ensure-directory-exists (list my:d:cloud my:d:blog))

    (setq load-path
          (seq-remove (lambda (dir) (member dir my:excluded-directories))
                      load-path)))

  ;; Smart Input Source (auto switch input source, e.g. English <-> Japanese)
  ;; macOS requirement: `brew install macism`
  (leaf sis
    :ensure t
    :config
    ;; First run:
    ;;   Run M-x sis-get with each input source active
    ;;   Copy the corresponding IDs here
    (sis-ism-lazyman-config
     "com.apple.keylayout.ABC" ;; English layout
     "com.apple.inputmethod.Kotoeri.RomajiTyping.Japanese" ;; Japanese IME
     'macism)

    ;; set cursor color explicitly
    (setq sis-english-cursor-color "blue"
          sis-other-cursor-color "lime")

    ;; Enable global modes
    (sis-global-respect-mode t)       ;; auto switch based on context
    (sis-global-cursor-color-mode t)  ;; change cursor color by input source
    (sis-global-inline-mode t))       ;; inline switch inside comments/strings

  (add-hook 'after-load-theme-hook
            (lambda ()
              (set-face-background 'cursor (frame-parameter nil 'cursor-color))))

  ;; Logitech MX Ergo S Configuration
  (leaf *device/MX_ErgoS
    :config
    (setq mouse-wheel-scroll-amount '(1 ((shift) . 5) ((control) . 10))
          mouse-wheel-progressive-speed nil
          scroll-conservatively 10000
          scroll-margin 2
          scroll-preserve-screen-position t
          mac-mouse-wheel-smooth-scroll t
          mouse-wheel-tilt-scroll t
          mouse-wheel-flip-direction nil)

    (global-set-key [mouse-2] 'yank)
    (global-set-key [mouse-4] 'previous-buffer)
    (global-set-key [mouse-5] 'next-buffer))

  ;; Apple Music Controller (macOS only)
  (when (eq system-type 'darwin)
    (leaf apple-music
      :doc "Apple Music control via AppleScript"
      :config
      ;; Core functions for AppleScript execution
      (defun apple-music-osascript-async (script &optional callback)
        "Run AppleScript SCRIPT asynchronously."
        (let* ((proc-name "apple-music-async")
               (buffer-name "*Apple Music Async*")
               (osascript-cmd (list "osascript" "-e" script))
               (proc (apply 'start-process proc-name buffer-name osascript-cmd)))
          (when callback
            (set-process-sentinel
             proc
             (lambda (process event)
               (when (string= event "finished\n")
                 (with-current-buffer (process-buffer process)
                   (let ((output (string-trim (buffer-string))))
                     (funcall callback output)))
                 (kill-buffer (process-buffer process))))))))

      (defun apple-music-osascript-sync (script)
        "Run AppleScript SCRIPT synchronously."
        (string-trim
         (shell-command-to-string
          (format "osascript -e '%s'" script))))

      ;; Player controls
      (defun apple-music-play-pause ()
        "Toggle play/pause."
        (interactive)
        (apple-music-osascript-async "tell application \"Music\" to playpause"))

      (defun apple-music-next-track ()
        "Skip to next track."
        (interactive)
        (apple-music-osascript-async "tell application \"Music\" to next track"))

      (defun apple-music-previous-track ()
        "Go to previous track."
        (interactive)
        (apple-music-osascript-async "tell application \"Music\" to previous track"))

      ;; Apple Music playlist support
      (defun apple-music-get-playlists ()
        "Return a list of playlist names from Apple Music."
        (split-string
         (apple-music-osascript-sync
  	"tell application \"Music\" to get name of playlists")
         ", "))

      (defun apple-music-play-playlist (playlist)
        "Play the Apple Music playlist named PLAYLIST."
        (interactive
         (list (completing-read "Playlist: " (apple-music-get-playlists))))
        (apple-music-osascript-async
         (format "tell application \"Music\" to play playlist \"%s\"" playlist)))

      ;; Hydra interface
      (defhydra hydra-apple-music (:hint nil)
        "
  Apple Music:
  _p_: Play/Pause  _n_: Next  _b_: Back  _l_: Playlist  _q_: Quit
  "
        ("p" apple-music-play-pause)
        ("n" apple-music-next-track)
        ("b" apple-music-previous-track)
        ("l" apple-music-play-playlist)
        ("q" nil "quit"))

      (global-set-key (kbd "C-c m") 'hydra-apple-music/body)))

  (provide 'user)
  ;;; user.el ends here
#+end_src

** Main Configuration
:PROPERTIES:
   :CUSTOM_ID: core
   :END:

Core Emacs configuration with modular design.

*** Header

#+begin_src emacs-lisp :tangle lisp/README.el
  ;;; --- Emacs Configuration -*- mode: emacs-lisp; lexical-binding:t; -*-

  ;; Copyright (c) 2021-2025 YAMASHITA, Takao <tjy1965@gmail.com>
  ;; Licensed under the GNU General Public License version 3 or later.

  ;; $Lastupdate: 2025/09/11 21:34:22 $

  ;;; Commentary:
  ;; It includes package management, user-specific settings, and modular design.

  ;;; Code:
#+end_src

*** Fonts/UI
**** Fonts

- Font Setup
  This section defines and applies font configurations for Emacs, including:
  - The default monospaced font.
  - An alternate font for comments and variable-pitch text.
  - An emoji font for proper emoji rendering.
- Nerd Icons
  Enables Nerd Icons for visual enhancements in Dired and other UI elements.
- Ligature Setup
  Configures programming ligatures (e.g., `->`, `=>`, `===`) using the `ligature` package.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;;; Font Setup ---------------------------------------------------------------

  ;; -----------------------------------------------------------------------------
  ;; Default font configuration
  (defvar my:font-default
    (or (getenv "EMACS_FONT_FAMILY")
        (cond
         ((eq system-type 'windows-nt) "Consolas")
         ((eq system-type 'darwin) "SF Mono")
         (t "Monospace")))
    "Primary default font for Emacs.")

  (defvar my:font-alt
    (or (getenv "EMACS_FONT_ALT")
        (cond
         ((eq system-type 'windows-nt) "Consolas")
         ((eq system-type 'darwin) "SF Mono")
         (t "Monospace")))
    "Alternate font, e.g., for comments or variable-pitch text.")

  (defvar my:font-size
    (let ((env (getenv "EMACS_FONT_SIZE")))
      (if env
          (string-to-number env)
        (if (and (display-graphic-p)
                 (display-pixel-width)
                 (> (display-pixel-width) 1920))
            24
          20)))
    "Default font size (in pt).")

  (defvar my:emoji-font "Noto Color Emoji"
    "Default font for displaying emoji.")

  ;; -----------------------------------------------------------------------------
  ;; Utility function to check if a font is available on the system.

  (defun font-exists-p (font-name)
    "Return t if FONT-NAME is available on the system."
    (when (find-font (font-spec :family font-name))
      t))

  (defun font-setup (&optional frame)
    "Apply font settings to FRAME or current frame."
    (let ((target-frame (or frame (selected-frame))))
      (when (display-graphic-p target-frame)
        ;; --- Default font
        (when (and (font-exists-p my:font-default)
                   (numberp my:font-size))
          (set-face-attribute 'default target-frame
                              :family my:font-default
                              :height (* my:font-size 10))
          (message "✅ Default font: %s (%dpt)"
                   my:font-default my:font-size))

        ;; --- Variable-pitch font
        (when (font-exists-p my:font-alt)
          (set-face-attribute 'variable-pitch target-frame
                              :family my:font-alt)
          (set-fontset-font t 'japanese-jisx0208
                            (font-spec :family my:font-alt))
          (message "✅ Variable-pitch font (JP): %s"
                   my:font-alt))

        ;; --- Emoji font
        (when (font-exists-p my:emoji-font)
          (set-fontset-font t 'emoji
                            (font-spec :family my:emoji-font)
                            nil 'prepend)
          (message "✅ Emoji font: %s" my:emoji-font)))))

  (add-hook 'window-setup-hook #'font-setup)
  (add-hook 'after-make-frame-functions #'font-setup)

  (defun my:font-setup-on-frame (frame)
    "Apply `font-setup` to newly created FRAME in daemon sessions."
    (when (display-graphic-p frame)
      (with-selected-frame frame
        (font-setup))))

  (if (daemonp)
      (add-hook 'after-make-frame-functions #'my:font-setup-on-frame)
    (when (display-graphic-p)
      (font-setup)))

  ;; -----------------------------------------------------------------------------
  ;; Adjust font-lock faces after loading a theme
  (add-hook 'after-load-theme-hook
            (lambda ()
              (when (font-exists-p my:font-alt)
                (set-face-attribute 'font-lock-comment-face nil
                                    :family my:font-alt :slant 'italic)
                (set-face-attribute 'font-lock-doc-face nil
                                    :family my:font-alt :slant 'italic)
                (message "Comment/doc font set to: %s" my:font-alt))))

  ;; -----------------------------------------------------------------------------
  ;;; Nerd Icons Setup
  (defvar my:nerd-icons-font "JetBrainsMono Nerd Font Mono"
    "Font used for Nerd Icons.")

  (leaf nerd-icons
    :ensure t
    :if (display-graphic-p)
    :custom ((nerd-icons-color-icons . (font-exists-p my:nerd-icons-font))))

  ;; Show icons in Dired using nerd-icons.
  (leaf nerd-icons-dired
    :ensure t
    :hook (dired-mode . nerd-icons-dired-mode)
    :config
    ;; Run once manually if fonts are missing:
    ;; M-x nerd-icons-install-fonts
    )

  ;; -----------------------------------------------------------------------------
  ;;; Ligature Setup
  (defvar my:ligature-font "Fira Code"
    "Font used for programming ligatures.")

  (leaf ligature
    :ensure t
    :config
    (when (and (font-exists-p my:font-default)
               (font-exists-p my:ligature-font))
      (ligature-set-ligatures 'prog-mode
                              '("->" "=>" "::" "===" "!=" "&&" "||"
                                ":::" "!!" "??" "-->" "<--" "->>" "<<-"))
      (global-ligature-mode 1)))
#+end_src

**** UI

- Fullscreen Mode
  Ensures Emacs starts in fullscreen mode, regardless of whether it runs as a standalone instance or daemon.
- Dynamic Window Resizing (Golden Ratio)
  Automatically adjusts window sizes, keeping the current window larger for better focus.
- Theme Configuration
  This setup uses =ef-themes= for modern, accessible color schemes.
  - Loads =ef-frost= when running in GUI.
  - Loads =deeper-blue= when in terminal.
- Spacious Padding
  Adds clean padding around UI elements and mode lines for a more modern look.
- Minions (Mode Line Management)
  Minions consolidates minor modes into a compact menu, reducing mode-line clutter.
- Time and Battery Display
  Displays the current time (24-hour format) and battery percentage in the mode line.
- Tab Bar and Tab Line
  Enables tab-bar and tab-line with a clean, right-aligned layout.
- Treemacs (Project Drawer)
  Adds a sidebar file explorer with live file watching and follow-mode.
- Desktop Session Management
  Saves and restores window layouts and open files between sessions.
- Winner Mode
  Enables undo/redo for window layouts with =M-[= and =M-]=.
- Custom Window Layout Utilities
  Adds functions for saving/restoring layouts and toggling window dedication.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; ---------------------------------------------------------------------------
  ;;; Fullscreen Mode Configuration
  ;; Ensures Emacs starts in fullscreen mode.
  (leaf fullscreen
    ;; Use startup hook so the first frame surely exists (non-daemon).
    :init
    (if (daemonp)
        (add-hook 'after-make-frame-functions
                  (lambda (frame)
                    (when (display-graphic-p frame)
                      (set-frame-parameter frame 'fullscreen 'fullboth))))
      (add-hook 'emacs-startup-hook
                (lambda ()
                  (when (display-graphic-p)
                    (set-frame-parameter nil 'fullscreen 'fullboth))))))

  ;; ---------------------------------------------------------------------------
  ;;; Dynamic Window Resizing (Zoom)
  ;; Automatically resizes windows, focusing the current one.
  (leaf zoom
    :ensure t
    :hook (after-init-hook . zoom-mode)
    :custom
    ;; Keep the selected window around golden-ratio size (width . height).
    ((zoom-size . '(0.62 . 0.62))
     ;; Ignore auxiliary modes/buffers.
     (zoom-ignored-major-modes . '(ediff-mode dired-mode treemacs-mode))
     (zoom-ignored-buffer-names . '("*Messages*" "*Help*"))
     ;; Safety: skip in minibuffer or when only one window.
     (zoom-ignored-predicates . '((lambda () (window-minibuffer-p))
                                  (lambda () (< (count-windows) 2))))))

  ;; ---------------------------------------------------------------------------
  ;;; Theme Configuration (ef-themes)
  ;; Loads `ef-frost` in GUI or `deeper-blue` in terminal.
  (leaf ef-themes
    :ensure t
    :custom ((ef-themes-to-toggle . '(ef-frost ef-spring)))
    :config
    (load-theme (if (display-graphic-p) 'ef-frost 'deeper-blue) t))

  ;; ---------------------------------------------------------------------------
  ;;; Spacious Padding
  ;; Adds extra padding around UI elements for a clean look.
  (leaf spacious-padding
    :ensure t
    :if (display-graphic-p)
    :custom ((spacious-padding-widths . '((left . 15) (right . 15) (top . 10) (bottom . 10)))
             (spacious-padding-subtle-mode-line . t)
             (spacious-padding-mode-line-active-border-width . 1)
             (spacious-padding-mode-line-inactive-border-width . 0))
    :config
    (spacious-padding-mode 1))

  ;; ---------------------------------------------------------------------------
  ;;; Minions (Mode Line Management)
  ;; Consolidates minor modes into a single menu.
  (leaf minions
    :ensure t
    :custom ((minions-mode-line-lighter . "⚙"))
    :hook (after-init-hook . minions-mode))

  ;; ---------------------------------------------------------------------------
  ;;; Doom-modeline
  (leaf doom-modeline
    :ensure t
    :hook (after-init-hook . doom-modeline-mode))

  (leaf time-and-battery
    :after doom-modeline
    :init
    (setq display-time-interval 30
          display-time-day-and-date t
          display-time-24hr-format t
          ;; Use default battery format; doom-modeline reads display-battery-mode.
          )
    :config
    (display-time-mode 1)     ;; enable time in mode-line
    (display-battery-mode 1)) ;; enable battery in mode-line

  ;; ---------------------------------------------------------------------------
  ;;; Tab Bar & Tab Line
  ;; Enables tab-bar and tab-line with custom format.
  (leaf tab-bar
    :custom ((tab-bar-show . 1)
             (tab-bar-new-tab-choice . "*scratch*")
             (tab-bar-format . '(tab-bar-format-tabs tab-bar-separator tab-bar-format-align-right)))
    :hook (after-init-hook . tab-bar-mode))

  (leaf tab-line
    ;; Consider disabling one of bar/line if UI feels redundant.
    :hook (after-init-hook . global-tab-line-mode))

  ;; ---------------------------------------------------------------------------
  ;;; Treemacs (Project Drawer)
  ;; Provides a sidebar file explorer.
  (leaf treemacs
    :ensure t
    :if (display-graphic-p)
    :custom ((treemacs-no-png-images . nil)
             (treemacs-filewatch-mode . t)
             (treemacs-follow-mode . t)
             (treemacs-indentation . 2)
             (treemacs-missing-project-action . 'remove)))
  ;; key bindings are centralized (see my:keys below)

  ;; ---------------------------------------------------------------------------
  ;;; Desktop Session Management
  ;; Saves and restores window layouts and open files.
  (leaf desktop
    :custom `((desktop-dirname . ,(concat no-littering-var-directory "desktop"))
              (desktop-save . 'if-exists)
              (desktop-load-locked-desktop . t)
              (desktop-auto-save-timeout . 180)
              (desktop-restore-eager . 10))
    :hook ((kill-emacs-hook . desktop-save-in-desktop-dir)
           (after-init-hook . (lambda ()
                                (make-directory (concat no-littering-var-directory "desktop") t)
                                (desktop-read))))
    :config
    (desktop-save-mode 1))

  ;; ---------------------------------------------------------------------------
  ;;; Winner Mode
  ;; Allows undo/redo of window configurations.
  (leaf winner
    :config
    (winner-mode 1)) ;; enable mode early; keys are centralized below

  ;; ---------------------------------------------------------------------------
  ;;; Custom Window Layout Utilities
  (defvar my:saved-window-config nil
    "Stores the current window configuration for later restoration.")

  (defun my:save-window-layout ()
    "Save the current window configuration persistently."
    (interactive)
    (setq my:saved-window-config (window-state-get nil t))
    (message "Window configuration saved."))

  (defun my:restore-window-layout ()
    "Restore the previously saved window configuration."
    (interactive)
    (if my:saved-window-config
        (progn
          (window-state-put my:saved-window-config)
          (message "Window configuration restored."))
      (message "No saved window configuration found.")))

  (defun my:toggle-window-dedication ()
    "Toggle the dedicated status of the currently selected window."
    (interactive)
    (let ((window (selected-window)))
      (set-window-dedicated-p window (not (window-dedicated-p window)))
      (message "Window dedication %s"
               (if (window-dedicated-p window) "enabled" "disabled"))))
#+end_src

*** Essential Configuration
**** Minimum setting

This section contains basic enhancements such as automatic timestamps, electric pairs,
and relative line numbers.

- Insert Timestamp on Save
  The following function updates a `$Lastupdate` timestamp at the top of the buffer
  whenever a file is saved.
- Electric Pair Mode
  Automatically insert matching parentheses, quotes, or brackets.
- Relative Line Numbers
  Enable relative line numbers in programming and text modes for easier navigation.

  #+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
  ;;; Basic Editor Configuration

  ;; Insert timestamp on save
  (defun my:save-buffer-wrapper ()
    "Insert or update a `$Lastupdate` timestamp at the top of the buffer."
    (interactive)
    (let ((timestamp (concat "$Lastupdate: " (format-time-string "%Y/%m/%d %H:%M:%S") " $")))
      (save-excursion
        (goto-char (point-min))
        (while (re-search-forward "\\$Lastupdate: [0-9/: ]*\\$" nil t)
          (replace-match timestamp t nil)))))

  (add-hook 'before-save-hook #'my:save-buffer-wrapper)

  ;; Electric pair mode - automatically insert matching brackets/quotes
  (leaf electric-pair
    :doc "Auto insert matching parentheses"
    :init (electric-pair-mode 1))

  ;; Display relative line numbers in programming and text modes
  (leaf display-line-numbers
    :hook ((prog-mode text-mode) . display-line-numbers-mode)
    :init (setq display-line-numbers-type 'relative))

  ;; -----------------------------------------------------------------------------
  ;;; File Management Configuration

  ;; TRAMP setup for remote file editing
  (leaf tramp
    :pre-setq
    `((tramp-persistency-file-name . ,(concat no-littering-var-directory "tramp"))
      (tramp-auto-save-directory . ,(concat no-littering-var-directory "tramp-autosave")))
    :custom
    `((tramp-default-method . "scp")
      (tramp-verbose . 10)))

  ;; Auto-save and backup configuration
  (leaf files
    :custom
    `((auto-save-file-name-transforms . '((".*" ,(concat no-littering-var-directory "backup") t)))
      (auto-save-list-file-prefix . ,(concat no-littering-var-directory "backup/.saves-"))
      (backup-directory-alist . '(("." . ,(concat no-littering-var-directory "backup"))))
      (delete-old-versions . t)
      (auto-save-visited-interval . 2))
    :global-minor-mode auto-save-visited-mode)
#+end_src

**** Editing Enhancements

- Session Persistence
  This section ensures that Emacs remembers various session details such as
  cursor positions, recently opened files, and minibuffer history.
- Parentheses and Pair Management
  Provides structured editing and visual cues for parentheses.
- Tree-Sitter Configuration
  Enables modern syntax highlighting and parsing.
- Editing Tools and Navigation
  Includes tools for undo/redo, window switching, multiple cursors, Git, and search.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
    ;;; Saveplace (Remember Cursor Positions)
  ;; Restores the last cursor position when reopening files.

  (leaf saveplace
    :init
    (setq save-place-file (concat no-littering-var-directory "saveplace"))
    (save-place-mode +1))

  ;; Maintain list of recently opened files
  (leaf recentf
    :init
    (setq recentf-max-saved-items 100
          recentf-save-file (concat no-littering-var-directory "recentf"))
    (recentf-mode +1))

  ;; Save minibuffer history across sessions
  (leaf savehist
    :custom
    `((savehist-file . ,(concat no-littering-var-directory "savehist"))
      (savehist-additional-variables '(kill-ring search-ring regexp-search-ring))
      (savehist-autosave-interval . 300))
    :global-minor-mode t)

  ;; -----------------------------------------------------------------------------
    ;;; Parentheses and Pair Management

  ;; Structured editing for Emacs Lisp
  (leaf paredit
    :ensure t
    :hook (emacs-lisp-mode . enable-paredit-mode))

  ;; Highlight matching parentheses
  (leaf paren
    :custom
    ((show-paren-delay . 0)
     (show-paren-style . 'expression)
     (show-paren-highlight-openparen . t))
    :global-minor-mode show-paren-mode)

  ;; Smart pair handling (disabled in minibuffer)
  (leaf puni
    :ensure t
    :global-minor-mode puni-global-mode
    :hook ((minibuffer-setup . (lambda () (puni-global-mode -1)))))

  ;; -----------------------------------------------------------------------------
    ;;; Tree-Sitter Configuration

  ;; Modern syntax highlighting and parsing
  ;; Prefer built-in treesit on Emacs 30+. Ensure the package is loaded before using its API.
  (when (featurep 'treesit)
    (leaf treesit-auto
      :ensure t
      :require t
      :custom ((treesit-auto-install . t))  ;; auto-install grammars interactively
      :config
      (global-treesit-auto-mode 1)
      (setopt treesit-font-lock-level 3)))

  ;; -----------------------------------------------------------------------------
    ;;; Auto-Revert
  ;; Automatically reloads files when changed on disk (silent refresh every 2s).

  (leaf autorevert
    :custom
    ((auto-revert-interval . 2)
     (auto-revert-verbose . nil))
    :global-minor-mode global-auto-revert-mode)

  ;; -----------------------------------------------------------------------------
    ;;; Which-Key (Key Binding Hints)
  ;; Shows available keybindings in a popup for the current prefix.

  (leaf which-key
    :ensure t
    :global-minor-mode t
    :custom ((which-key-idle-delay . 0.5)))

  ;; -----------------------------------------------------------------------------
    ;;; Undo-Fu (Advanced Undo/Redo)
  ;; Provides linear undo/redo history with better region handling.

  (leaf undo-fu
    :ensure t
    :custom ((undo-fu-allow-undo-in-region . t)))

  ;; -----------------------------------------------------------------------------
    ;;; Ace Window (Window Navigation)
  ;; Provides quick window switching with visual hints.

  (leaf ace-window
    :ensure t
    :custom
    ((aw-keys . '(?a ?s ?d ?f ?g ?h ?j ?k ?l))
     (aw-scope . 'frame)
     (aw-background . t))
    :config
    (ace-window-display-mode 1))

  ;; -----------------------------------------------------------------------------
    ;;; Visual Line Mode
  ;; Enables soft line wrapping for text-based buffers.

  (leaf visual-line-mode
    :hook (text-mode . visual-line-mode))

  ;; -----------------------------------------------------------------------------
    ;;; macOS Clipboard Integration
  ;; Ensures Emacs uses the macOS clipboard via `pbcopy`.

  (leaf pbcopy
    :if (memq window-system '(mac ns))
    :ensure t
    :config
    (turn-on-pbcopy))

  ;; -----------------------------------------------------------------------------
    ;;; Dired Enhancements
  ;; Adds filtering and subtree expansion to Dired.

  (leaf dired-filter :ensure t)
  (leaf dired-subtree
    :after dired)
  ;; key bindings are centralized (see my:keys below)

  ;; -----------------------------------------------------------------------------
    ;;; Editing Tools
  ;; Region expansion, aggressive indentation, and selection overwrite.

  (leaf expand-region :ensure t)
  (leaf aggressive-indent
    :ensure t
    :global-minor-mode global-aggressive-indent-mode)
  (leaf delsel
    :global-minor-mode delete-selection-mode)

  ;; -----------------------------------------------------------------------------
    ;;; Search Tools
  ;; Configures `rg` (ripgrep) as the default search backend.

  (when (executable-find "rg")
    (setopt grep-program "rg")
    (leaf rg :ensure t))

  ;; -----------------------------------------------------------------------------
    ;;; Code Navigation
  ;; Uses Dumb-Jump with `rg` for fast symbol navigation.

  (leaf dumb-jump
    :ensure t
    :hook (xref-backend-functions . dumb-jump-xref-activate)
    :custom
    `((dumb-jump-force-searcher  . 'rg)
      (dumb-jump-prefer-searcher . 'rg)))

  ;; -----------------------------------------------------------------------------
    ;;; Multiple Cursors
  ;; Enables simultaneous editing with multiple cursors.

  (leaf multiple-cursors :ensure t)

  ;; -----------------------------------------------------------------------------
    ;;; Magit (Git Integration)
  ;; A powerful and user-friendly Git interface.

  (leaf magit :ensure t)

  ;; -----------------------------------------------------------------------------
    ;;; Syntax & Spell Checking
  ;; Configures Flycheck (syntax) and Flyspell (spelling).

  (leaf flycheck
    :ensure t
    :hook (prog-mode . flycheck-mode))

  (leaf flyspell
    :ensure t
    :hook (text-mode . flyspell-mode)
    :custom ((ispell-program-name . "aspell")))

  ;; -----------------------------------------------------------------------------
    ;;; Project Management
  ;; Projectile for project navigation and search.

  (leaf projectile
    :ensure t
    :global-minor-mode t)

  ;; -----------------------------------------------------------------------------
    ;;; Snippet Management (YASnippet)
  ;; Loads user-defined snippets from `my:yas-snippet-dir`.

  (leaf yasnippet
    :ensure t
    :global-minor-mode yas-global-mode
    :init
    (defvar my:yas-snippet-dir (concat my:d "snippets")
      "Default directory for YASnippet user snippets.")
    ;; Create snippet directory if it doesn't exist
    (unless (file-directory-p my:yas-snippet-dir)
      (make-directory my:yas-snippet-dir t))
    :config
    (setq yas-snippet-dirs (list my:yas-snippet-dir))
    (yas-reload-all))

  (leaf yasnippet-snippets
    :ensure t
    :after yasnippet)
#+end_src

**** Completion Framework

- Completion Framework
  This section configures a *modern completion stack* built around =Vertico=, =Corfu=, and =Orderless=.
  It also integrates *Prescient* for persistent sorting, *Consult* for navigation, and *Embark* for context-aware actions.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
  ;;; Completion Frameworks
  ;; - Configures a modern completion stack: Vertico, Corfu, Orderless, etc.

  (leaf completion-settings
    :config
    ;; Prescient: persistent sorting & filtering
    (leaf prescient
      :ensure t
      :custom ((prescient-aggressive-file-save . t))
      :global-minor-mode prescient-persist-mode)

    ;; Vertico: vertical completion UI
    (leaf vertico
      :ensure t
      :global-minor-mode vertico-mode
      :custom ((vertico-count . 15))
      :config
      (leaf vertico-posframe
        :ensure t
        :if (display-graphic-p)
        :after vertico
        :require posframe
        :custom ((vertico-posframe-border-width . 2)
                 (vertico-posframe-parameters . '((left-fringe . 4) (right-fringe . 4))))
        :config (vertico-posframe-mode 1)))

    (leaf vertico-prescient
      :ensure t
      :after (vertico prescient)
      :global-minor-mode t)

    ;; Marginalia: add annotations to completion candidates
    (leaf marginalia
      :ensure t
      :global-minor-mode marginalia-mode)

    ;; Consult: powerful search & navigation
    (leaf consult
      :ensure t
      :custom
      ((xref-show-xrefs-function . #'consult-xref)
       (xref-show-definitions-function . #'consult-xref)))

    ;; Embark: context-sensitive actions
    (leaf embark
      :ensure t
      :custom
      ((prefix-help-command . #'embark-prefix-help-command)
       (embark-collect-live-update . t))
      :config
      ;; Enable live updates in Embark collect buffers.
      (add-hook 'embark-collect-mode-hook #'embark-collect-live-mode)
      (when (require 'all-the-icons nil t)
        (setq embark-indicators
              '(embark-minimal-indicator
                embark-highlight-indicator
                embark-isearch-highlight-indicator)))
      (leaf embark-consult
        :ensure t
        :after (embark consult)
        :hook (embark-collect-mode . consult-preview-at-point-mode)
        :custom (consult-preview-key . "M-.")))

    ;; Embark keybindings inside Vertico
    (defun my:setup-embark-vertico-directory ()
      "Integrate embark commands inside Vertico minibuffer."
      (when (and (boundp 'vertico-map) (require 'embark nil t))
        (define-key vertico-map (kbd "C-.") #'embark-act)
        (define-key vertico-map (kbd "C-;") #'embark-dwim)))

    (add-hook 'vertico-mode-hook #'my:setup-embark-vertico-directory)

    ;; Corfu: popup completions
    (leaf corfu
      :ensure t
      :init
      (global-corfu-mode)
      :custom
      ((corfu-auto . t)
       (corfu-auto-delay . 0)
       (corfu-auto-prefix . 2)
       (corfu-cycle . t))
      :config
      ;; Add icons to Corfu completions
      (leaf kind-icon
        :ensure t
        :after corfu
        :custom
        ((kind-icon-default-face . 'corfu-default))
        :config
        (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter)))

    ;; Cape: extra completion sources for Corfu
    (leaf cape
      :ensure t
      :init
      (mapc (lambda (fn) (add-to-list 'completion-at-point-functions fn))
            '(cape-file cape-dabbrev cape-keyword)))

    ;; Orderless: fuzzy matching
    (leaf orderless
      :ensure t
      :custom
      ((completion-styles . '(orderless basic))
       (completion-category-overrides . '((file (styles . (partial-completion)))))))

    ;; nerd-icons-{ibuffer,completion}
    (leaf nerd-icons-ibuffer
      :ensure t
      :hook (ibuffer-mode-hook . nerd-icons-ibuffer-mode))
    (leaf nerd-icons-completion
      :ensure t
      :hook (marginalia-mode-hook . nerd-icons-completion-marginalia-setup)
      :config
      (nerd-icons-completion-mode)))
#+end_src

**** EWW configuration

#+begin_src emacs-lisp :tangle lisp/README.el
  (leaf eww
    :ensure nil
    :custom ((eww-search-prefix . "https://duckduckgo.com/html/?kl=jp-jp&k1=-1&kc=1&kf=-1&q=")
  	   (eww-download-directory . "~/Downloads"))                ;; Download directory
    :config
    ;; Save history and bookmarks
    (setq eww-bookmarks-file (expand-file-name "eww-bookmarks" my:d:var))
    (setq eww-history-limit 200)

    ;; Variable to store search term
    (defvar eww-hl-search-word nil
      "Word to highlight and search with isearch after EWW loads.")

    ;; Custom search command
    (defun eww-search (term)
      "Search TERM with `eww' and start `isearch' with TERM."
      (interactive "sSearch terms: ")
      (setq eww-hl-search-word term)
      (eww-browse-url (concat eww-search-prefix term)))

    ;; After rendering, automatically start isearch with the search term
    (add-hook 'eww-after-render-hook
              (lambda ()
                (when eww-hl-search-word
                  (isearch-mode t)
                  (isearch-yank-string eww-hl-search-word)
                  (setq eww-hl-search-word nil))))

    ;; Toggle images on/off
    (defun my:eww-toggle-images ()
      "Toggle whether images are loaded in EWW."
      (interactive)
      (setq shr-inhibit-images (not shr-inhibit-images))
      (eww-reload)))
#+end_src

**** Programming Utilities

- LSP Configuration
  This configuration provides *Language Server Protocol (LSP)* support via two possible backends:
  The variable `my:use-lsp` determines which backend is active.
  - *Eglot* (default, lightweight).
  - *LSP-Mode* (feature-rich, with UI enhancements).
- Aidermacs (AI Integration)
  Aidermacs integrates AI-assisted development using OpenRouter or OpenAI APIs.
- Vterm
  Enables a fast, full-featured terminal emulator inside Emacs.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
  ;;; LSP Configuration (Eglot or LSP-Mode)
  ;; Provides Language Server Protocol (LSP) support for intelligent code features.
  ;; `my:use-lsp` determines which backend to use:
  ;; - `eglot` (default, lightweight)
  ;; - `lsp`   (LSP-Mode, feature-rich)

  (defvar my:use-lsp 'eglot
    "LSP backend selection. Use `eglot` (default) or `lsp`.")

  ;; -----------------------------------------------------------------------------
  ;;; Eglot (Lightweight LSP Client)
  ;; - Starts language servers automatically in `prog-mode`.
  ;; - Provides essential LSP features (rename, code actions, diagnostics).
  ;; - Uses Flymake for on-the-fly diagnostics.

  (when (eq my:use-lsp 'eglot)
    (leaf eglot
      :hook (prog-mode . eglot-ensure)
      :custom
      `((eglot-autoshutdown . t)      ;; Stop servers when not in use
        (eglot-sync-connect . nil)    ;; Connect asynchronously
        (eglot-events-buffer-size . 200))
      :bind (:eglot-mode-map
             ("C-c h" . eglot-help-at-point)
             ("C-c r" . eglot-rename)
             ("C-c a" . eglot-code-actions)
             ("C-c d" . flymake-show-buffer-diagnostics))))

  ;; -----------------------------------------------------------------------------
  ;;; LSP-Mode (Full-Featured LSP Client)
  ;; - Activated when `my:use-lsp` is set to `lsp`.
  ;; - Includes advanced features such as:
  ;;   - Breadcrumb navigation
  ;;   - Extensive diagnostics
  ;;   - Enhanced completion

  (when (eq my:use-lsp 'lsp)
    (leaf lsp-mode
      :ensure t
      :hook ((python-mode      . lsp)
             (rust-mode        . lsp)
             (go-mode          . lsp)
             (js-mode          . lsp)
             (typescript-mode  . lsp)
             (c-mode           . lsp)
             (c++-mode         . lsp))
      :custom
      `((lsp-enable-snippet . t)            ;; Enable snippet completion
        (lsp-idle-delay . 0.5)              ;; Delay before LSP actions
        (lsp-headerline-breadcrumb-enable . t)
        (lsp-prefer-flymake . nil))         ;; Use Flycheck instead of Flymake
      :config
      (setq lsp-completion-provider :capf)))

  ;; -----------------------------------------------------------------------------
  ;;; LSP UI Enhancements
  ;; - Adds inline documentation, diagnostics, and code action hints.
  ;; - Works only when using LSP-Mode.

  (when (eq my:use-lsp 'lsp)
    (leaf lsp-ui
      :ensure t
      :after lsp-mode
      :custom
      `((lsp-ui-doc-enable . t)
        (lsp-ui-sideline-enable . t)
        (lsp-ui-sideline-show-hover . t)
        (lsp-ui-sideline-show-code-actions . t)
        (lsp-ui-sideline-show-diagnostics . t))))

  ;; -----------------------------------------------------------------------------
  ;; Aidermacs configuration

  (leaf aidermacs
    :ensure t
    :vc (:url "https://github.com/MatthewZMD/aidermacs.git" :branch "main")
    :init
    ;; Prefer OpenRouter when available; fallback to OpenAI.
    (cond
     ((getenv "OPENROUTER_API_KEY")
      (setenv "OPENAI_API_BASE" "https://openrouter.ai/api/v1")
      (setenv "OPENAI_API_KEY"  (getenv "OPENROUTER_API_KEY"))
      (setopt aidermacs-default-model "openrouter/anthropic/claude-3.5-sonnet"))
     ((getenv "OPENAI_API_KEY")
      (setenv "OPENAI_API_BASE" "https://api.openai.com/v1")
      (setopt aidermacs-default-model "gpt-4o-mini"))
     (t
      (display-warning 'aidermacs
                       "No API keys set. Set OPENROUTER_API_KEY or OPENAI_API_KEY.")))
    (setopt aidermacs-retry-attempts 3
            aidermacs-retry-delay   2.0
            aidermacs-backend       'vterm
            aidermacs-vterm-use-theme-colors nil))

  ;; -----------------------------------------------------------------------------
  ;;; Vterm

  (leaf vterm :ensure t)
#+end_src

*** Org-mode
**** Org-mode Core Setup

- Org Mode Configuration
  This section configures *Org mode* for a GTD-style workflow with tasks, notes, agendas, and capture templates.
- Org Modern Styling
  Improves Org mode visuals with cleaner headings, ellipsis, and agenda tweaks.
- Org Superstar (Pretty Headings)
  Enhances Org headlines by replacing the default asterisks with a set of Unicode symbols.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
  ;;; Org Mode Configuration
  ;; Provides a GTD-style workflow with notes, tasks, agendas, and capture templates.

  (leaf org
    :ensure nil
    :leaf-defer t
    :preface
    ;; Org directory setup
    (defvar warning-suppress-types nil)
    (unless (boundp 'my:d:cloud)
      (setq my:d:cloud (concat no-littering-var-directory "./")))

    ;; Utility: List all open Org files
    (defun org-buffer-files ()
      "Return a list of currently open Org files."
      (delq nil
            (mapcar #'buffer-file-name (org-buffer-list 'files))))

    ;; Utility: Show a specific Org file in current buffer
    (defun show-org-buffer (file)
      "Display an Org FILE from `org-directory`."
      (interactive (list (read-file-name "Org file: " org-directory nil t)))
      (let ((filepath (expand-file-name file org-directory)))
        (if (get-file-buffer filepath)
            (switch-to-buffer (get-file-buffer filepath))
          (find-file filepath))))

    :custom ((org-support-shift-select . t))
    :init
    ;; Org directory
    (setq org-directory (expand-file-name "org/" my:d:cloud))
    (my:ensure-directory-exists org-directory)

    ;; Link & cache settings
    (setq org-return-follows-link t
          org-mouse-1-follows-link t
          warning-suppress-types (append warning-suppress-types '((org-element-cache)))
          org-element-use-cache nil)

    ;; PDF export (LaTeX)
    (setq org-latex-pdf-process
          '("pdflatex -interaction nonstopmode -output-directory %o %f"
            "pdflatex -interaction nonstopmode -output-directory %o %f"))

    ;; Key bindings for quick access to major Org files
    :bind
    (("C-M--" . (lambda () (interactive) (show-org-buffer "gtd.org")))
     ("C-M-^" . (lambda () (interactive) (show-org-buffer "notes.org")))
     ("C-M-~" . (lambda () (interactive) (show-org-buffer "kb.org"))))

    :config
    ;; General Org settings
    (setq org-agenda-files (list org-directory)
          org-cycle-emulate-tab 'white-space
          org-default-notes-file "notes.org"
          org-enforce-todo-dependencies t
          org-idle-time 0.3
          org-log-done 'time
          org-startup-folded 'content
          org-startup-truncated nil
          org-use-speed-commands t
          org-link-frame-setup '((file . find-file)))

    ;; Agenda files (exclude archives)
    (setq org-agenda-files
          (seq-filter (lambda (file)
                        (not (string-match-p "archives" file)))
                      (directory-files-recursively org-directory "\\.org$")))

    ;; TODO keywords
    (setq org-todo-keywords
          '((sequence "TODO(t)" "SOMEDAY(s)" "WAITING(w)" "|" "DONE(d)" "CANCELED(c@)")))

    ;; Refile targets
    (setq org-refile-targets
          '((nil :maxlevel . 3)
            (org-buffer-files :maxlevel . 1)
            (org-agenda-files :maxlevel . 3)))

    ;; Capture templates
    (setq org-capture-templates
          `(("t" "Todo" entry (file+headline ,(expand-file-name "gtd.org" org-directory) "Inbox")
             "* TODO %?\n %i\n %a")
            ("n" "Note" entry (file+headline ,(expand-file-name "notes.org" org-directory) "Notes")
             "* %?\nEntered on %U\n %i\n %a")
            ("j" "Journal" entry (function org-journal-find-location)
             "* %(format-time-string org-journal-time-format)%^{Title}\n%i%?")
            ("m" "Meeting" entry (file ,(expand-file-name "meetings.org" org-directory))
             "* MEETING with %? :meeting:\n  %U\n  %a"))))

  ;; -----------------------------------------------------------------------------
  ;;; Org Modern Styling
  ;; Improves Org visual style with cleaner headings, ellipsis, and agenda tweaks.

  (leaf org-modern
    :config
    (setopt
     org-startup-indented t
     org-hide-leading-stars t
     org-auto-align-tags nil
     org-tags-column 0
     org-catch-invisible-edits 'show-and-error
     org-special-ctrl-a/e t
     org-insert-heading-respect-content t
     org-hide-emphasis-markers t
     org-pretty-entities t
     org-agenda-tags-column 0
     org-agenda-block-separator ?─
     org-agenda-time-grid
     '((daily today require-timed)
       (800 1000 1200 1400 1600 1800 2000)
       " ┄┄┄┄┄ " "┄┄┄┄┄┄┄┄┄┄┄┄┄┄┄")
     org-agenda-current-time-string
     "◀── now ─────────────────────────────────────────────────")
    (setopt org-ellipsis " ▾")
    (set-face-attribute 'org-ellipsis nil :inherit 'default :box nil))

  ;; -----------------------------------------------------------------------------
  ;;; Org Superstar (Pretty Headings)
  ;; Enhances the visual appearance of Org headlines by replacing the default
  ;; asterisks with a set of Unicode symbols.

  (leaf org-superstar
    :after org
    :custom
    ;; Custom bullet symbols for different heading levels
    (org-superstar-headline-bullets-list . '("◉" "★" "○" "▷"))
    ;; Keep leading stars (set to `t` to remove them completely)
    (org-superstar-remove-leading-stars . nil)
    :hook
    ;; Enable `org-superstar-mode` automatically for Org buffers
    (org-mode . org-superstar-mode))
#+end_src

**** Additional Org-related packages

- Org Babel (Code Execution in Org)
  Enables execution of code blocks in multiple languages, including Emacs Lisp, shell, Python, R, Ditaa, and PlantUML.
- Org Journal
  Daily journaling with seamless integration into the agenda.
- Org Roam (Networked Note-Taking)
  Org Roam provides a personal knowledge base with backlinks and graph-based navigation.
- Org Download (Image Management)
  Enables drag-and-drop and clipboard-based image insertion into Org files. Images are stored in a =pictures= subdirectory.
- TOC-Org (Table of Contents)
  Automatically generates and updates tables of contents for Org and Markdown files.
- Org Cliplink (Insert Clickable Links)
  Fetches webpage titles and inserts properly formatted Org links.
- Org LaTeX Export Configuration
  Defines common LaTeX packages and sets up a multi-pass =pdflatex= pipeline with BibTeX support.
- Org Export to Hugo
  Supports exporting Org content to Hugo-compatible Markdown.
- Markdown Mode
  Enables =markdown-mode= for editing =.md= files.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
  ;;; Org Babel (Code Execution in Org)
  ;; Enables execution of code blocks in multiple languages.

  (leaf ob
    :after org
    :defun org-babel-do-load-languages
    :config
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (shell . t)
       (python . t)
       (R . t)
       (ditaa . t)
       (plantuml . t))))

  ;; -----------------------------------------------------------------------------
  ;;; Org Journal
  ;; Daily journaling with agenda integration.

  (leaf org-journal
    :ensure t
    :after org
    :config
    (setq org-journal-dir (concat org-directory "/journal")
          org-journal-enable-agenda-integration t)
    (defun org-journal-find-location ()
      "Open today's journal entry."
      (org-journal-new-entry t)))

  ;; -----------------------------------------------------------------------------
  ;;; Org Roam (Networked Note-Taking)
  ;; A personal knowledge base with backlinks and a graph-based view.

  (leaf org-roam
    :ensure t
    :after org
    :config
    (setq org-roam-directory (concat org-directory "/org-roam"))
    (unless (file-directory-p org-roam-directory)
      (make-directory org-roam-directory t))
    (org-roam-db-autosync-mode))

  ;; -----------------------------------------------------------------------------
  ;;; Org Download (Image Management)
  ;; Enables drag-and-drop or clipboard-based image insertion into Org files.
  ;; Images are stored in an "pictures" directory under `org-directory`.

  (leaf org-download
    :ensure t
    :after org
    :config
    (setq org-download-image-dir (expand-file-name "pictures" org-directory))
    (unless (file-directory-p org-download-image-dir)
      (make-directory org-download-image-dir t)))

  ;; -----------------------------------------------------------------------------
  ;;; TOC-Org (Table of Contents)
  ;; Automatically generates and updates tables of contents for Org and Markdown.

  (leaf toc-org
    :ensure t
    :after org markdown-mode
    :config
    (add-hook 'org-mode-hook 'toc-org-enable)
    (add-hook 'markdown-mode-hook 'toc-org-mode))

  ;; -----------------------------------------------------------------------------
  ;;; Org Cliplink (Insert Clickable Links)
  ;; Fetches the title of a webpage and inserts a properly formatted Org link.

  (leaf org-cliplink
    :ensure t
    :after org
    :bind ("C-x p i" . org-cliplink))

  ;; -----------------------------------------------------------------------------
  ;;; Org LaTeX Export Configuration
  ;; Adds common LaTeX packages and defines a multi-pass `pdflatex` build pipeline
  ;; with BibTeX integration for high-quality PDF exports.

  (leaf org-latex
    :after org
    :custom
    (org-latex-packages-alist
     '(("" "graphicx" t)
       ("" "longtable" nil)
       ("" "wrapfig" nil)))
    (setq org-latex-pdf-process
          '("pdflatex -interaction nonstopmode -output-directory %o %f"
            "bibtex %b"
            "pdflatex -interaction nonstopmode -output-directory %o %f"
            "pdflatex -interaction nonstopmode -output-directory %o %f")))

  ;; -----------------------------------------------------------------------------
  ;;; Org Export to Hugo (Static Site Generation)
  ;; Exports Org content to the Hugo static site generator format.

  (leaf ox-hugo
    :ensure t
    :require t
    :after ox
    :custom ((org-hugo-front-matter-format . "toml")))

  ;; -----------------------------------------------------------------------------
  ;;; Hugo Blog Capture Template
  ;; Adds an Org-Capture template for quickly creating new Hugo blog posts.

  (leaf *ox-hugo--capture
    :require org-capture
    :defvar (org-capture-templates)
    :config
    (defun generate-safe-filename ()
      "Generate a unique, safe filename for Hugo export."
      (format "%s-%s" (format-time-string "%Y")
              (string-trim (shell-command-to-string "uuidgen | cut -c1-8"))))
    (add-to-list 'org-capture-templates
                 '("b" "Create new blog post" entry
                   (file+headline my:f:capture-blog-file "blog")
                   "** TODO %?\n  :PROPERTIES:\n  :EXPORT_FILE_NAME: %(generate-safe-filename)\n  :EXPORT_DATE:\n  :EXPORT_HUGO_TAGS:\n  :EXPORT_HUGO_CATEGORIES:\n  :EXPORT_HUGO_LASTMOD:\n  :EXPORT_HUGO_CUSTOM_FRONT_MATTER: :pin false\n  :END:\n\n")))

  ;; -----------------------------------------------------------------------------
  ;;; Markdown Mode
  ;; Enables `markdown-mode` for `.md` files.

  (leaf markdown-mode
    :ensure t
    :mode ("\\.md\\'" . markdown-mode))
#+end_src

*** Utilities Package
**** Extra Utilities

- AUCTeX (LaTeX Editing)
  This section configures *AUCTeX* for advanced LaTeX editing with PDF workflows.
  It uses =latexmk= for automated compilation and integrates with =synctex= for forward/inverse search.
- Authentication Management
  This section handles secure authentication and credential management using =auth-source=, =pass=, and GPG.
  It validates environment variables, integrates with =auth-source-pass=, and configures GPG encryption.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
  ;;; AUCTeX (LaTeX Editing)
  ;; Configures AUCTeX for PDF-based workflows with `latexmk`.

  (leaf auctex
    :ensure t
    :init
    (setq TeX-auto-save t
          TeX-parse-self t
          TeX-save-query nil
          TeX-PDF-mode t)
    (setq-default TeX-master nil)
    :config
    (setq TeX-command-default "LatexMk")
    (add-hook 'LaTeX-mode-hook
              (lambda ()
                (push
                 '("LatexMk" "latexmk -pdf -interaction=nonstopmode -synctex=1 %s"
                   TeX-run-TeX nil t :help "Run latexmk for automated PDF generation")
                 TeX-command-list))))

  ;; -----------------------------------------------------------------------------
  ;;; Authentication Management
  ;; Secure credential management using `auth-source`, `pass`, and GPG.

  (leaf *authentication
    :init
    (defvar my:d:password-store
      (or (getenv "PASSWORD_STORE_DIR")
          (concat no-littering-var-directory "password-store/"))
      "Path to the password store.")

    ;; Validate environment variables
    (unless (getenv "GPG_KEY_ID")
      (warn "GPG_KEY_ID is not set. Authentication features may not work properly."))
    (unless (file-directory-p my:d:password-store)
      (warn "Password store directory does not exist: %s" my:d:password-store))

    ;; GPG & auth-source
    (leaf epa-file
      :config
      (epa-file-enable)
      (setq epa-pinentry-mode
            (if (getenv "USE_GPG_LOOPBACK") 'loopback 'default)))

    (leaf auth-source
      :config
      (setq auth-source-gpg-encrypt-to
            (or (getenv "GPG_KEY_ID")
                (user-error "GPG_KEY_ID is not set. Authentication will not work."))))

    ;; Password-store and auth-source-pass
    (leaf password-store :ensure t)
    (leaf auth-source-pass
      :ensure t
      :config
      (when (executable-find "pass")
        (auth-source-pass-enable)))

    ;; Secure plstore
    (leaf plstore
      :config
      (setq plstore-secret-keys 'silent
            plstore-encrypt-to (getenv "GPG_KEY_ID"))))
#+end_src

**** Miscellaneous Helper Functions

- Scratch Buffer Management
  Ensures that the =*scratch*= buffer always exists.
  Provides commands to recreate or maintain the scratch buffer even after it is closed.
- Automatic Lexical Binding
  Automatically inserts a =lexical-binding: t= header into =.el= files located under =no-littering-var-directory=.
- Asynchronous Task Execution Helper
  Utility function to safely run tasks asynchronously with error handling.
- Backup File Cleanup
  Automatically deletes old backup files (older than 7 days) in the backup directory.
- Read-Only Buffer Handling
  Enables =view-mode= automatically for read-only buffers.
- UI & Navigation Helpers
  Includes helper functions for line numbers, window splitting, and finding conflicting keybindings.
- Dired Helper
  Adds a helper command to open Dired files in another window.
- External Integration
  Commands to interact with external tools like Visual Studio Code, environment variables, and Emacs build info.
- Org Mode Folding Shortcuts
  Defines custom keybindings for folding and unfolding Org subtrees.
- Hooks
  Various hooks for startup, file opening, saving, and mode-specific behaviors.

#+begin_src emacs-lisp :tangle lisp/README.el
  ;;; ---------------------------------------------------------------------------
  ;;; Utility Functions

  (defun my:auto-tangle-updated-src-blocks ()
    "Automatically tangle updated Org source blocks when saving `README.org`."
    (when (and buffer-file-name
               (string= (file-name-nondirectory buffer-file-name) "README.org"))
      (let ((org-confirm-babel-evaluate nil))
        (org-babel-tangle))))

  ;; -----------------------------------------------------------------------------
  ;;; Scratch Buffer Management
  ;; Ensures that the `*scratch*` buffer always exists, and allows recreation.

  (defun my:create-scratch-buffer ()
    "Ensure that a `*scratch*` buffer exists."
    (unless (get-buffer "*scratch*")
      (with-current-buffer (get-buffer-create "*scratch*")
        (funcall initial-major-mode)
        (when (and initial-scratch-message
                   (not (string-empty-p initial-scratch-message)))
          (insert initial-scratch-message))
        (current-buffer))))

  (defun my:recreate-scratch-buffer ()
    "Kill and recreate the `*scratch*` buffer."
    (interactive)
    (when (get-buffer "*scratch*")
      (kill-buffer "*scratch*"))
    (my:create-scratch-buffer)
    (switch-to-buffer "*scratch*"))

  (defun my:after-kill-buffer-advice (&rest _)
    "Ensure `*scratch*` buffer exists after any buffer is killed."
    (run-at-time 0.1 nil #'my:create-scratch-buffer))

  (advice-add 'kill-buffer :after #'my:after-kill-buffer-advice)

  ;; -----------------------------------------------------------------------------
  ;;; Automatic Lexical Binding
  ;; Inserts a `lexical-binding: t` header into `.el` files in `no-littering-var-directory`.

  (defun my:auto-insert-lexical-binding ()
    "Automatically insert `lexical-binding: t` in Emacs Lisp files under `no-littering-var-directory`."
    (when (and (stringp buffer-file-name)
               (boundp 'no-littering-var-directory)
               (string-prefix-p (expand-file-name no-littering-var-directory)
                                (expand-file-name buffer-file-name))
               (string-match-p "\\.el\\'" buffer-file-name)
               (not (save-excursion
                      (goto-char (point-min))
                      (re-search-forward "lexical-binding" (line-end-position 5) t))))
      (save-excursion
        (goto-char (point-min))
        (insert ";; -*- lexical-binding: t; -*- \n"))))

  ;; -----------------------------------------------------------------------------
  ;;; Asynchronous Task Execution Helper

  (defun my:safe-run-async (task)
    "Run TASK asynchronously, catching and reporting any errors."
    (run-at-time 0 nil
                 (lambda ()
                   (condition-case err
                       (funcall task)
                     (error (message "Async error: %s" err))))))

  ;; -----------------------------------------------------------------------------
  ;;; Backup File Cleanup
  ;; Deletes old backup files (older than 7 days) asynchronously.

  (defun my:delete-old-backups ()
    "Delete backup files older than 7 days."
    (interactive)
    (my:safe-run-async
     (lambda ()
       (let ((backup-dir (concat no-littering-var-directory "backup/"))
             (threshold (- (float-time (current-time)) (* 7 24 60 60))))
         (when (file-directory-p backup-dir)
           (dolist (file (directory-files backup-dir t))
             (when (and (file-regular-p file)
                        (< (float-time (file-attribute-modification-time
                                        (file-attributes file)))
                           threshold))
               (delete-file file))))))))

  ;; -----------------------------------------------------------------------------
  ;;; Read-Only Buffer Handling
  ;; Automatically enables `view-mode` for read-only buffers.

  (defun my:enable-view-mode-on-read-only ()
    "Enable `view-mode` when buffer is read-only."
    (if buffer-read-only
        (view-mode 1)
      (view-mode -1)))
  (add-hook 'read-only-mode-hook #'my:enable-view-mode-on-read-only)

  ;; -----------------------------------------------------------------------------
  ;;; UI & Navigation Helpers

  (defun my:toggle-linum-lines ()
    "Toggle line numbers using `display-line-numbers-mode`."
    (interactive)
    (display-line-numbers-mode 'toggle))

  (defun my:toggle-window-split ()
    "Toggle between horizontal and vertical split for two windows."
    (interactive)
    (when (= (count-windows) 2)
      (let* ((this-buf (window-buffer))
             (next-buf (window-buffer (next-window)))
             (this-edges (window-edges))
             (next-edges (window-edges (next-window)))
             (split-vert (= (car this-edges) (car next-edges)))
             (split-fn (if split-vert
                           #'split-window-horizontally
                         #'split-window-vertically)))
        (delete-other-windows)
        (funcall split-fn)
        (set-window-buffer (selected-window) this-buf)
        (set-window-buffer (next-window) next-buf)
        (select-window (selected-window)))))

  (defun my:find-keybinding-conflicts ()
    "Find and display conflicting keybindings across active keymaps."
    (interactive)
    (let ((conflicts (make-hash-table :test 'equal))
          (buffer-name "*Keybinding Conflicts*"))
      (mapatoms (lambda (sym)
                  (when (and (boundp sym) (keymapp (symbol-value sym)))
                    (map-keymap
                     (lambda (key cmd)
                       (when (commandp cmd)
                         (let ((desc (key-description (vector key)))
                               (existing (gethash desc conflicts)))
                           (puthash desc (delete-dups (cons cmd existing))
                                    conflicts))))
                     (symbol-value sym)))))
      (with-current-buffer (get-buffer-create buffer-name)
        (read-only-mode -1)
        (erase-buffer)
        (insert "* Keybinding Conflicts *\n\n")
        (maphash (lambda (key cmds)
                   (when (> (length cmds) 1)
                     (insert (format "%s => %s\n"
                                     key
                                     (mapconcat #'symbol-name cmds ", ")))))
                 conflicts)
        (read-only-mode 1))
      (switch-to-buffer buffer-name)))

  ;; -----------------------------------------------------------------------------
  ;;; Dired Helper

  (defun my:dired-view-file-other-window ()
    "Open selected Dired file or directory in another window."
    (interactive)
    (let ((file (dired-get-file-for-visit)))
      (if (file-directory-p file)
          (or (and (cdr dired-subdir-alist)
                   (dired-goto-subdir file))
              (dired file))
        (view-file-other-window file))))

  ;; -----------------------------------------------------------------------------
  ;;; External Integration

  (defun my:open-by-vscode ()
    "Open current file in Visual Studio Code at line/column."
    (interactive)
    (when (buffer-file-name)
      (async-shell-command
       (format "code -r -g %s:%d:%d"
               (buffer-file-name)
               (line-number-at-pos)
               (current-column)))))

  (defun my:show-env-variable (var)
    "Display the value of environment variable VAR."
    (interactive "sEnvironment variable: ")
    (let ((val (getenv var)))
      (message "%s = %s" var (or val "Not set"))))

  (defun my:print-build-info ()
    "Display Emacs build details (commit, branch, system, features, options)."
    (interactive)
    (let ((buf (get-buffer-create "*Build Info*")))
      (with-current-buffer buf
        (let ((inhibit-read-only t))
          (erase-buffer)
          ;; Core info
          (insert (format "- GNU Emacs *%s*\n\n" emacs-version))
          (insert "|Property|Value|\n|--------|-----|\n")
          (insert (format "|Commit|%s|\n" (emacs-repository-get-version)))
          (insert (format "|Branch|%s|\n" (emacs-repository-get-branch)))
          (insert (format "|System|%s|\n" system-configuration))
          (insert (format "|Date|%s|\n"
                          (format-time-string "%Y-%m-%d %T (%Z)" emacs-build-time)))
          ;; Patch detection
          (insert (format "|Patch|%s ns-inline.patch|\n"
                          (cond
                           ((boundp 'mac-ime--cursor-type) "with")
                           (t "N/A"))))
          ;; Features & options
          (insert (format "|Features|%s|\n" system-configuration-features))
          (insert (format "|Options|%s|\n" system-configuration-options)))
        (view-mode 1))
      (switch-to-buffer buf)))

  ;; -----------------------------------------------------------------------------
  ;;; Org Mode Folding Shortcuts
  (with-eval-after-load 'org
    (require 'org-fold)
    (defun my-org-fold-subtree ()   (interactive) (org-fold-subtree t))
    (defun my-org-unfold-subtree () (interactive) (org-show-subtree))
    (defun my-org-toggle-fold ()
      "Toggle fold for current Org subtree."
      (interactive)
      (save-excursion
        (org-back-to-heading t)
        (if (org-fold-folded-p (point))
            (org-show-subtree)
          (org-fold-subtree t))))
    (define-key org-mode-map (kbd "C-c C-f") #'my-org-fold-subtree)
    (define-key org-mode-map (kbd "C-c C-e") #'my-org-unfold-subtree)
    (define-key org-mode-map (kbd "C-c C-t") #'my-org-toggle-fold))

  ;; -----------------------------------------------------------------------------
  ;;; Hooks

  (add-hook 'org-mode-hook
            (lambda ()
              (add-hook 'after-save-hook #'my:auto-tangle-updated-src-blocks
                        nil 'make-it-local)))
  (add-hook 'emacs-startup-hook #'my:delete-old-backups)
  (add-hook 'find-file-hook #'my:auto-insert-lexical-binding)
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
  (add-hook 'prog-mode-hook 'goto-address-prog-mode)
  (add-hook 'text-mode-hook 'goto-address-mode)
#+end_src

***** Key Bindings

- Hydra for Text Scaling
  Hydra provides a quick, transient keymap for text scaling.
  This hydra allows increasing, decreasing, or resetting the font size.
- Common Key Bindings
  This section defines frequently used keybindings for:
  - *Navigation* (buffers, windows)
  - *File operations*
  - *Text editing* (scaling, commenting, alignment)
  - *Search* (consult, ripgrep)
  - *Org mode* (agenda, capture, roam)
  - *Git* (magit)
  - *Miscellaneous* (restart, execute commands)

#+begin_src emacs-lisp :tangle lisp/README.el
  ;; -----------------------------------------------------------------------------
  ;;; Hydra for Text Scaling
  ;; Provides quick keybindings to increase, decrease, or reset text size.

  (defhydra hydra-text-scale (:hint nil :color red)
    "
  ^Text Scaling^
  ----------------------------
  [_+_] Increase   [_-_] Decrease   [_0_] Reset
  "
    ("+" text-scale-increase)
    ("-" text-scale-decrease)
    ("0" (text-scale-set 0) :color blue)
    ("q" nil "quit" :color blue))

  ;; -----------------------------------------------------------------------------
  ;;; Common Key Bindings

  ;; Centralized keybindings with proper load order.
  (leaf my:keys
    :doc "Centralized keybindings via `leaf-keys`, ordered by map lifetime."
    :emacs>= 30.0
    :init
    ;; Global keys that depend on packages: bind after the package is ready
    (with-eval-after-load 'winner
      (leaf-keys
       (global-map
        ("M-[" . winner-undo)
        ("M-]" . winner-redo))))

    ;; Mode-local keys: bind after the mode package loads
    (with-eval-after-load 'treemacs
      (leaf-keys
       (treemacs-mode-map
        ([mouse-1] . treemacs-single-click-expand-action))))

    ;; Dired and its extensions
    (with-eval-after-load 'dired
      (leaf-keys
       (dired-mode-map
        ("i"   . dired-subtree-insert)
        ("TAB" . dired-subtree-toggle))))

    ;; Global key bindings
    (leaf-keys
     ;; Function keys and help
     (("<f1>"    . help)
      ("<f5>"    . revert-buffer-quick)
      ("<f8>"    . treemacs)
      ("C-h"     . backward-delete-char)

      ;; Undo/redo
      ("C-/"     . undo-fu-only-undo)
      ("C-?"     . undo-fu-only-redo)

      ;; Text scaling
      ("C-+"     . text-scale-increase)
      ("C--"     . text-scale-decrease)
      ("C-0"     . (lambda () (interactive) (text-scale-set 0)))
      ("C-c z"   . hydra-text-scale/body)

      ;; Buffer navigation
      ("C-c b"   . consult-buffer)
      ("M-n"     . forward-paragraph)
      ("M-p"     . backward-paragraph)
      ("s-<down>". end-of-buffer)
      ("s-<up>"  . beginning-of-buffer)
      ("s-<right>" . next-buffer)
      ("s-<left>"  . previous-buffer)

      ;; Window management
      ("C-."     . other-window)
      ("C-c 2"   . my:toggle-window-split)
      ("s-."     . ace-window)
      ("s-w"     . ace-swap-window)
      ("s-d"     . delete-frame)
      ("s-m"     . (lambda () (interactive)
                     (let ((frame (make-frame)))
                       (with-selected-frame frame
                         (switch-to-buffer (generate-new-buffer "untitled"))))))

      ;; File operations
      ("s-j"     . find-file-other-window)
      ("s-o"     . find-file-other-frame)
      ("C-c o"   . find-file)
      ("C-c v"   . find-file-read-only)
      ("C-c V"   . view-file-other-window)
      ("C-c k"   . kill-buffer-and-window)

      ;; Search
      ("C-s"     . consult-line)
      ("C-c r"   . consult-ripgrep)

      ;; Text manipulation
      ("C-="     . er/expand-region)
      ("C-c M-a" . align-regexp)
      ("C-c ;"   . comment-or-uncomment-region)
      ("C-c l"   . display-line-numbers-mode)

      ;; Org mode & Roam
      ("C-c d a" . org-agenda)
      ("C-c d c" . org-capture)
      ("C-c d i" . org-roam-node-insert)
      ("C-c d f" . org-roam-node-find)

      ;; Aider
      ("C-c a a" . aidermacs-transient-menu)

      ;; EWW (global bindings for browsing)
      ("C-c w w" . eww)                ;; Open EWW (prompt URL/search)
      ("C-c w s" . eww-search)         ;; Search + start isearch
      ("C-c w o" . eww-open-file)      ;; Open local HTML
      ("C-c w b" . eww-list-bookmarks) ;; Bookmarks
      ("C-c w r" . eww-readable)       ;; Readable mode
      ("C-c w u" . eww-back-url)       ;; Back
      ("C-c w f" . eww-forward-url)    ;; Forward
      ("C-c w I" . my:eww-toggle-images) ;; Toggle Image

      ;; Misc
      ("C-x g"   . magit-status)
      ("s-r"     . restart-emacs)
      ("M-x"     . execute-extended-command)))

    ;; Enable directional window navigation with Shift + arrow keys.
    (windmove-default-keybindings)

    ;; -----------------------------------------------------------------------------
    ;; Dired Enhancements
    ;; Adds a custom `z` key in Dired to open files in another window.

    (add-hook 'dired-mode-hook
              (lambda ()
                (define-key dired-mode-map "z"
  			  'my:dired-view-file-other-window)))
    )
#+end_src

*** Footer
#+begin_src emacs-lisp :tangle lisp/README.el
  (provide 'README)
  ;;; README.el ends here
#+end_src
