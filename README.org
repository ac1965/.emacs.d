# -*- mode: org; coding: utf-8; -*-

#+TITLE: Modern Emacs Configuration
#+AUTHOR: YAMASHITA, Takao
#+EMAIL: tjy1965@gmail.com
#+LANGUAGE: en
#+OPTIONS: toc:3 num:t
#+STARTUP: overview
#+PROPERTY: header-args :results silent :exports code :mkdirp yes :padline no :tangle no
#+PROPERTY: header-args:emacs-lisp :lexical t :noweb no-export

* Introduction
:PROPERTIES:
  :CUSTOM_ID: introduction
  :END:

A modern, literate Emacs configuration using Org Mode's Babel format, emphasizing performance, language server integration, AI assistance, and productivity.

** Features
:PROPERTIES:
:CUSTOM_ID: features
:END:

- *Performance & Native Compilation* — Early-init moves ELN under `.cache/`, silences async warnings, widens GC at startup and restores sane values later, and uses GCMH for idle GC.
- *Language Server Protocol* — Backend-agnostic helpers in `core/general.el`; choose *Eglot* or *lsp-mode* via `core/switches.el` with presence checks and auto-enable logic.
- *AI Integration* — Aidermacs (vterm backend). Prefers OpenRouter when `OPENROUTER_API_KEY` is set; otherwise uses OpenAI with `OPENAI_API_KEY`.
- *Modern UI & Editing* — Tree-sitter remaps (`*-ts-mode`), ef-themes + spacious-padding, Nerd Icons, Vertico/Orderless/Corfu/CAPE/Embark/Consult, Doom/Nano modeline switchers.
- *Productivity Tools* — Opinionated Org stack (agenda, capture, journal, roam, download, TOC), Magit + diff-hl/Forge, REST client, Docker/dev helpers, tidy backups/autosave-visited.

[[file:demo.png]]

** Coding Rules
:PROPERTIES:
:CUSTOM_ID: conventions
:END:

*- `lexical-binding: t` is *mandatory*. The provided feature *must match the file name* (e.g., `lisp/core.el` → `(provide 'core)`).
- Follow *official documentation* only. Do *not* rely on undocumented features or future/unstable behavior.
- In every *leaf* block, group settings under `:straight`, `:bind`, `:hook`, and `:custom`.
- For built-in packages, *always* specify `:straight nil`.

*** Header template
#+begin_src emacs-lisp
  ;;; module.el --- description -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;;; Commentary:
  ;; Short explanation about what this module does.
  ;; If it belongs to a category (core, ui, completion, org, dev, vcs, utils),
  ;; mention it here as well.
  ;;
  ;;; Code:

  ... code ...

  (provide 'module)
  ;;; module.el ends here
#+end_src

*** Namespaces and naming

- Module prefixes: `core-...`, `ui-...`, `completion-...`, `orgx-...`, `dev-...`, `vcs-...`, `utils-...`
- Personal namespace: `my/...`
- *Internal-only* functions/variables use a *double hyphen* `--` (e.g., `core--restore-startup-state`)
- *Predicates* end with `-p` (e.g., `core-enabled-p`)
- *Hook helpers* should make their purpose clear: `PREFIX--on-...` or `PREFIX-...-hook-function`
- *Keymaps* end with `-map`, and *hook variables* end with `-hook`
- Add `;;;###autoload` *only* to public commands (never to internal `--` functions).

** Installation
:PROPERTIES:
   :CUSTOM_ID: installation
   :END:

*** Prerequisites
:PROPERTIES:
:CUSTOM_ID: prerequisites
:END:

- **Required**
  - Emacs *30.0+* with native compilation (`--with-native-compilation`)
  - Git
  - GNU Make
  - GCC *10+* with `libgccjit`

- **Optional but Recommended**
  - ripgrep (`rg`) → faster project-wide search
  - aspell or hunspell → spell checking
  - pass + GnuPG → password and auth-source integration
  - Homebrew (macOS only) → for consistent toolchain installation

*** Building Emacs

Use the provided build script:
[[https://github.com/ac1965/dotfiles/blob/master/.local/bin/build-emacs.sh][build-emacs.sh]]

#+begin_src shell
  build-emacs.sh --native-compilation
#+end_src

*** Quick Start

1. Clone the repository:
   #+begin_src shell
     git clone --depth 1 https://github.com/ac1965/.emacs.d ~/.emacs.d
   #+end_src

2. Tangle configuration:
   #+begin_src shell
     EMACS=/Applications/Emacs.app/Contents/MacOS/Emacs make -C ~/.emacs.d/
   #+end_src

*** Makefile

#+begin_src text :tangle Makefile :comments no
  # Makefile for Emacs config build (portable / robust)
  # - One-shot `make all`: tangle -> byte-compile (sequential)
  # - Resolves EMACSD / STRAIGHT_BASE_DIR from early-init.el when available
  # - No build/ dir required
  # - Space-safe, BSD/macOS/GNU friendly

  SHELL := /bin/sh

  EMACS  ?= emacs
  ORG    ?= README.org
  EARLY  ?= early-init.el
  INIT   ?= init.el

  # Project directories (relative to repo root)
  LISPDIR     ?= lisp
  PERSONALDIR ?= personal

  # Marker file indicating tangle completion
  TANGLE_STAMP := .tangle-stamp

  # Strictness toggles (set to 1 to enable)
  STRICT_BYTE_WARN ?= 0   # -> (setq byte-compile-error-on-warn t)
  NATIVE_COMPILE   ?= 1   # -> use native-compile for .eln if available

  # ---------------------------------------------------------------------
  # Common Emacs command fragments
  EMACS_BATCH := "$(EMACS)" --batch
  EMACS_Q     := $(EMACS_BATCH) -Q

  # Minimal Org loading under -Q
  EVAL_REQ_ORG := \
    --eval "(require 'org)" \
    --eval "(require 'ob-core)" \
    --eval "(org-babel-do-load-languages 'org-babel-load-languages '((emacs-lisp . t)))" \
    --eval "(setq org-confirm-babel-evaluate nil noninteractive t)"

  # Optional: inject leaf paths under -Q (portable: ignore-errors)
  EVAL_LEAF := \
    --eval "(let* ((ldir (expand-file-name \"$(LEAF_DIR)\")) (kwdir (expand-file-name \"$(LEAFKW_DIR)\"))) \
             (when (file-directory-p ldir)  (add-to-list 'load-path ldir)) \
             (when (file-directory-p kwdir) (add-to-list 'load-path kwdir)) \
             (ignore-errors (require 'leaf)) \
             (ignore-errors (require 'leaf-keywords)) \
             (when (featurep 'leaf-keywords) (leaf-keywords-init)))"

  # Optional: strict compile errors on warnings
  EVAL_STRICT := $(if $(filter 1,$(STRICT_BYTE_WARN)),--eval "(setq byte-compile-error-on-warn t)",)

  # Optional: prefer native-compile when available (no-op if not)
  EVAL_NATIVE := $(if $(filter 1,$(NATIVE_COMPILE)),--eval "(setq comp-deferred-compilation t)",)

  # ---------------------------------------------------------------------
  # Resolve EMACSD by loading EARLY; fallback to user-emacs-directory
  EMACSD := $(shell \
    if [ -f "$(EARLY)" ]; then \
      $(EMACS_Q) -l "$(EARLY)" \
        --eval "(princ (expand-file-name (cond ((fboundp 'my:d) (my:d)) (t user-emacs-directory))))"; \
    else \
      $(EMACS_Q) --eval "(princ (expand-file-name user-emacs-directory))"; \
    fi)

  # Resolve STRAIGHT_BASE_DIR with several fallbacks
  STRAIGHT_BASE_DIR := $(shell \
    if [ -f "$(EARLY)" ]; then \
      $(EMACS_Q) -l "$(EARLY)" \
        --eval "(princ (expand-file-name \"straight\" (cond ((boundp 'STRAIGHT_BASE_DIR) STRAIGHT_BASE_DIR) \
                                               ((fboundp 'my:straight-base-dir) (my:straight-base-dir)) \
                                               ((boundp 'straight-base-dir) straight-base-dir) \
                                               (t user-emacs-directory))))"; \
    else \
      printf "%s" "$(EMACSD)/straight"; \
    fi)

  LEAF_DIR   := $(STRAIGHT_BASE_DIR)/repos/leaf
  LEAFKW_DIR := $(STRAIGHT_BASE_DIR)/repos/leaf-keywords

  # Reference only (not deps)
  ELFILES  = $(shell { [ -d "$(LISPDIR)" ] && find "$(LISPDIR)" -type f -name '*.el' 2>/dev/null; } ; \
                      { [ -d "$(PERSONALDIR)" ] && find "$(PERSONALDIR)" -type f -name '*.el' 2>/dev/null; } )

  # ---------------------------------------------------------------------
  .PHONY: all help tangle compile compile-q compile-personal compile-lisp \
          compile-incremental clean distclean show-files echo-paths echo-myd echo-sbd check-init

  # Default help
  help:
  	@printf '%s\n' \
  'Targets:' \
  '  all                 Tangle then byte-compile with init.el' \
  '  tangle              org-babel-tangle-file $(ORG) -> *.el' \
  '  compile             Byte-compile under init.el (always recompile)' \
  '  compile-incremental Byte-compile only when .el is newer than .elc' \
  '  compile-q           Byte-compile under -Q (inject leaf if available)' \
  '  compile-personal    Byte-compile personal/*.el under init.el' \
  '  compile-lisp        Byte-compile lisp/*.el under init.el' \
  '  clean               Remove *.elc under lisp/ and personal/' \
  '  distclean           clean + remove $(TANGLE_STAMP) + delete stray *.eln' \
  '  show-files          List .el under lisp/ and personal/' \
  '  echo-paths          Show resolved paths (EMACSD, STRAIGHT_BASE_DIR, etc.)' \
  '' \
  'Options (env):' \
  '  EMACS=<cmd> ORG=<file> EARLY=<file> INIT=<file>' \
  '  STRICT_BYTE_WARN=1     Treat byte-compile warnings as errors' \
  '  NATIVE_COMPILE=1       Prefer native-compile (when available)'

  # Top-level: always run tangle before compile
  all: tangle compile

  # 1) Tangle README.org -> *.el and drop a completion stamp
  $(TANGLE_STAMP): $(ORG)
  	@echo "[tangle] $(ORG)"
  	@$(EMACS_Q) \
  	  $(EVAL_REQ_ORG) \
  	  --eval "(org-babel-tangle-file \"$(ORG)\")"
  	@touch "$(TANGLE_STAMP)"

  tangle: $(TANGLE_STAMP)

  # Sanity check before init-based compile
  check-init:
  	@test -f "$(INIT)" || { \
  	  echo "[error] init.el not found at: $(INIT)"; \
  	  echo "        -> set INIT=/path/to/init.el or use 'make compile-q'"; \
  	  exit 1; }

  # Helper: count .el files safely (for friendly message)
  define COUNT_EL
  ( { [ -d "$(LISPDIR)" ] && find "$(LISPDIR)" -type f -name '*.el' -print0; } 2>/dev/null; \
    { [ -d "$(PERSONALDIR)" ] && find "$(PERSONALDIR)" -type f -name '*.el' -print0; } 2>/dev/null ) \
  | tr -cd '\000' | wc -c | awk '{print $$1}'
  endef

  # 2) Compile using full init (NUL-safe while loop; no xargs)
  compile: check-init $(TANGLE_STAMP)
  	@echo "Byte-compiling .el under '$(LISPDIR)' and '$(PERSONALDIR)'..."
  	@CNT=`$(COUNT_EL)` ; \
  	if [ $$CNT -eq 0 ]; then \
  	  echo "[compile] no .el files found; did tangle write to '$(LISPDIR)' or '$(PERSONALDIR)'?"; \
  	  exit 2; \
  	fi ; \
  	( \
  	  { [ -d "$(LISPDIR)" ] && find "$(LISPDIR)" -type f -name '*.el' -print0; } 2>/dev/null; \
  	  { [ -d "$(PERSONALDIR)" ] && find "$(PERSONALDIR)" -type f -name '*.el' -print0; } 2>/dev/null \
  	) | while IFS= read -r -d '' f; do \
  	    echo "[byte-compile:init] $$f"; \
  	    $(EMACS_BATCH) -l "$(EARLY)" -l "$(INIT)" \
  	      $(EVAL_STRICT) $(EVAL_NATIVE) \
  	      --eval "(setq byte-compile-verbose t debug-on-error t)" \
  	      --eval "(byte-compile-file \"$$f\")"; \
  	  done ; \
  	echo "[compile] done"

  # 2') Incremental compile (only when .el > .elc or .elc missing)
  compile-incremental: check-init $(TANGLE_STAMP)
  	@echo "[compile-incremental] recompile only when .el is newer than .elc"
  	@$(EMACS_BATCH) -l "$(EARLY)" -l "$(INIT)" \
  	  $(EVAL_STRICT) $(EVAL_NATIVE) \
  	  --eval "(setq byte-compile-verbose t debug-on-error t)" \
  	  --eval "(dolist (d '(\"$(LISPDIR)\" \"$(PERSONALDIR)\")) \
  	            (when (and (stringp d) (> (length d) 0) (file-directory-p d)) \
  	              ;; FLAG=0 -> only when .el is newer or .elc missing
  	              (byte-recompile-directory d 0)))"
  	@echo "[compile-incremental] done"

  # Optional: compile under -Q by injecting leaf (does not load init.el)
  compile-q: $(TANGLE_STAMP)
  	@echo "[compile-q] -Q with STRAIGHT_BASE_DIR=$(STRAIGHT_BASE_DIR)"
  	@CNT=`$(COUNT_EL)` ; \
  	if [ $$CNT -eq 0 ]; then \
  	  echo "[compile-q] no .el files found; did tangle write to '$(LISPDIR)' or '$(PERSONALDIR)'?"; \
  	  exit 2; \
  	fi ; \
  	( \
  	  { [ -d "$(LISPDIR)" ] && find "$(LISPDIR)" -type f -name '*.el' -print0; } 2>/dev/null; \
  	  { [ -d "$(PERSONALDIR)" ] && find "$(PERSONALDIR)" -type f -name '*.el' -print0; } 2>/dev/null \
  	) | while IFS= read -r -d '' f; do \
  	    echo "  [byte-compile:-Q] $$f"; \
  	    $(EMACS_Q) \
  	      $(EVAL_LEAF) $(EVAL_STRICT) $(EVAL_NATIVE) \
  	      --eval "(setq byte-compile-verbose t debug-on-error t)" \
  	      --eval "(byte-compile-file \"$$f\")"; \
  	  done ; \
  	echo "[compile-q] done"

  # Optional: directory-scoped compile with init.el
  compile-personal: check-init $(TANGLE_STAMP)
  	@echo "[compile:init] personal/"
  	@if [ -d "$(PERSONALDIR)" ]; then \
  	  find "$(PERSONALDIR)" -type f -name '*.el' -print0 \
  	  | while IFS= read -r -d '' f; do \
  	      echo "[byte-compile:init] $$f"; \
  	      $(EMACS_BATCH) -l "$(EARLY)" -l "$(INIT)" \
  	        $(EVAL_STRICT) $(EVAL_NATIVE) \
  	        --eval "(setq byte-compile-verbose t debug-on-error t)" \
  	        --eval "(byte-compile-file \"$$f\")"; \
  	    done ; \
  	fi

  compile-lisp: check-init $(TANGLE_STAMP)
  	@echo "[compile:init] lisp/"
  	@if [ -d "$(LISPDIR)" ]; then \
  	  find "$(LISPDIR)" -type f -name '*.el' -print0 \
  	  | while IFS= read -r -d '' f; do \
  	      echo "[byte-compile:init] $$f"; \
  	      $(EMACS_BATCH) -l "$(EARLY)" -l "$(INIT)" \
  	        $(EVAL_STRICT) $(EVAL_NATIVE) \
  	        --eval "(setq byte-compile-verbose t debug-on-error t)" \
  	        --eval "(byte-compile-file \"$$f\")"; \
  	    done ; \
  	fi

  # Helpers
  show-files:
  	@echo "[list] $(LISPDIR)";    { [ -d "$(LISPDIR)" ] && find "$(LISPDIR)" -type f -name '*.el' | sort; } || true
  	@echo "[list] $(PERSONALDIR)"; { [ -d "$(PERSONALDIR)" ] && find "$(PERSONALDIR)" -type f -name '*.el' | sort; } || true

  echo-paths:
  	@echo "EMACSD=$(EMACSD)"; \
  	echo "STRAIGHT_BASE_DIR=$(STRAIGHT_BASE_DIR)"; \
  	echo "LEAF_DIR=$(LEAF_DIR)"; \
  	echo "LEAFKW_DIR=$(LEAFKW_DIR)"; \
  	echo "EARLY=$(EARLY)"; \
  	echo "INIT=$(INIT)"

  echo-myd:
  	@echo $(EMACSD)

  echo-sbd:
  	@echo $(STRAIGHT_BASE_DIR)

  # Cleanup
  clean:
  	@echo "[clean] remove *.elc under $(LISPDIR) and $(PERSONALDIR)"
  	@{ [ -d "$(LISPDIR)" ] && find "$(LISPDIR)" -type f -name '*.elc' -delete; } 2>/dev/null || true
  	@{ [ -d "$(PERSONALDIR)" ] && find "$(PERSONALDIR)" -type f -name '*.elc' -delete; } 2>/dev/null || true

  distclean: clean
  	@echo "[distclean] remove $(TANGLE_STAMP) and stray *.eln"
  	@rm -f "$(TANGLE_STAMP)"
  	@find . -type f -name '*.eln' -delete
#+end_src

*** System Information

**** Apple Silicon (Primary)
- GNU Emacs *31.0.50*

|Property|Value|
|--------|-----|
|Commit|1f3d417736cbf363f64cf8b99752408bbfd266a5|
|Branch|master|
|System|aarch64-apple-darwin25.0.0|
|Date|2025-10-18 20:46:44 (JST)|
|Patch|N/A ns-inline.patch|
|Features|ACL DBUS GLIB GNUTLS LCMS2 LIBXML2 MODULES NATIVE_COMP NOTIFY KQUEUE NS PDUMPER PNG RSVG SQLITE3 THREADS TOOLKIT_SCROLL_BARS TREE_SITTER WEBP XIM ZLIB|
|Options|--with-native-compilation --with-gnutls=ifavailable --with-json --with-modules --with-tree-sitter --with-xml2 --with-librsvg --with-mailutils --with-native-image-api --with-ns CPPFLAGS=-I/opt/homebrew/opt/llvm/include 'LDFLAGS=-L/opt/homebrew/opt/llvm/lib -L/opt/homebrew/opt/llvm/lib/c++ -Wl,-rpath,/opt/homebrew/opt/llvm/lib/c++'|

**** Intel (Secondary)
- GNU Emacs *31.0.50*

|Property|Value|
|--------+-----|
|Commit|aa12cebaa684d7b3ea7e131666d33bcc71b45625|
|Branch|master|
|System|x86_64-apple-darwin24.4.0|
|Date|2025-03-23 10:35:38 (JST)|
|Patch|N/A ns-inline.patch|
|Features|ACL DBUS GIF GLIB GMP GNUTLS JPEG LCMS2 LIBXML2 MODULES NATIVE_COMP NOTIFY KQUEUE NS PDUMPER PNG RSVG SQLITE3 THREADS TIFF TOOLKIT_SCROLL_BARS TREE_SITTER WEBP XIM XWIDGETS ZLIB|
|Options|--with-native-compilation --with-gnutls=ifavailable --with-json --with-modules --with-tree-sitter --with-xml2 --with-xwidgets --with-librsvg CFLAGS=-I/Library/Developer/CommandLineTools/SDKs/MacOSX.sdk/usr/include CPPFLAGS=-I/usr/local/opt/llvm/include 'LDFLAGS=-L/usr/local/opt/llvm/lib -L/usr/local/opt/llvm/lib/c++)|

* Configuration Files
:PROPERTIES:
:CUSTOM_ID: structure
:END:

This Emacs configuration is modular and organized for *Emacs 30+*.

- `early-init.el` → startup optimizations & directories
- `init.el` → package bootstrap & base settings
- `lisp/` → modular configs (core, ui, completion, org, dev, vcs, utils)
- `personal/` → user-specific settings

** Core Bootstrap — early-init.el & init.el
:PROPERTIES:
:CUSTOM_ID: core-bootstrap
:END:

*** Overview

**** Purpose
Provide a clean, fast, and macOS-friendly bootstrap that runs *before* regular initialization and sets safe defaults for directories, performance, package management (**straight.el + leaf**), and UI. The second stage (`init.el`) finishes bootstrapping (**straight.el**), imports the login environment on macOS, applies performance knobs, and exposes a deterministic module entrypoint.

**** What this configuration does
- Disables `package.el` early (we use *straight.el* and *leaf* exclusively).
- Speeds up startup (temporarily widen GC and clear `file-name-handler-alist`, then restore).
- Normalizes state locations under `.cache/`, `.etc/`, `.var/` (incl. *native-compile*).
- Prefers *Homebrew* toolchain on macOS (PATH, `LIBRARY_PATH`, and `CC` → `gcc-N`).
- Turns *backupsd* and classic *auto-save* off early (you can use `auto-save-visited-mode` later).
- Applies early UI defaults to avoid flicker (no tool/menu/scroll bars, predictable frames).
- Bootstraps *straight.el* robustly (network download guarded with helpful error messages).
- Loads a minimal base of packages, initializes *leaf* keywords, and enables core performance tweaks (GCMH, `read-process-output-max`).
- Sets URL state paths *before* `url.el` loads (so *straight* inherits them).
- Provides a safe “personal override” loader (`personal/<user>.el`) and a `modules.el` entrypoint.

**** Module map (where things live)
|File|Role|
|----+----|
| =early-init.el= |Pre-init bootstrap (dirs, performance, `package.el` off, macOS toolchain, early UI)|
| =init.el=      |Main init (URL state, straight bootstrap, env import, performance knobs, module loader)|

**** How it works (boot flow)
1. Emacs loads *early-init.el*:
   - Paths/dirs are established, `package.el` is disabled, GC/file-handlers are relaxed,
     UI is quieted, and macOS toolchain variables are prepared.
2. Emacs loads *init.el*:
   - URL state directories are set *before* `url.el`, then *straight.el* is bootstrapped.
   - On macOS GUI/daemon, the login environment is imported via *exec-path-from-shell*.
   - *leaf* is initialized and a few base packages are ensured.
   - Performance knobs (GCMH, `read-process-output-max`) are applied and later restored.
   - A per-user personal file is safely loaded, and `modules.el` acts as the entrypoint.
3. After init, a concise startup message is printed (time + GC count).

**** Key settings (reference)
- =package-enable-at-startup= :: =nil= — rely on *straight.el*.
- =straight-base-dir= :: =.cache/= under your config base.
- =native-comp-eln-load-path= :: Uses =.cache/eln-cache= to keep artifacts tidy.
- =read-process-output-max= :: Temporarily raised to 4 MiB for better LSP/IO throughput.
- =gcmh-high-cons-threshold= :: 16 MiB; =gcmh-mode= on for smoother GC behavior.

**** Usage tips
- Keep customization out of `early-init.el`—treat it as infrastructure. Put user-level
  behaviors into modules loaded from `init.el` (via `modules.el`) or `personal/<user>.el`.
- If you install `gcc`/`libgccjit` via Homebrew after Emacs is running, restart Emacs to
  pick up `LIBRARY_PATH`/`CC` changes for native compilation.
- To rehome all artifacts, move the config directory and Emacs will regenerate `.cache/`, `.etc/`, and `.var/`.

**** Troubleshooting
- *“Native compilation can’t find libgccjit on macOS”* → Ensure Homebrew’s
  `opt/libgccjit` exists and Emacs sees `LIBRARY_PATH`. Thais early-init sets it if present.
- *“Straight bootstrap failed”* → Network hiccup during `url-retrieve-synchronously`.
  Re-run; the code surfaces a clear `[straight] bootstrap failed: ...` message.
- *“Inhibit startup echo warning”* → We set `inhibit-startup-echo-area-message` to your
  real user name string to satisfy Emacs’ type constraint.

*** early-init.el

#+begin_src emacs-lisp :tangle early-init.el
  ;;; early-init.el --- Early initialization (core) -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;;; Commentary:
  ;; Core bootstrap executed before regular init.
  ;; Category: core
  ;;
  ;; - Disable package.el (we use straight.el + leaf)
  ;; - Fast startup: widen GC/file-name-handlers and restore after startup
  ;; - Keep artifacts under .cache/ .etc/ .var/ (native-compile, no-littering, straight)
  ;; - macOS: prefer Homebrew gcc/libgccjit (PATH/LIBRARY_PATH/CC)
  ;; - Backups OFF; classic auto-save OFF (use auto-save-visited in init)
  ;; - Early UI: frame defaults & hide GUI bars to avoid flicker
  ;;
  ;;; Code:

  (eval-when-compile (require 'subr-x))
  (require 'seq)

  ;;;; Internal helpers (core namespace) -----------------------------------------
  (defun core--ensure-directory-exists (dir)
    "Ensure DIR exists, creating it if necessary."
    (unless (file-directory-p dir)
      (condition-case err
          (make-directory dir t)
        (error (warn "Failed to create directory: %s (%s)"
                     dir (error-message-string err))))))

  (defun core--system-username ()
    "Return current login name as a string or nil."
    (ignore-errors (user-login-name)))

  (defun core--restore-startup-state ()
    "Restore GC and file handler settings after startup."
    (setq file-name-handler-alist core--orig-file-name-handler-alist
          gc-cons-threshold 16777216     ;; 16MB
          gc-cons-percentage 0.1))

  ;; Backward-compat public alias for existing configs
  (defalias 'my/ensure-directory-exists #'core--ensure-directory-exists)

  ;;;; 1) Disable package.el ------------------------------------------------------
  (setq package-enable-at-startup nil
        package-quickstart nil)

  ;;;; 2) Base directory ----------------------------------------------------------
  (defvar my:d
    (or (and load-file-name (file-name-directory (file-chase-links load-file-name)))
        user-emacs-directory)
    "Base directory for this Emacs configuration.")
  (setq user-emacs-directory (file-name-as-directory my:d))

  ;;;; 3) Hidden sub-directories --------------------------------------------------
  (defconst my:d:var        (expand-file-name ".var"        my:d))
  (defconst my:d:etc        (expand-file-name ".etc"        my:d))
  (defconst my:d:cache      (expand-file-name ".cache"      my:d))
  (defconst my:d:lisp       (expand-file-name "lisp"        my:d))
  (defconst my:d:eln-cache  (expand-file-name "eln-cache"   my:d:cache))
  (defconst my:d:treesit    (expand-file-name "tree-sitter" my:d:var))
  (defconst my:d:url        (expand-file-name "url/"        my:d:var)
    "Directory for url.* state (cookies, history, etc.) under my:d:var.")
  (defconst my:d:eww        (expand-file-name "eww/"        my:d:var)
    "Directory for EWW state (bookmarks, history) under my:d:var.")

  (dolist (dir (list my:d:var my:d:etc my:d:cache my:d:lisp
                     my:d:treesit my:d:url my:d:eww
                     my:d:eln-cache))
    (core--ensure-directory-exists dir))

  ;;;; 4) macOS: prefer Homebrew gcc/libgccjit -----------------------------------
  (when (eq system-type 'darwin)
    (let* ((brew-prefix
            (or (getenv "HOMEBREW_PREFIX")
                (and (file-directory-p "/opt/homebrew") "/opt/homebrew")
                (and (file-directory-p "/usr/local")   "/usr/local")))
           (bin  (and brew-prefix (expand-file-name "bin" brew-prefix)))
           (prefix-libgccjit (and brew-prefix (expand-file-name "opt/libgccjit" brew-prefix)))
           (prefix-gcc       (and brew-prefix (expand-file-name "opt/gcc"       brew-prefix)))
           (candidates (seq-filter
                        #'file-directory-p
                        (list
                         (and prefix-libgccjit (expand-file-name "lib/gcc/current" prefix-libgccjit))
                         (and prefix-libgccjit (expand-file-name "lib"             prefix-libgccjit))
                         (and prefix-gcc       (expand-file-name "lib/gcc/current" prefix-gcc))))))
      ;; PATH: prepend brew bin once.
      (when (and bin (file-directory-p bin))
        (let* ((path (or (getenv "PATH") ""))
               (parts (split-string path ":" t)))
          (unless (member bin parts)
            (setenv "PATH" (concat bin ":" path)))))
      ;; LIBRARY_PATH: add & de-dup.
      (when candidates
        (setenv "LIBRARY_PATH"
                (string-join
                 (delete-dups
                  (append candidates
                          (when-let* ((old (getenv "LIBRARY_PATH")))
                            (split-string old ":" t))))
                 ":")))
      ;; CC: pick highest gcc-N available.
      (when (and bin (file-directory-p bin))
        (when-let* ((gcc
                     (car (seq-filter
                           #'file-exists-p
                           (mapcar (lambda (n) (expand-file-name (format "gcc-%d" n) bin))
                                   (number-sequence 20 10 -1))))))
          (setenv "CC" gcc)))))

  ;;;; 5) Native compilation path (.cache/eln-cache) ------------------------------
  (when (featurep 'native-compile)
    (setopt native-comp-eln-load-path (list my:d:eln-cache)
            native-comp-async-report-warnings-errors 'silent))

  ;;;; 6) Pre-wire no-littering dirs ----------------------------------------------
  (defvar no-littering-etc-directory (file-name-as-directory my:d:etc))
  (defvar no-littering-var-directory (file-name-as-directory my:d:var))

  ;;;; 7) straight.el base under .cache/ ------------------------------------------
  (setopt straight-base-dir my:d:cache
          straight-use-package-by-default t
          straight-profiles '((nil . "default.el")))

  ;;;; 8) Startup speedups (restore after startup) --------------------------------
  (defvar core--orig-file-name-handler-alist file-name-handler-alist)
  (setq file-name-handler-alist nil
        gc-cons-threshold most-positive-fixnum
        gc-cons-percentage 0.6)
  (add-hook 'emacs-startup-hook #'core--restore-startup-state)

  ;;;; 9) Backups OFF / classic auto-save OFF -------------------------------------
  (setq make-backup-files nil
        version-control nil
        delete-old-versions nil
        backup-by-copying nil
        auto-save-default nil
        auto-save-list-file-prefix nil)
  (core--ensure-directory-exists (expand-file-name "backups" my:d:var))

  ;;;; 10) Early UI (avoid flicker) ----------------------------------------------
  (setopt frame-resize-pixelwise t
          frame-inhibit-implied-resize t
          cursor-in-non-selected-windows nil
          x-underline-at-descent-line t
          window-divider-default-right-width 16
          window-divider-default-places 'right-only)

  (dolist (it '((fullscreen . fullboth)
                (undecorated . nil)
                (internal-border-width . 8)
                (left-fringe . 0)
                (right-fringe . 0)
                (tool-bar-lines . 0)))
    (add-to-list 'default-frame-alist it)
    (add-to-list 'initial-frame-alist it))

  (when (fboundp 'menu-bar-mode)  (menu-bar-mode -1))
  (when (fboundp 'tool-bar-mode)  (tool-bar-mode -1))
  (when (fboundp 'scroll-bar-mode)(scroll-bar-mode -1))

  ;; Correct type: must be user name string.
  (let ((u (core--system-username)))
    (when (stringp u)
      (setq inhibit-startup-echo-area-message u)))

  (provide 'early-init)
  ;;; early-init.el ends here
#+end_src

*** init.el

#+begin_src emacs-lisp :tangle init.el
  ;;; init.el --- Main initialization (core) -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;;; Commentary:
  ;; Entry point for Emacs 30+ configuration.
  ;; Category: core
  ;;
  ;; - URL state paths (before url.el)
  ;; - straight.el bootstrap
  ;; - Early import of shell environment (exec-path-from-shell)
  ;; - Performance (GCMH / read-process-output-max)
  ;; - Core built-ins via leaf (do not duplicate early-init responsibilities)
  ;; - Backups OFF + auto-save-visited managed elsewhere (early-init / core)
  ;;
  ;;; Code:

  (eval-when-compile (require 'subr-x))

  ;;;; 0) URL state BEFORE url.el is loaded --------------------------------------
  (setopt url-configuration-directory (or (bound-and-true-p my:d:url)
                                          (expand-file-name "url/" user-emacs-directory))
          url-cookie-file  (expand-file-name "cookies" url-configuration-directory)
          url-history-file (expand-file-name "history" url-configuration-directory)
          url-cache-directory (expand-file-name "cache" url-configuration-directory))
  (dolist (d (list url-configuration-directory url-cache-directory))
    (when d (make-directory d t)))
  (require 'url)  ;; used by straight bootstrap

  ;;;; 1) Bootstrap straight.el ---------------------------------------------------
  (defvar bootstrap-version 7)
  (let* ((base (or (bound-and-true-p straight-base-dir) user-emacs-directory))
         (bootstrap-file (expand-file-name "straight/repos/straight.el/bootstrap.el" base)))
    (unless (file-exists-p bootstrap-file)
      (condition-case err
          (with-current-buffer
              (url-retrieve-synchronously
               "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
               'silent 'inhibit-cookies)
            (goto-char (point-max))
            (eval-print-last-sexp))
        (error (user-error "[straight] bootstrap failed: %s"
                           (error-message-string err)))))
    (load bootstrap-file nil 'nomessage))

  ;;;; 1.5) Import environment early (macOS GUI/daemon safe) ---------------------
  (straight-use-package 'exec-path-from-shell)
  (when (memq window-system '(mac ns))
    (setq exec-path-from-shell-check-startup-files nil
          exec-path-from-shell-arguments '("-l" "-i"))
    (exec-path-from-shell-copy-envs
     '("PATH" "LANG" "PASSWORD_STORE_DIR" "GPG_KEY_ID"
       "OPENROUTER_API_KEY" "OPENAI_API_KEY"))
    (exec-path-from-shell-initialize))

  ;; Base packages
  (dolist (pkg '(leaf leaf-keywords leaf-convert blackout helpful imenu-list hydra org))
    (ignore-errors (straight-use-package pkg)))
  (eval-when-compile
    (require 'leaf)
    (require 'leaf-keywords))
  (when (fboundp 'leaf-keywords-init)
    (leaf-keywords-init))
  (require 'org)

  ;;;; 2) Performance tweaks ------------------------------------------------------
  (defvar core--orig-read-process-output-max
    (and (boundp 'read-process-output-max) read-process-output-max))
  (when (boundp 'read-process-output-max)
    (setq read-process-output-max (* 4 1024 1024)))  ;; 4MB for LSP/IO
  (add-hook 'emacs-startup-hook
            (lambda ()
              (when (boundp 'read-process-output-max)
                (setq read-process-output-max core--orig-read-process-output-max))))

  (leaf gcmh
    :straight t
    :custom ((gcmh-idle-delay . 5)
             (gcmh-high-cons-threshold . 16777216)) ;; 16MB
    :config
    (gcmh-mode 1))

  ;;;; 3) Core built-ins (do NOT duplicate early-init responsibilities) ----------
  (leaf emacs
    :straight nil
    :bind (("M-/" . cape-dabbrev))
    :hook ((prog-mode . display-line-numbers-mode))
    :custom ((inhibit-startup-screen . t)
             (inhibit-startup-message . t)
             (initial-scratch-message . nil)
             (initial-major-mode . 'fundamental-mode)  ;; must be quoted
             (use-short-answers . t)
             (create-lockfiles . nil)
             (idle-update-delay . 0.2)
             (ring-bell-function . #'ignore)          ;; function value must be #'quoted
             (display-line-numbers-type . 'relative)  ;; <-- quote the symbol
             (frame-title-format . t))
    :config
    (when (fboundp 'window-divider-mode) (window-divider-mode 1))
    (when (fboundp 'pixel-scroll-precision-mode) (pixel-scroll-precision-mode 1))
    (when (fboundp 'electric-pair-mode) (electric-pair-mode 1))
    (dolist (k '("C-z" "C-x C-z" "C-x C-c" "M-z" "M-m"))
      (when (lookup-key (current-global-map) (kbd k))
        (keymap-global-unset k)))
    (setq confirm-kill-emacs #'y-or-n-p)
    ;; backup/auto-save policy is handled in early-init and core/editing; do not duplicate here.
    )

  ;;;; 4) Modifiers per OS --------------------------------------------------------
  (leaf my:modifier
    :straight nil
    :config
    (pcase system-type
      ('darwin
       (setq mac-option-modifier 'meta
             mac-command-modifier 'super
             mac-control-modifier 'control
             mac-function-modifier 'hyper))
      ('windows-nt
       (setq w32-lwindow-modifier 'super
             w32-rwindow-modifier 'super
             w32-apps-modifier   'hyper))
      (_ nil)))

  ;;;; 5) Files and environment ---------------------------------------------------
  ;; ⛳ duplicate exec-path-from-shell block removed (initialized earlier).
  (leaf no-littering :straight t)

  ;; macOS: complement exec-path/PATH with Homebrew bin (just in case).
  (when (eq system-type 'darwin)
    (dolist (p '("/opt/homebrew/bin" "/usr/local/bin"))
      (when (and (file-directory-p p) (not (member p exec-path)))
        (add-to-list 'exec-path p)
        (unless (and (getenv "PATH") (string-match-p (regexp-quote p) (getenv "PATH")))
          (setenv "PATH" (concat p ":" (getenv "PATH")))))))

  ;;;; 6) Dired display settings --------------------------------------------------
  ;; ⛳ moved to core/editing.el to avoid split/duplication. (section removed)

  ;;;; 7) Personal file loader ----------------------------------------------------
  (defun utils--safe-load-file (file &optional noerror)
    "Load FILE safely. If NOERROR is non-nil, log instead of raising."
    (when (and file (file-exists-p file))
      (condition-case err
          (load file nil 'nomessage)
        (error
         (funcall (if noerror #'message #'user-error)
                  "[load] failed to load %s: %s"
                  file (error-message-string err))))))
  (defalias 'my/safe-load-file #'utils--safe-load-file)

  (my/safe-load-file
   (expand-file-name (concat "personal/" user-login-name ".el")
                     (or (bound-and-true-p my:d) user-emacs-directory))
   t)

  ;;;; 8) Modules entrypoint ------------------------------------------------------
  (let* ((root (or (and (boundp 'my:d) (stringp my:d) (file-directory-p my:d) my:d)
                   user-emacs-directory))
         (lisp-dir (expand-file-name "lisp" root)))
    (add-to-list 'load-path lisp-dir)
    (require 'modules nil t))

  ;;;; 9) Startup message ---------------------------------------------------------
  (defun core--announce-startup ()
    "Report startup time and GC count to *Messages*."
    (message "Emacs ready in %.2f seconds with %d GCs."
             (float-time (time-subtract after-init-time before-init-time))
             gcs-done))
  (add-hook 'after-init-hook
            (lambda () (run-with-idle-timer 0 nil #'core--announce-startup)))

  (provide 'init)
  ;;; init.el ends here
#+end_src

** Modular Loader & Core Module Suite — `modules.el` + core/ui/completion/orgx/dev/utils
:PROPERTIES:
:CUSTOM_ID: modular-loader-and-core-suite
:END:

*** Overview — Deterministic, Modular Emacs 30+ Configuration
:PROPERTIES:
:CUSTOM_ID: overview
:END:

**** Purpose
Provide a **deterministic, modular Emacs 30+ configuration** that is:
- *Composable*: modules live under =lisp/= and load in a fixed order.
- *Selectable*: enable/skip modules and switch *UI* and *LSP* backends at runtime.
- *Low-noise*: route Customize output to =.etc/custom.el=; keep writable state under =.var/=.

**** What this configuration does
- *Bootstraps modules* via =modules.el=, honoring:
  - =my:modules-verbose= for per-module timings + summary logs,
  - =my:modules-skip= to omit features,
  - =my:modules-extra= to append features at the end.
- *Switches UI/LSP stack** centrally (=core/switches.el=):
  - UI: =none | doom | nano=
  - LSP: =eglot | lsp=
- *Hardens daily UX* with focused cores:
  - Editing & Dired defaults; undo stack (=undo-fu= / =vundo=); window ops (=ace-window=),
  - Session (saveplace/recentf/savehist/desktop/winner); autosave policy,
  - Utilities (auto-tangle updated Org blocks, scratch auto-recreate, backups GC).
- *UI surface*:
  - Fonts/ligatures, theme (nano-theme + spacious-padding), Treemacs, Minions, time/battery,
  - Optional *nano-modeline* or d**doom-modeline** bundles.
- *Development surface*:
  - Tree-sitter grammars + language remaps; project/editorconfig,
  - Eglot or lsp-mode (+ lsp-ui); Apheleia formatting; Docker/REST/SQL helpers,
  - Aidermacs integration under =~/.var/aideremacs/=.
- *Completion surface (stacked)*:
  - Orderless + Vertico (+ Marginalia),
  - Corfu (+ kind-icon, CAPE) for in-buffer CAPF completion,
  - Consult (+ Embark / embark-consult) for search/actions.

**** Module map (where things live)
| Module file                               | Role / Notes                                                                                 |
|-------------------------------------------+----------------------------------------------------------------------------------------------|
| =modules.el=                              | Central loader; honors =my:modules-verbose/skip/extra=; prints per-module timings.           |
| =core/custom.el=                          | Route Customize to =.etc/custom.el=; helpers to open/dump curated vars/faces.                |
| =core/editing.el=                         | Paredit/Puni, paren/visuals, Dired + GNU ls on macOS, undo-fu/vundo, aggressive-indent, etc. |
| =core/general.el=                         | Non-modal global *leader* (s-SPC on macOS / C-c SPC otherwise), helpers, which-key labels.   |
| =core/history.el=                         | saveplace/recentf/savehist setup (in =.var/=), adds safe directories & variables.            |
| =core/switches.el=                        | =my:use-ui= (none/doom/nano) and =my:use-lsp= (eglot/lsp) enablement with presence checks.   |
| =core/tools.el=                           | Win split toggle, keybinding conflict scanner, Tree-sitter install/ensure, Org fold helpers. |
| =core/utils.el=                           | Auto-tangle updated Org, insert lexical-binding headers under =.var/=, view-mode on RO.      |
| =ui/ui-font.el=                           | Default/emoji font selection, daemon-safe apply, optional ligatures.                         |
| =ui/ui-theme.el=                          | nano-theme colors + spacious-padding; face normalization on theme enable.                    |
| =ui/ui-window.el=                         | Zoom focus layout, desktop save/restore, winner, quick layout save/restore.                  |
| =ui/ui-utils.el=                          | Minions, display-time/battery, Treemacs & nerd-icons-dired, pbcopy on macOS.                 |
| =ui/ui-nano-modeline.el=                  | Nano modeline bundle wrapper (idempotent hooks, guarded require).                             |
| =ui/ui-doom-modeline.el=                  | Doom modeline bundle (nerd-icons).                                                           |
| =completion/completion-core.el=           | Orderless + category overrides (partial-completion for files).                               |
| =completion/completion-vertico.el=        | Vertico (+ posframe on GUI) and Marginalia.                                                  |
| =completion/completion-consult.el=        | Consult (xref glue).                                                                          |
| =completion/completion-embark.el=         | Embark + embark-consult integration.                                                         |
| =completion/completion-corfu.el=          | Corfu (+ kind-icon) + CAPE sources; TAB navigation.                                          |
| =completion/completion-icons.el=          | nerd-icons for ibuffer/completion surfaces.                                                  |
| =orgx/org-core.el=                        | Org core; agenda refs, capture, refile targets, directories under =.var/org=.                |
| =orgx/org-visual.el=                      | org-modern visuals, agenda layout/icons.                                                     |
| =orgx/org-extensions.el=                  | org-journal, org-roam, org-download, toc-org, org-cliplink.                                  |
| =orgx/org-export.el=                      | ox-hugo, markdown-mode/preview, edit-indirect.                                               |
| =dev/web-core.el=                         | Treesit remaps; project/files/editorconfig; TS/TSX/YAML/JSON associations.                   |
| =dev/dev-lsp-eglot.el=                    | Eglot baseline; safe auto-enable when server contact is guessable.                           |
| =dev/dev-lsp-mode.el=                     | lsp-mode (+ lsp-ui); LSP completion delegated to Corfu.                                      |
| =dev/format.el=                           | Apheleia global mode; Prettier(d) wiring for web stacks.                                     |
| =dev/dev-build.el=                        | Make/compile integration; ANSI color in compile buffer.                                      |
| =dev/dev-docker.el=                       | Dockerfile/YAML modes, =docker= commands, tramp-container.                                   |
| =dev/dev-term.el=                         | vterm + vterm-toggle keybindings and display rules.                                          |
| =dev/rest.el=                             | restclient (+ jq).                                                                            |
| =dev/sql.el=                              | SQL defaults (PostgreSQL), sql-indent, sqlformat (pgformatter).                              |
| =utils/utils-functions.el=                | General helpers (smart kill-buffer).                                                         |
| =utils/utils-backup.el=                   | GC old backups in =.var/backup/= at startup.                                                 |
| =utils/utils-async.el=                    | Safe async runner with error trap.                                                           |
| =utils/search-nav.el=                     | ripgrep via Consult; dumb-jump Xref backend; EWW helpers + image toggle.                     |

**** How it works (flow)
1. *Load order**
   =modules.el= builds a final list from =my:modules= + =my:modules-extra= minus =my:modules-skip=, then =require='s each feature with error trapping and (optionally) prints timing.
2. *Switch centers*
   =core/switches.el= checks presence, then enables the selected *UI* bundle (=doom=/=nano=) and *LSP* (=eglot=/=lsp=) via autoloaded entry functions.
3. *State & Customize*
   =core/custom.el= sets =custom-file= to =.etc/custom.el=, ensures the file/dir exist, and provides commands to open/dump curated settings and faces.
4. *Daily UX*
   Editing/Dired/window/session modules apply sane defaults; UI modules configure fonts/themes/modeline; the completion stack is layered; dev modules wire LSP/Tree-sitter/formatting.
5. *Org & export*
   Org directories live under =.var/org/…=; visuals via org-modern; export helpers (ox-hugo/Markdown) are available.

**** Key settings (reference)
- *Loader*
  - =my:modules-verbose= :: non-nil → per-module timing + summarya
  - =my:modules-skip= :: list of =feature= symbols to skip (e.g., ='(dev/dev-docker)=)
  - =my:modules-extra= :: list of =feature= symbols appended after defaults
- *Switches*
  - =my:use-ui= :: one of ='none 'doom 'nano=
  - =my:use-lsp= :: one of ='eglot 'lsp=
- *Tree-sitter*
  - =treesit-language-source-alist= :: upstream repos; grammars under =my:d:var/tree-sitter/=
  - =treesit-extra-load-path= :: includes =my:d:treesit=
  - =my/treesit-ensure= :: install missing grammars safely (Emacs 30 Tree-sitter API)
- *Customize & State*
  - =custom-file= :: → =.etc/custom.el=
  - Writable runtime :: under =.var/= (savehist/recentf/saveplace/desktop/transient/backup/…)
- *Leader (non-modal)*
  - macOS: =s-SPC=, else: =C-c SPC= → grouped submaps (=b, w, p, g, c, e, t, o, m, a, q, h=)

**** Usage tips
- *Skip slow/unused* modules via =my:modules-skip= (e.g., temporarily exclude =dev/dev-docker=).
- *Append experimentals* with =my:modules-extra= so they load after the stable base.
- *Flip UI/LSP* quickly: set =my:use-ui= / =my:use-lsp= before the loader runs (or re-eval switches).
- *Install Tree-sitter grammars* once with =M-x my/treesit-ensure=; re-run after adding languages.
- *Keep Customize clean*: use =M-x my/custom-open= / =my/custom-dump-current= to curate what’s persisted.

**** Troubleshooting
- *“Nano modeline didn’t enable”* → The wrapper defers until =nano-modeline= is available; ensure the package is installed, then run =M-x my/ui-enable-nano=.
- *“LSP not engaging in some buffers (Eglot)”* → Eglot auto-enables only when a server contact is guessable; run =M-x eglot= or add a project config.
- *“Tree-sitter install error”* → Requires Emacs built with Tree-sitter; verify =M-: (featurep 'treesit)= and install Xcode/Build deps on macOS.
- *“GNU ls not found on macOS Dired”* → Install coreutils via Homebrew so =gls= is available, or fall back to built-in =ls= with reduced features.

**** Related source blocks (ready to paste; no tangle)
#+begin_src emacs-lisp :tangle no
  ;; --- Loader knobs -------------------------------------------------------------
  (setq my:modules-verbose t)
  (setq my:modules-skip  '(dev/dev-docker))        ;; example: skip docker during CI
  (setq my:modules-extra '(utils/utils-async))     ;; example: append extras at the end

  ;; --- Switch your stacks -------------------------------------------------------
  (setq my:use-ui  'nano)  ;; 'none | 'doom | 'nano
  (setq my:use-lsp 'eglot) ;; 'eglot | 'lsp

  ;; --- Run the loader (modules.el must be on load-path) ------------------------
  (require 'modules)  ;; provides `my/modules-load` and runs it once at the end
  ;; If you prefer explicit invocation, comment the last line of modules.el and call:
  ;; (my/modules-load)

  ;; --- Tree-sitter bootstrapping (run once; re-run when adding languages) ------
  (when (fboundp 'my/treesit-ensure)
    (my/treesit-ensure))

  ;; --- Customize file helpers ---------------------------------------------------
  ;; Open the curated customize file:
  ;;   M-x my/custom-open
  ;; Dump a curated snapshot of selected vars/faces:
  ;;   M-x my/custom-dump-current
#+end_src

*** modules.el
:PROPERTIES:
:CUSTOM_ID: core-modules
:END:

#+begin_src emacs-lisp :tangle lisp/modules.el
  ;;; modules.el --- Modular config loader -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;;; Commentary:
  ;; Central entry point to load modular configs placed under lisp/.
  ;; Category: core
  ;;
  ;;; Code:

  (eval-when-compile (require 'subr-x)) ;; string-join, etc.

  (defgroup my:modules nil
    "Loader options for modular Emacs configuration."
    :group 'convenience)

  (defcustom my:modules-verbose t
    "If non-nil, print per-module load time and a summary."
    :type 'boolean
    :group 'my:modules)

  (defcustom my:modules-skip nil
    "List of module features to skip during loading."
    :type '(repeat symbol)
    :group 'my:modules)

  (defcustom my:modules-extra nil
    "List of extra module features to append after `my:modules'."
    :type '(repeat symbol)
    :group 'my:modules)

  (defconst my:modules
    '(
      ;; Core
      core/general
      core/tools
      core/utils
      core/history
      core/editing
      core/switches
      core/custom

      ;; UI
      ui/ui-font
      ui/ui-theme
      ui/ui-window
      ui/ui-utils

      ;; Completion
      completion/completion-core
      completion/completion-vertico
      completion/completion-consult
      completion/completion-embark
      completion/completion-corfu
      completion/completion-icons

      ;; Org ecosystem (module namespace = orgx/)
      orgx/org-core
      orgx/org-visual
      orgx/org-extensions
      orgx/org-export

      ;; VCS
      vcs/vcs-magit
      vcs/vcs-gutter
      vcs/vcs-forge

      ;; Development
      dev/dev-ai
      dev/dev-term
      dev/dev-build
      dev/dev-docker

      ;; Web/Infra
      dev/web-core
      dev/format
      dev/infra-modes
      dev/sql
      dev/rest

      ;; Utils
      utils/utils-functions
      utils/utils-scratch
      utils/utils-backup
      utils/utils-async
      utils/search-nav
      )
    "Default set of modules to load in order.")

  (defun my/modules--should-load-p (feature)
    "Return non-nil if FEATURE should be loaded."
    (not (memq feature my:modules-skip)))

  (defun my/modules--require-safe (feature)
    "Require FEATURE with error trapping. Return non-nil on success."
    (condition-case err
        (progn (require feature) t)
      (error (message "[modules] Failed to load %s: %s"
                      feature (error-message-string err))
             nil)))

  ;; FIX: name uses ':' to match call sites
  (defun my:modules--format-seconds (sec)
    "Format SEC (float seconds) compactly."
    (cond
     ((< sec 0.001) (format "%.3fms" (* sec 1000.0)))
     ((< sec 1.0)   (format "%.1fms"  (* sec 1000.0)))
     (t             (format "%.2fs"   sec))))

  (defun my/modules-load ()
    "Load all modules defined by `my:modules', respecting options."
    (let* ((final (seq-remove
                   (lambda (m) (not (my/modules--should-load-p m)))
                   (append my:modules my:modules-extra)))
           (ok 0) (ng 0)
           (t0 (and my:modules-verbose (current-time))))
      (dolist (mod final)
        (let ((m0 (and my:modules-verbose (current-time))))
          (if (my/modules--require-safe mod) (setq ok (1+ ok)) (setq ng (1+ ng)))
          (when my:modules-verbose
            (message "[modules] %-24s %s"
                     mod (my:modules--format-seconds
                          (float-time (time-subtract (current-time) m0)))))))
      (when my:modules-verbose
        (message "[modules] loaded=%d skipped=%d failed=%d total=%s"
                 ok
                 (- (length (append my:modules my:modules-extra))
                    (length final))
                 ng
                 (my:modules--format-seconds
                  (float-time (time-subtract (current-time) t0)))))))

  (my/modules-load)

  (provide 'modules)
  ;;; modules.el ends here
#+end_src

*** core/general.el
:PROPERTIES:
:CUSTOM_ID: core-general
:END:

#+begin_src emacs-lisp :tangle lisp/core/general.el
  ;;; core/general.el --- General settings & keybindings (NO Meow) -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: core
  ;;
  ;;; Commentary:
  ;; - Drop Meow: provide a non-modal, global leader-key layout.
  ;; - Keep useful global bindings for macOS-like shortcuts and muscle memory.
  ;; - Provide LSP-agnostic helpers (code actions, rename, format).
  ;; - Authentication helpers (GPG + pass) remain as-is.
  ;;
  ;; Design notes:
  ;; - We define a global leader key (default: s-SPC on macOS, C-c SPC on others).
  ;; - Under the leader, we expose groups: b(buffers), w(windows), p(project), g(git),
  ;;   c(code), e(errors), t(toggles), o(org/roam), m(misc/web), a(ai), q(session), h(help).
  ;; - Which-Key labels are generated to match the chosen leader key at runtime.
  ;;
  ;;; Code:

  (eval-when-compile
    (require 'leaf)
    (require 'subr-x))

  ;;; Default frame settings
  (setq default-frame-alist '((min-height . 1)  '(height . 45)
                              (min-width  . 1)  '(width  . 81)
                              (vertical-scroll-bars . nil)
                              (internal-border-width . 24)
                              (left-fringe . 0)
                              (right-fringe . 0)
                              (tool-bar-lines . 0)
                              (menu-bar-lines . 0)))

  (setq initial-frame-alist default-frame-alist)

  ;;;; Text scaling hydra ---------------------------------------------------------
  (leaf hydra
    :straight t
    :config
    (defhydra core-hydra-text-scale (:hint nil :color red)
      "
  ^Text Scaling^
  [_+_] increase   [_-_] decrease   [_0_] reset   [_q_] quit
  "
      ("+" text-scale-increase)
      ("-" text-scale-decrease)
      ("0" (text-scale-set 0) :color blue)
      ("q" nil "quit" :color blue)))

  ;;;; Small utilities ------------------------------------------------------------
  (leaf my:utils
    :straight nil
    :init
    (defun my/new-frame-with-scratch ()
      "Create a new frame and switch to a fresh buffer."
      (interactive)
      (let ((frame (make-frame)))
        (with-selected-frame frame
          (switch-to-buffer (generate-new-buffer "untitled")))))

    (defun my/restart-or-exit ()
      "Restart Emacs if `restart-emacs' exists; otherwise save & exit."
      (interactive)
      (if (fboundp 'restart-emacs)
          (restart-emacs)
        (save-buffers-kill-emacs)))

    ;; Arrow-based window motions (keeps default muscle memory).
    (windmove-default-keybindings))

  ;;;; IDE-agnostic helpers (Eglot / lsp-mode) -----------------------------------
  (defun my/code-actions ()
    "Run code actions via Eglot or lsp-mode."
    (interactive)
    (cond
     ((fboundp 'eglot-code-actions) (eglot-code-actions))
     ((fboundp 'lsp-execute-code-action) (lsp-execute-code-action))
     (t (user-error "No code action backend (Eglot/LSP) available"))))

  (defun my/rename-symbol ()
    "Rename symbol via Eglot or lsp-mode."
    (interactive)
    (cond
     ((fboundp 'eglot-rename) (eglot-rename))
     ((fboundp 'lsp-rename) (lsp-rename))
     (t (user-error "No rename backend (Eglot/LSP) available"))))

  (defun my/format-buffer ()
    "Format buffer via Eglot/LSP; fallback to `indent-region'."
    (interactive)
    (cond
     ((fboundp 'eglot-format-buffer) (eglot-format-buffer))
     ((fboundp 'lsp-format-buffer) (lsp-format-buffer))
     ((fboundp 'indent-region) (indent-region (point-min) (point-max)))
     (t (user-error "No formatter available"))))

  (defun my/consult-ripgrep-project ()
    "Run ripgrep in current project; fallback to prompting."
    (interactive)
    (let* ((pr (when (fboundp 'project-current) (project-current)))
           (root (when pr (car (project-roots pr)))))
      (if (and root (fboundp 'consult-ripgrep))
          (consult-ripgrep root)
        (call-interactively 'consult-ripgrep))))

  (defun my/toggle-transient-line-numbers ()
    "Toggle line numbers, preserving buffer-local overrides."
    (interactive)
    (if (bound-and-true-p display-line-numbers-mode)
        (display-line-numbers-mode 0)
      (display-line-numbers-mode 1)))

  ;;;; Global LEADER (non-modal) --------------------------------------------------
  ;; Choose a sane global leader key:
  ;; - macOS (ns/mac): use "C-c SPC".
  ;; - Others: use "C-c SPC" (reserved for users; safe with which-key).
  (defconst my:leader-key
    (if (memq system-type '(darwin)) "C-c SPC" "C-c SPC")
    "Key sequence used as global leader key.")

  ;; Which-Key prefix label visible in minibuffer (purely cosmetic).
  (defconst my:leader-which-prefix
    (if (memq system-type '(darwin)) "C-c SPC" "C-c SPC")
    "Human-readable leader prefix string for which-key labels.")

  ;; Top-level leader map and grouped submaps.
  (define-prefix-command 'my/leader-map)
  (define-prefix-command 'my/leader-b-map) ;; buffers
  (define-prefix-command 'my/leader-w-map) ;; windows
  (define-prefix-command 'my/leader-p-map) ;; project
  (define-prefix-command 'my/leader-g-map) ;; git
  (define-prefix-command 'my/leader-c-map) ;; code
  (define-prefix-command 'my/leader-e-map) ;; errors/diagnostics
  (define-prefix-command 'my/leader-t-map) ;; toggles
  (define-prefix-command 'my/leader-o-map) ;; org/roam
  (define-prefix-command 'my/leader-m-map) ;; misc/web
  (define-prefix-command 'my/leader-a-map) ;; ai
  (define-prefix-command 'my/leader-q-map) ;; session/quit
  (define-prefix-command 'my/leader-h-map) ;; help

  ;; Bind leader globally.
  (when (fboundp 'keymap-global-set)
    (keymap-global-set my:leader-key 'my/leader-map))

  ;; Group heads under the leader.
  (define-key my/leader-map (kbd "b") 'my/leader-b-map)
  (define-key my/leader-map (kbd "w") 'my/leader-w-map)
  (define-key my/leader-map (kbd "p") 'my/leader-p-map)
  (define-key my/leader-map (kbd "g") 'my/leader-g-map)
  (define-key my/leader-map (kbd "c") 'my/leader-c-map)
  (define-key my/leader-map (kbd "e") 'my/leader-e-map)
  (define-key my/leader-map (kbd "t") 'my/leader-t-map)
  (define-key my/leader-map (kbd "o") 'my/leader-o-map)
  (define-key my/leader-map (kbd "m") 'my/leader-m-map)
  (define-key my/leader-map (kbd "a") 'my/leader-a-map)
  (define-key my/leader-map (kbd "q") 'my/leader-q-map)
  (define-key my/leader-map (kbd "h") 'my/leader-h-map)

  ;; 1) Top-level leader bindings (LEADER ...)
  (define-key my/leader-map (kbd "SPC") #'execute-extended-command) ;; M-x
  (define-key my/leader-map (kbd "/")   #'consult-line)
  (define-key my/leader-map (kbd ";")   #'comment-or-uncomment-region)
  (define-key my/leader-map (kbd "=")   #'er/expand-region)
  (define-key my/leader-map (kbd "`")   #'eval-expression)
  (define-key my/leader-map (kbd "z")   #'core-hydra-text-scale/body)
  ;; frequent file/recents helpers
  (define-key my/leader-map (kbd ".")   #'other-window)
  (define-key my/leader-map (kbd "f")   #'find-file)
  (define-key my/leader-map (kbd "F")   #'find-file-other-window)
  (define-key my/leader-map (kbd "O")   #'find-file-other-frame)
  (define-key my/leader-map (kbd "r")   #'consult-recent-file)

  ;; 2) Buffers (LEADER b ...)
  (define-key my/leader-b-map (kbd "b") #'consult-buffer)
  (define-key my/leader-b-map (kbd "B") #'consult-project-buffer)
  (define-key my/leader-b-map (kbd "k") #'my/kill-buffer-smart)
  (define-key my/leader-b-map (kbd "n") #'next-buffer)
  (define-key my/leader-b-map (kbd "p") #'previous-buffer)
  (define-key my/leader-b-map (kbd "r") #'revert-buffer)

  ;; 3) Windows (LEADER w ...)
  (define-key my/leader-w-map (kbd "w") #'ace-window)
  (define-key my/leader-w-map (kbd "s") #'split-window-below)
  (define-key my/leader-w-map (kbd "v") #'split-window-right)
  (define-key my/leader-w-map (kbd "d") #'delete-window)
  (define-key my/leader-w-map (kbd "o") #'delete-other-windows)
  (define-key my/leader-w-map (kbd "=") #'balance-windows)
  (define-key my/leader-w-map (kbd "2") #'my/toggle-window-split)

  ;; 4) Project (LEADER p ...)
  (define-key my/leader-p-map (kbd "p") #'project-switch-project)
  (define-key my/leader-p-map (kbd "f") #'project-find-file)
  (define-key my/leader-p-map (kbd "s") #'my/consult-ripgrep-project)
  (define-key my/leader-p-map (kbd "b") #'consult-project-buffer)
  (define-key my/leader-p-map (kbd "r") #'project-query-replace-regexp)
  (define-key my/leader-p-map (kbd "d") #'project-dired)

  ;; 5) Search (LEADER s ...)  — expose under main map for convenience, too.
  ;; (You can also keep these under "p" or top-level.)
  (define-key my/leader-map (kbd "s s") #'consult-line)
  (define-key my/leader-map (kbd "s r") #'consult-ripgrep)
  (define-key my/leader-map (kbd "s g") #'my/consult-ripgrep-project)
  (define-key my/leader-map (kbd "s m") #'consult-imenu)

  ;; 6) Git (LEADER g ...)
  (define-key my/leader-g-map (kbd "s") #'magit-status)
  (define-key my/leader-g-map (kbd "b") #'magit-blame-addition)
  (define-key my/leader-g-map (kbd "l") #'magit-log-current)
  (define-key my/leader-g-map (kbd "d") #'magit-diff-buffer-file)
  (define-key my/leader-g-map (kbd "c") #'magit-commit)

  ;; 7) Code (LEADER c ...) — LSP-agnostic helpers
  (define-key my/leader-c-map (kbd "a") #'my/code-actions)
  (define-key my/leader-c-map (kbd "r") #'my/rename-symbol)
  (define-key my/leader-c-map (kbd "f") #'my/format-buffer)
  (define-key my/leader-c-map (kbd "d") #'xref-find-definitions)
  (define-key my/leader-c-map (kbd "D") #'xref-find-definitions-other-window)
  (define-key my/leader-c-map (kbd "R") #'xref-find-references)
  (define-key my/leader-c-map (kbd "i") #'completion-at-point)

  ;; 8) Errors/diagnostics (LEADER e ...)
  (define-key my/leader-e-map (kbd "n") #'flymake-goto-next-error)
  (define-key my/leader-e-map (kbd "p") #'flymake-goto-prev-error)
  (define-key my/leader-e-map (kbd "l") #'flymake-show-buffer-diagnostics)

  ;; 9) Toggles (LEADER t ...)
  (define-key my/leader-t-map (kbd "l") #'my/toggle-transient-line-numbers)
  (define-key my/leader-t-map (kbd "w") #'whitespace-mode)
  (define-key my/leader-t-map (kbd "r") #'read-only-mode)
  (define-key my/leader-t-map (kbd "z") #'core-hydra-text-scale/body)
  (define-key my/leader-t-map (kbd "i") #'my/eww-toggle-images)

  ;; 10) Org & Roam (LEADER o ...)
  (define-key my/leader-o-map (kbd "a") #'org-agenda)
  (define-key my/leader-o-map (kbd "c") #'org-capture)
  (define-key my/leader-o-map (kbd "i") #'org-roam-node-insert)
  (define-key my/leader-o-map (kbd "f") #'org-roam-node-find)

  ;; 11) Misc/Web (LEADER m ...)
  (define-key my/leader-m-map (kbd "e") #'eww)
  (define-key my/leader-m-map (kbd "s") #'my/eww-search)
  (define-key my/leader-m-map (kbd "o") #'eww-open-file)
  (define-key my/leader-m-map (kbd "b") #'eww-list-bookmarks)
  (define-key my/leader-m-map (kbd "r") #'eww-readable)
  (define-key my/leader-m-map (kbd "u") #'my/eww-toggle-images)

  ;; 12) AI (LEADER a ...)
  (define-key my/leader-a-map (kbd "a") #'aidermacs-transient-menu)

  ;; 13) Session/quit (LEADER q ...)
  (define-key my/leader-q-map (kbd "n") #'my/new-frame-with-scratch)
  (define-key my/leader-q-map (kbd "r") #'my/restart-or-exit)
  (define-key my/leader-q-map (kbd "q") #'save-buffers-kill-emacs)

  ;; 14) Help (LEADER h ...)
  (define-key my/leader-h-map (kbd "k") #'describe-key)
  (define-key my/leader-h-map (kbd "f") #'describe-function)
  (define-key my/leader-h-map (kbd "v") #'describe-variable)

  ;;;; Which-Key (discoverability for leader groups) ------------------------------
  (leaf which-key
    :straight t
    :hook (after-init-hook . which-key-mode)
    :custom ((which-key-idle-delay . 0.4))
    :config
    ;; Label leader groups dynamically according to `my:leader-which-prefix`.
    (dolist (it `((,(concat my:leader-which-prefix " b") . "buffers")
                  (,(concat my:leader-which-prefix " w") . "windows")
                  (,(concat my:leader-which-prefix " p") . "project")
                  (,(concat my:leader-which-prefix " s") . "search")
                  (,(concat my:leader-which-prefix " g") . "git")
                  (,(concat my:leader-which-prefix " c") . "code")
                  (,(concat my:leader-which-prefix " e") . "errors")
                  (,(concat my:leader-which-prefix " t") . "toggles")
                  (,(concat my:leader-which-prefix " o") . "org/roam")
                  (,(concat my:leader-which-prefix " m") . "misc/web")
                  (,(concat my:leader-which-prefix " a") . "ai")
                  (,(concat my:leader-which-prefix " q") . "session")
                  (,(concat my:leader-which-prefix " h") . "help")))
      (which-key-add-key-based-replacements (car it) (cdr it))))

  ;;;; Global bindings (outside leader) -------------------------------------------
  (leaf my:key:global
    :straight nil
    :bind
    (("<f1>"      . help-command)
     ("<f5>"      . my/revert-buffer-quick)
     ("<f8>"      . treemacs)
     ("C-."       . other-window)
     ("C-/"       . undo-fu-only-undo)
     ("C-="       . er/expand-region)
     ("C-?"       . undo-fu-only-redo)
     ("C-c 0"     . delete-window)
     ("C-c 1"     . delete-other-window)
     ("C-c 2"     . my/toggle-window-split)
     ("C-c ;"     . comment-or-uncomment-region)
     ("C-c M-a"   . align-regexp)
     ("C-c V"     . view-file-other-window)
     ("C-c a a"   . aidermacs-transient-menu)
     ("C-c b"     . consult-buffer)
     ("C-c d a"   . org-agenda)
     ("C-c d c"   . org-capture)
     ("C-c d f"   . org-roam-node-find)
     ("C-c d i"   . org-roam-node-insert)
     ("C-c k"     . my/kill-buffer-smart)
     ("C-c l"     . display-line-numbers-mode)
     ("C-c o"     . find-file)
     ("C-c r"     . consult-ripgrep)
     ("C-c v"     . find-file-read-only)
     ("C-c w b"   . eww-list-bookmarks)
     ("C-c w o"   . eww-open-file)
     ("C-c w r"   . eww-readable)
     ("C-c w s"   . my/eww-search)
     ("C-c w u"   . my/eww-toggle-images)
     ("C-c w w"   . eww)
     ("C-c z"     . core-hydra-text-scale/body)
     ("C-h"       . backward-delete-char)
     ("C-s"       . consult-line)
     ("s-."       . ace-window)
     ("s-<down>"  . end-of-buffer)
     ("s-<left>"  . previous-buffer)
     ("s-<right>" . next-buffer)
     ("s-<up>"    . beginning-of-buffer)
     ("s-b"       . consult-buffer)
     ("s-j"       . find-file-other-window)
     ("s-m"       . my/new-frame-with-scratch)
     ("s-o"       . find-file-other-frame)
     ("s-r"       . my/restart-or-exit)
     ("s-w"       . ace-swap-window)
     ("M-x"       . execute-extended-command)))

  ;;;; Auth / secrets -------------------------------------------------------------
  (defvar my:d:password-store
    (or (getenv "PASSWORD_STORE_DIR")
        (concat no-littering-var-directory "password-store/"))
    "Path to the password store.")

  (defun my/auth-check-env ()
    "Validate authentication environment and warn if misconfigured."
    (unless (getenv "GPG_KEY_ID")
      (display-warning 'auth "GPG_KEY_ID is not set." :level 'debug))
    (unless (file-directory-p my:d:password-store)
      (display-warning 'auth
                       (format "Password store directory does not exist: %s"
                               my:d:password-store)
                       :level 'warning)))

  (leaf *authentication
    :straight nil
    :init
    (my/auth-check-env)

    (leaf epa-file
      :straight nil
      :commands (epa-file-enable)
      :init
      (setq epa-pinentry-mode
            (if (getenv "USE_GPG_LOOPBACK") 'loopback 'default))
      (add-hook 'emacs-startup-hook #'epa-file-enable))

    (leaf auth-source
      :straight nil
      :init
      (with-eval-after-load 'auth-source
        (let ((key (getenv "GPG_KEY_ID")))
          (if key
              (setq auth-source-gpg-encrypt-to key)
            (display-warning 'auth-source
                             "GPG_KEY_ID is not set. Authentication backends may be limited.")))))

    (leaf password-store :straight t)

    (leaf auth-source-pass
      :straight t
      :commands (auth-source-pass-enable)
      :hook (emacs-startup-hook . (lambda ()
                                    (when (executable-find "pass")
                                      (auth-source-pass-enable)))))

    (leaf plstore
      :straight nil
      :init
      (with-eval-after-load 'plstore
        (setq plstore-secret-keys 'silent
              plstore-encrypt-to (getenv "GPG_KEY_ID")))))

  (provide 'core/general)
  ;;; core/general.el ends here
#+end_src

*** core/tools.el
:PROPERTIES:
:CUSTOM_ID: core-tools
:END:

#+begin_src emacs-lisp :tangle lisp/core/tools.el
  ;;; core/tools.el --- Developer & UI helper tools -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: core
  ;;
  ;;; Code:

  (defun my/toggle-linum-lines () (interactive) (display-line-numbers-mode 'toggle))

  (defun my/toggle-window-split ()
    "Toggle between horizontal and vertical split for two windows."
    (interactive)
    (when (= (count-windows) 2)
      (let* ((this-buf (window-buffer))
             (next-buf (window-buffer (next-window)))
             (this-edges (window-edges))
             (next-edges (window-edges (next-window)))
             (split-vert (= (car this-edges) (car next-edges)))
             (split-fn (if split-vert #'split-window-horizontally #'split-window-vertically)))
        (delete-other-windows)
        (funcall split-fn)
        (set-window-buffer (selected-window) this-buf)
        (set-window-buffer (next-window) next-buf)
        (select-window (selected-window)))))

  (defun my/find-keybinding-conflicts ()
    "Find and display conflicting keybindings in active keymaps."
    (interactive)
    (let ((conflicts (make-hash-table :test 'equal))
          (maps (current-active-maps t))
          (buffer-name "*Keybinding Conflicts*"))
      (dolist (map maps)
        (map-keymap
         (lambda (key cmd)
           (when (commandp cmd)
             (let ((desc (key-description (vector key)))
                   (existing (gethash desc conflicts)))
               (puthash desc (delete-dups (cons cmd existing)) conflicts))))
         map))
      (with-current-buffer (get-buffer-create buffer-name)
        (read-only-mode -1)
        (erase-buffer)
        (insert "* Keybinding Conflicts *\n\n")
        (maphash (lambda (key cmds)
                   (when (> (length cmds) 1)
                     (insert (format "%s => %s\n" key (mapconcat #'symbol-name cmds ", ")))))
                 conflicts)
        (read-only-mode 1))
      (pop-to-buffer buffer-name)))

  (defun my/dired-view-file-other-window ()
    "Open selected Dired file or directory in another window."
    (interactive)
    (let ((file (dired-get-file-for-visit)))
      (if (file-directory-p file)
          (or (and (cdr dired-subdir-alist) (dired-goto-subdir file)) (dired file))
        (view-file-other-window file))))

  ;; --------- Tree-sitter helpers -------------------------------------------------
  ;;;; Tree-sitter (modern API; grammars under my:d:var/tree-sitter) --------------
  ;; We store/look up compiled grammars (.so/.dylib) under my:d:var/tree-sitter.

  (defvar my:d:var
    (or (bound-and-true-p my:d:var)
        ;; Prefer no-littering var dir if available
        (and (boundp 'no-littering-var-directory) no-littering-var-directory)
        ;; Fallback: ~/.emacs.d/.var/
        (expand-file-name ".var/" user-emacs-directory))
    "Base directory for variable (writable) runtime data.")

  (defun my/ensure-directory-exists (dir)
    "Create DIR if it does not exist."
    (unless (file-directory-p dir)
      (make-directory dir t)))

  (defconst my:d:treesit
    (expand-file-name "tree-sitter/" my:d:var)
    "Directory to store and load Tree-sitter grammars.")

  ;; Ensure directory exists and register it as an extra search path.
  (my/ensure-directory-exists my:d:treesit)
  (add-to-list 'treesit-extra-load-path my:d:treesit)

  ;; Declare language sources. Keys are language symbols used by treesit.
  (setq treesit-language-source-alist
        '((bash       "https://github.com/tree-sitter/tree-sitter-bash")
          (c          "https://github.com/tree-sitter/tree-sitter-c")
          (cpp        "https://github.com/tree-sitter/tree-sitter-cpp")
          (css        "https://github.com/tree-sitter/tree-sitter-css")
          (go         "https://github.com/tree-sitter/tree-sitter-go")
          (html       "https://github.com/tree-sitter/tree-sitter-html")
          (javascript "https://github.com/tree-sitter/tree-sitter-javascript")
          (json       "https://github.com/tree-sitter/tree-sitter-json")
          (python     "https://github.com/tree-sitter/tree-sitter-python")
          (ruby       "https://github.com/tree-sitter/tree-sitter-ruby")
          (rust       "https://github.com/tree-sitter/tree-sitter-rust")
          (toml       "https://github.com/tree-sitter/tree-sitter-toml")
          (tsx        "https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")
          (typescript "https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src")
          (yaml       "https://github.com/ikatyang/tree-sitter-yaml")))

  (defun my/treesit--language-installed-p (lang)
    "Return non-nil if LANG grammar library can be loaded."
    (and (fboundp 'treesit-language-available-p)
         (ignore-errors (treesit-language-available-p lang))))

  (defun my/treesit-install-languages (&optional langs)
    "Install LANGS via `treesit-install-language-grammar` (0–1 arg API).
  If LANGS is nil, install everything from `treesit-language-source-alist`."
    (unless (fboundp 'treesit-install-language-grammar)
      (user-error "This Emacs is not built with tree-sitter support"))
    (let ((targets (or langs (mapcar #'car treesit-language-source-alist))))
      (dolist (lang targets)
        (unless (my/treesit--language-installed-p lang)
  	(message "[treesit] Installing %s …" lang)



          (treesit-install-language-grammar lang)))))

  (defun my/treesit-ensure (&optional langs)
    "Ensure LANGS grammars are available; install missing ones."
    (interactive)
    (my/treesit-install-languages langs)
    (message "[treesit] All requested grammars are available."))

  ;; Legacy shim preserved for compatibility with old sites passing 7 args, etc.
  (defun my/treesit--call-with-outdir (&rest maybe-legacy-args)
    "Compatibility wrapper. Accept LANG or list of LANG symbols, ignore extras."
    (let ((head (car maybe-legacy-args)))
      (cond
       ((symbolp head) (my/treesit-ensure (list head)))
       ((and (listp head) (cl-every #'symbolp head)) (my/treesit-ensure head))
       (t (user-error "Unsupported call form: %S" maybe-legacy-args)))))
  ;; --------- Misc dev / UI helpers ----------------------------------------------

  (defun my/open-by-vscode () (interactive)
    (when (buffer-file-name)
      (async-shell-command
       (format "code -r -g %s:%d:%d" (buffer-file-name) (line-number-at-pos) (current-column)))))

  (defun my/show-env-variable (var) (interactive "sEnvironment variable: ")
    (message "%s = %s" var (or (getenv var) "Not set")))

  (defun my/print-build-info () (interactive)
    (let ((buf (get-buffer-create "*Build Info*")))
      (with-current-buffer buf
        (let ((inhibit-read-only t))
          (erase-buffer)
          (insert (format "- GNU Emacs *%s*\n\n" emacs-version))
          (insert "|Property|Value|\n|--------|-----|\n")
          (insert (format "|Commit|%s|\n" (if (fboundp 'emacs-repository-get-version)
                                              (emacs-repository-get-version) "N/A")))
          (insert (format "|Branch|%s|\n" (if (fboundp 'emacs-repository-get-branch)
                                              (emacs-repository-get-branch) "N/A")))
          (insert (format "|System|%s|\n" system-configuration))
          (insert (format "|Date|%s|\n" (format-time-string "%Y-%m-%d %T (%Z)" emacs-build-time)))
          (insert (format "|Patch|%s ns-inline.patch|\n" (if (boundp 'mac-ime--cursor-type) "with" "N/A")))
          (insert (format "|Features|%s|\n" system-configuration-features))
          (insert (format "|Options|%s|\n" system-configuration-options)))
        (view-mode 1))
      (switch-to-buffer buf)))

  (with-eval-after-load 'org
    (require 'org-fold)
    (defun my/org-fold-subtree ()   (interactive) (org-fold-subtree t))
    (defun my/org-unfold-subtree () (interactive) (org-show-subtree))
    (defun my/org-toggle-fold () (interactive)
      (save-excursion
        (org-back-to-heading t)
        (if (org-fold-folded-p (point)) (org-show-subtree) (org-fold-subtree t))))
    (define-key org-mode-map (kbd "C-c f") #'my/org-fold-subtree)
    (define-key org-mode-map (kbd "C-c e") #'my/org-unfold-subtree)
    (define-key org-mode-map (kbd "C-c t") #'my/org-toggle-fold))

  (provide 'core/tools)
  ;;; core/tools.el ends here
#+end_src

*** core/utils.el
:PROPERTIES:
:CUSTOM_ID: core-utils
:END:

#+begin_src emacs-lisp :tangle lisp/core/utils.el
  ;;; core/utils.el --- Core utility helpers -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: core
  ;;
  ;;; Code:

  (defun my/auto-tangle-updated-src-blocks ()
    "Automatically tangle updated Org source blocks when saving `README.org`."
    (when (and buffer-file-name (string= (file-name-nondirectory buffer-file-name) "README.org"))
      (let ((org-confirm-babel-evaluate nil)) (org-babel-tangle))))

  (add-hook 'org-mode-hook
            (lambda () (add-hook 'after-save-hook #'my/auto-tangle-updated-src-blocks nil 'make-it-local)))

  (defun my/revert-buffer-quick ()
    (interactive) (revert-buffer :ignore-auto :noconfirm))

  (defun my/auto-insert-lexical-binding ()
    "Insert `lexical-binding: t` in Emacs Lisp files under `no-littering-var-directory`."
    (when (and (stringp buffer-file-name)
               (boundp 'no-littering-var-directory)
               (string-prefix-p (expand-file-name no-littering-var-directory) (expand-file-name buffer-file-name))
               (string-match-p "\\.el\\'" buffer-file-name)
               (not (save-excursion (goto-char (point-min))
                                    (re-search-forward "lexical-binding" (line-end-position 5) t))))
      (save-excursion (goto-char (point-min)) (insert ";; -*- lexical-binding: t; -*- \n"))))
  (add-hook 'find-file-hook #'my/auto-insert-lexical-binding)

  (defun my/enable-view-mode-on-read-only ()
    (if buffer-read-only (view-mode 1) (view-mode -1)))
  (add-hook 'read-only-mode-hook #'my/enable-view-mode-on-read-only)

  (provide 'core/utils)
  ;;; core/utils.el ends here
#+end_src

*** core/history.el
:PROPERTIES:
:CUSTOM_ID: core-history
:END:

#+begin_src emacs-lisp :tangle lisp/core/history.el
  ;;; core/history.el --- Session persistence & autorevert -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: core
  ;;
  ;;; Code:

  (leaf saveplace :straight nil
    :init
    (setq save-place-file (concat no-littering-var-directory "saveplace"))
    (save-place-mode +1))

  (leaf recentf :straight nil
    :init
    (setq recentf-max-saved-items 100
          recentf-save-file (concat no-littering-var-directory "recentf"))
    (recentf-mode +1))

  (leaf savehist
    :straight nil
    :global-minor-mode t
    :config
    (setq savehist-file (concat no-littering-var-directory "history"))
    (my/ensure-directory-exists (file-name-directory savehist-file))
    (add-to-list 'savehist-additional-variables 'my:desktop-ask-on-restore))

  (provide 'core/history)
  ;;; core/history.el ends here
#+end_src

*** core/editing.el
:PROPERTIES:
:CUSTOM_ID: core-editing
:END:

#+begin_src emacs-lisp :tangle lisp/core/editing.el
  ;;; core/editing.el --- Editing helpers & UX aids -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: core
  ;;
  ;;; Code:

  (leaf tramp
    :straight nil
    :pre-setq
    `((tramp-persistency-file-name . ,(concat no-littering-var-directory "tramp"))
      (tramp-auto-save-directory   . ,(concat no-littering-var-directory "tramp-autosave")))
    :custom
    '((tramp-default-method . "scp")
      (tramp-verbose        . 3)))

  ;; Single source of truth for auto-save-visited.
  (setopt auto-save-visited-interval 1
          auto-save-default        nil)
  (when (fboundp 'auto-save-visited-mode)
    (auto-save-visited-mode 1))

  (leaf paredit :straight t
    :hook (emacs-lisp-mode . (lambda ()
                               (enable-paredit-mode)
                               (electric-pair-local-mode -1))))

  (leaf paren :straight nil
    :custom ((show-paren-delay . 0)
             (show-paren-style . 'expression)
             (show-paren-highlight-openparen . t))
    :global-minor-mode show-paren-mode)

  (leaf puni :straight t
    :global-minor-mode puni-global-mode
    :hook ((minibuffer-setup . (lambda () (puni-global-mode -1)))))

  (leaf undo-fu :straight t
    :custom ((undo-fu-allow-undo-in-region . t)))

  (leaf vundo :straight t
    :bind (("C-c u" . vundo)))

  (leaf ace-window :straight t
    :custom ((aw-keys . '(?a ?s ?d ?f ?g ?h ?j ?k ?l))
             (aw-scope . 'frame)
             (aw-background . t))
    :config (ace-window-display-mode 1))

  (leaf visual-line-mode :straight nil
    :hook (text-mode . visual-line-mode))

  (leaf dired-filter :straight t)
  (leaf dired-subtree :straight t :after dired)

  ;; Dired listing/ls settings centralized here.
  (leaf dired :straight nil
    :bind (:dired-mode-map
           ("i"   . dired-subtree-insert)
           ("TAB" . dired-subtree-toggle)
           ("z"   . my/dired-view-file-other-window))
    :config
    (if (and (eq system-type 'darwin) (executable-find "gls"))
        (progn
          (setq insert-directory-program "gls"
                dired-use-ls-dired t
                dired-listing-switches "-aBhl --group-directories-first"))
      (setq dired-use-ls-dired nil
            dired-listing-switches "-alh")))

  (leaf expand-region :straight t :after treesit)
  (leaf aggressive-indent :straight t :hook (prog-mode . aggressive-indent-mode))
  (leaf delsel :straight nil :global-minor-mode delete-selection-mode)

  (leaf autorevert :straight nil
    :custom ((auto-revert-interval . 2)
             (auto-revert-verbose . nil))
    :global-minor-mode global-auto-revert-mode)

  (leaf transient
    :straight t
    :config
    (setq transient-history-file (concat no-littering-var-directory "transient/history.el")
          transient-levels-file  (concat no-littering-var-directory "transient/levels.el")
          transient-values-file  (concat no-littering-var-directory "transient/values.el"))
    (my/ensure-directory-exists (concat no-littering-var-directory "transient/")))

  (provide 'core/editing)
  ;;; core/editing.el ends here
#+end_src

*** core/switches.el
:PROPERTIES:
:CUSTOM_ID: core-switches
:END:

#+begin_src emacs-lisp :tangle lisp/core/switches.el
  ;;; core/switches.el --- Unified feature switches (UI/LSP) -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: core
  ;;
  ;;; Commentary:
  ;; Switcher for UI bundles (doom/nano) and LSP backends (eglot/lsp).
  ;;
  ;;; Code:

  (eval-when-compile (require 'subr-x))

  (when (boundp 'my:use:modules)
    (when (or (not (boundp 'my:use-ui)) (eq my:use-ui 'none))
      (setq my:use-ui my:use:modules)))
  (define-obsolete-variable-alias 'my:use:modules 'my:use-ui "2025-10-11")

  (defgroup my:switches nil "Unified switches for UI and LSP." :group 'convenience)

  (defcustom my:use-lsp 'eglot
    "Which LSP client to use. One of: `eglot`, `lsp`."
    :type '(choice (const eglot) (const lsp))
    :group 'my:switches)

  (defcustom my:use-ui 'none
    "Which UI bundle to use. One of: `none`, `doom`, `nano`."
    :type '(choice (const none) (const doom) (const nano))
    :group 'my:switches)

  (autoload 'my/ui-enable-doom  "ui/ui-doom-modeline" "Enable Doom UI bundle." t)
  (autoload 'my/ui-enable-nano  "ui/ui-nano-modeline" "Enable Nano UI bundle." t)
  (autoload 'my/lsp-enable-eglot   "dev/dev-lsp-eglot" "Enable Eglot LSP." t)
  (autoload 'my/lsp-enable-lspmode "dev/dev-lsp-mode"  "Enable lsp-mode LSP." t)

  (defun my/sw--present-p (kind choice)
    (pcase kind
      ('ui (pcase choice
             ('doom (or (fboundp 'my/ui-enable-doom)
                        (locate-library "ui/ui-doom-modeline")
                        (locate-library "doom-modeline")))
             ('nano (or (fboundp 'my/ui-enable-nano)
                        (locate-library "ui/ui-nano-modeline")
                        (locate-library "nano-modeline")))
             (_ t)))
      ('lsp (pcase choice
              ('eglot (or (fboundp 'my/lsp-enable-eglot)
                          (locate-library "dev/dev-lsp-eglot")
                          (locate-library "eglot")))
              ('lsp   (or (fboundp 'my/lsp-enable-lspmode)
                          (locate-library "dev/dev-lsp-mode")
                          (locate-library "lsp-mode")))
              (_ nil)))
      (_ nil)))

  (defun my/sw--enable-ui (choice)
    (pcase choice
      ('doom (cond
              ((fboundp 'my/ui-enable-doom) (my/ui-enable-doom) t)
              ((locate-library "ui/ui-doom-modeline")
               (load (locate-library "ui/ui-doom-modeline") nil 'nomessage)
               (when (fboundp 'my/ui-enable-doom) (my/ui-enable-doom) t))
              (t (message "[switches] Doom UI not found.") nil)))
      ('nano (cond
              ((fboundp 'my/ui-enable-nano) (my/ui-enable-nano) t)
              ((locate-library "ui/ui-nano-modeline")
               (load (locate-library "ui/ui-nano-modeline") nil 'nomessage)
               (when (fboundp 'my/ui-enable-nano) (my/ui-enable-nano) t))
              (t (message "[switches] Nano UI not found.") nil)))
      ('none (message "[switches] UI bundle disabled.") t)
      (_ (message "[switches] Unknown UI choice: %s" choice) nil)))

  (defun my/sw--enable-lsp (choice)
    (pcase choice
      ('eglot (cond
               ((fboundp 'my/lsp-enable-eglot) (my/lsp-enable-eglot) t)
               ((locate-library "dev/dev-lsp-eglot")
                (load (locate-library "dev/dev-lsp-eglot") nil 'nomessage)
                (when (fboundp 'my/lsp-enable-eglot) (my/lsp-enable-eglot) t))
               (t (message "[switches] Eglot setup not found.") nil)))
      ('lsp (cond
             ((fboundp 'my/lsp-enable-lspmode) (my/lsp-enable-lspmode) t)
             ((locate-library "dev/dev-lsp-mode")
              (load (locate-library "dev/dev-lsp-mode") nil 'nomessage)
              (when (fboundp 'my/lsp-enable-lspmode) (my/lsp-enable-lspmode) t))
             (t (message "[switches] lsp-mode setup not found.") nil)))
      (_ (message "[switches] Unknown LSP choice: %s" choice) nil)))

  (when (not (eq my:use-ui 'none))
    (let ((present (my/sw--present-p 'ui my:use-ui)))
      (cond
       ((my/sw--enable-ui my:use-ui) (message "[switches] UI bundle: %s" my:use-ui))
       (present (message "[switches] UI seems present but could not enable: %s" my:use-ui))
       (t (message "[switches] UI bundle unavailable: %s" my:use-ui)))))

  (let ((present (my/sw--present-p 'lsp my:use-lsp)))
    (cond
     ((my/sw--enable-lsp my:use-lsp) (message "[switches] LSP backend: %s" my:use-lsp))
     (present (message "[switches] LSP seems present but could not enable: %s" my:use-lsp))
     (t (message "[switches] LSP backend unavailable: %s" my:use-lsp))))

  (provide 'core/switches)
  ;;; core/switches.el ends here
#+end_src

*** core/custom.el
:PROPERTIES:
:CUSTOM_ID: custom-file
:END:

#+begin_src emacs-lisp :tangle lisp/core/custom.el
  ;;; core/custom.el --- custom-file helpers -*- lexical-binding: t; -*-
  ;;
  ;; Category: core
  ;;
  ;;; Commentary:
  ;; - Route customize output to .etc/custom.el
  ;; - Provide helpers to open and (optionally) dump current values/faces.

  (eval-when-compile (require 'subr-x))

  (defconst my:f:custom
    (or (bound-and-true-p my:f:custom)
        (expand-file-name "custom.el" (or (bound-and-true-p my:d:etc)
  					(expand-file-name ".etc" user-emacs-directory))))
    "Path to the custom-file (Customize output).")

  (defun my/custom--ensure-file ()
    "Ensure `custom-file` exists and has a small header."
    (let* ((dir (file-name-directory my:f:custom)))
      (unless (file-directory-p dir)
        (condition-case err
            (make-directory dir t)
          (error (warn "[custom] failed to create %s: %s" dir (error-message-string err)))))
      (unless (file-exists-p my:f:custom)
        (with-temp-file my:f:custom
          (insert ";;; custom.el --- Customize output -*- lexical-binding: t; -*-\n"
                  ";; This file is generated by Customize. Edit with care.\n\n")))))

  ;; 1) Route Customize output to .etc/custom.el (idempotent)
  (setq custom-file my:f:custom)
  (my/custom--ensure-file)
  (when (file-readable-p custom-file)
    (ignore-errors (load custom-file nil 'nomessage)))

  ;;;###autoload
  (defun my/custom-open ()
    "Open the `custom-file`."
    (interactive)
    (my/custom--ensure-file)
    (find-file my:f:custom))

  ;;;###autoload
  (defun my/custom-dump-current ()
    "Persist a curated set of current settings/faces into `custom-file`.
  This is a one-shot snapshot; it won't run automatically."
    (interactive)
    (my/custom--ensure-file)

    ;; ---- Variables you want to persist (add/remove as you like) ----
    (dolist (pair
             `(
               ;; Core UI
               (inhibit-startup-screen             . ,inhibit-startup-screen)
               (frame-resize-pixelwise             . ,(bound-and-true-p frame-resize-pixelwise))
               (display-line-numbers-type          . ,(and (boundp 'display-line-numbers-type) display-line-numbers-type))
               ;; Completion
               (completion-styles                  . ,(and (boundp 'completion-styles) completion-styles))
               (completion-category-overrides      . ,(and (boundp 'completion-category-overrides) completion-category-overrides))
               ;; Org (selected)
               (org-startup-indented               . ,(and (boundp 'org-startup-indented) org-startup-indented))
               (org-hide-leading-stars             . ,(and (boundp 'org-hide-leading-stars) org-hide-leading-stars))
               (org-tags-column                    . ,(and (boundp 'org-tags-column) org-tags-column))
               (org-agenda-tags-column             . ,(and (boundp 'org-agenda-tags-column) org-agenda-tags-column))
               ;; org-modern (selected)
               (org-modern-list                    . ,(and (boundp 'org-modern-list) org-modern-list))
               (org-modern-checkbox                . ,(and (boundp 'org-modern-checkbox) org-modern-checkbox))
               (org-modern-priority                . ,(and (boundp 'org-modern-priority) org-modern-priority))
               (org-modern-replace-stars           . ,(and (boundp 'org-modern-replace-stars) org-modern-replace-stars))
               (org-modern-todo-faces              . ,(and (boundp 'org-modern-todo-faces) org-modern-todo-faces))
               ))
      ;; Only save bound values; skip nil/unbound
      (when (car (last pair))
        (customize-save-variable (car pair) (cdr pair))))

    ;; ---- Faces you want to persist (add/remove as you like) ----
    (let ((faces
           '(
             (org-modern-date-active   ((t (:background "#373844" :foreground "#f8f8f2" :height 0.75 :weight light :width condensed))))
             (org-modern-time-active   ((t (:background "#44475a" :foreground "#f8f8f2" :height 0.75 :weight light :width condensed))))
             (org-modern-date-inactive ((t (:background "#373844" :foreground "#b0b8d1" :height 0.75 :weight light :width condensed))))
             (org-modern-time-inactive ((t (:background "#44475a" :foreground "#b0b8d1" :height 0.75 :weight light :width condensed))))
             (org-modern-tag           ((t (:background "#44475a" :foreground "#b0b8d1" :height 0.75 :weight light :width condensed))))
             (org-modern-statistics    ((t (:foreground "violet" :weight light))))
             )))
      (dolist (f faces)
        (let ((face (nth 0 f)) (spec (nth 1 f)))
          (custom-set-faces `(,face ,spec)))))
    ;; Actually write
    (custom-save-all)
    (message "[custom] Wrote current snapshot to %s" my:f:custom))

  (provide 'core/custom)
  ;;; core/custom.el ends here
#+end_src

**** Wiring
- If your *early-init.el* / *init.el* already points `custom-file` to `.etc/custom.el`, you’re good.
- Ensure `core/custom` is in the module list (e.g. `lisp/modules.el` → add `core/custom` to `my:modules`).

**** Usage
- `M-x my/custom-open` — open `.etc/custom.el`
- `M-x my/custom-dump-current` — write a one-time snapshot of selected settings/faces to `.etc/custom.el` (it does **not** run at startup)

*** ui/ui-font.el
:PROPERTIES:
:CUSTOM_ID: ui-font
:END:

#+begin_src emacs-lisp :tangle lisp/ui/ui-font.el
  ;;; ui/ui-font.el --- Font configuration -*- lexical-binding: t; -*-

  ;; Ensure the leaf macro is available at compile time here too.
  (eval-when-compile (require 'leaf))

  (defun my/system-default-font ()
    "Return a default monospace font family depending on OS."
    (cond ((eq system-type 'darwin) "Menlo")
          ((eq system-type 'gnu/linux) "Monospace")
          ((eq system-type 'windows-nt) "Consolas")
          (t "Monospace")))

  (defun my/system-emoji-font ()
    "Return a default emoji font family depending on OS."
    (cond ((eq system-type 'darwin) "Apple Color Emoji")
          ((eq system-type 'gnu/linux) "Noto Color Emoji")
          ((eq system-type 'windows-nt) "Segoe UI Emoji")
          (t "Noto Color Emoji")))

  (defun my/font-setup ()
    "Apply font settings, respecting overrides, and report applied fonts."
    (when (display-graphic-p)
      (set-face-attribute 'default nil
                          :family (or my:font-default (my/system-default-font))
                          :height (* 10 (or my:font-size 16)))
      (message "[font] default: %s, %.1f pt"
               (face-attribute 'default :family)
               (/ (float (face-attribute 'default :height)) 16))
      (set-face-attribute 'variable-pitch nil
                          :family (or my:font-alt (my/system-default-font)))
      (message "[font] variable-pitch: %s"
               (face-attribute 'variable-pitch :family))
      (set-fontset-font t 'emoji
                        (font-spec :family (or my:emoji-font (my/system-emoji-font))))
      (message "[font] emoji: %s"
               (or my:emoji-font (my/system-emoji-font)))))

  (defun my/show-current-font () (interactive)
    "Echo the current default font family and point size."
    (let* ((family (face-attribute 'default :family))
           (height (face-attribute 'default :height))
           (pt (/ height 10.0)))
      (message "Current font: %s, %.1f pt" family pt)))

  (if (daemonp)
      (add-hook 'after-make-frame-functions
                (lambda (frame) (with-selected-frame frame (my/font-setup))))
    (add-hook 'after-init-hook #'my/font-setup))

  ;; OPTIONAL ligatures — install & enable only when available/GUI.
  (leaf ligature
    :straight (ligature :type git :host github :repo "mickeynp/ligature.el")
    :when (display-graphic-p)
    :require t
    :config
    (when (fboundp 'ligature-set-ligatures)
      (ligature-set-ligatures 'prog-mode '("->" "=>" "::" "===" "!=" "&&" "||"))
      (global-ligature-mode 1)))

  (provide 'ui/ui-font)
  ;;; ui/ui-font.el ends here
#+end_src

*** ui/ui-theme.el
:PROPERTIES:
:CUSTOM_ID: ui-theme
:END:

#+begin_src emacs-lisp :tangle lisp/ui/ui-theme.el
  ;;; ui/ui-theme.el --- Theme configuration -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: ui
  ;;
  ;; Commentary:
  ;; ef-themes + spacious-padding with a face normalizer for Emacs 30+.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (setq-default line-spacing 0.24)

  ;; nano-faces/nano-base-colors → nano-theme → nano-modeline
  (leaf nano-emacs
    :straight (nano-emacs :type git :host github :repo "rougier/nano-emacs")
    :preface
    (defun my-nano-setup-colors ()
      "Setup custom nano colors."
      (setq nano-color-background "#fafafa")
      (setq nano-color-foreground "#374151")
      (setq nano-color-salient "#2563eb")
      (setq nano-color-popout "#f59e0b")
      (setq nano-color-critical "#dc2626")
      (setq nano-color-strong "#111827")
      (setq nano-color-faded "#9ca3af")
      (setq nano-color-subtle "#f3f4f6"))
    :config
    (require 'nano-layout)
    (my-nano-setup-colors)
    (require 'nano-faces)
    (nano-faces)
    (set-face-attribute 'nano-face-strong nil
                        :foreground (face-foreground 'nano-face-default)
                        :weight 'bold)
    (leaf nano-theme
      :straight (nano-theme :type git :host github :repo "rougier/nano-theme")
      :config
      (load-theme 'nano t)
      (set-face-attribute 'bold nil :weight 'bold)
      (set-face-attribute 'italic nil :slant 'italic)))

  (leaf spacious-padding :straight t
    :if (display-graphic-p)
    :custom ((spacious-padding-widths . '((left . 15) (right . 15)))
             (spacious-padding-subtle-mode-line . t))
    :config (spacious-padding-mode 1))

  (provide 'ui/ui-theme)
  ;;; ui/ui-theme.el ends here
#+end_src

*** ui/ui-doom-modeline.el
:PROPERTIES:
:CUSTOM_ID: ui-doom-modeline
:END:

#+begin_src emacs-lisp :tangle lisp/ui/ui-doom-modeline.el
  ;;; ui-doom-modeline.el --- Doom UI bundle entry -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: ui
  ;;
  ;;; Commentary:
  ;; Enable doom-modeline (with nerd-icons) as a compact UI bundle.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))
  (declare-function doom-modeline-mode "doom-modeline")

  (leaf nerd-icons :straight t)

  (leaf doom-modeline
    :straight t
    :custom ((doom-modeline-height . 28)
             (doom-modeline-buffer-file-name-style . 'truncate-with-project)
             (doom-modeline-minor-modes . nil)
             (doom-modeline-enable-word-count . t))
    :config
    (doom-modeline-mode 1))

  ;;;###autoload
  (defun my/ui-enable-doom ()
    "Enable Doom UI modeline bundle."
    (interactive)
    (unless (bound-and-true-p doom-modeline-mode)
      (doom-modeline-mode 1))
    (message "[ui] Doom modeline enabled."))

  (provide 'ui/ui-doom-modeline)
  ;;; ui/ui-doom-modeline.el ends here
#+end_src

*** ui/ui-nano-modeline.el
:PROPERTIES:
:CUSTOM_ID: ui-nano-modeline
:END:

#+begin_src emacs-lisp :tangle lisp/ui/ui-nano-modeline.el
  ;;; ui-nano-modeline.el --- Nano UI bundle entry -*- lexical-binding: t; -*-
  ;;
  ;; Category: ui
  ;;
  ;;; Commentary:
  ;; Robust loader that tries to require `nano-modeline` and wires all mode hooks.
  ;; Safe even if the package is not available at build time.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (declare-function nano-modeline-mode              "nano-modeline")
  (declare-function nano-modeline-prog-mode         "nano-modeline")
  (declare-function nano-modeline-text-mode         "nano-modeline")
  (declare-function nano-modeline-org-mode          "nano-modeline")
  (declare-function nano-modeline-pdf-mode          "nano-modeline")
  (declare-function nano-modeline-mu4e-headers-mode "nano-modeline")
  (declare-function nano-modeline-mu4e-message-mode "nano-modeline")
  (declare-function nano-modeline-elfeed-entry-mode "nano-modeline")
  (declare-function nano-modeline-elfeed-search-mode "nano-modeline")
  (declare-function nano-modeline-term-mode         "nano-modeline")
  (declare-function nano-modeline-xwidget-mode      "nano-modeline")
  (declare-function nano-modeline-message-mode      "nano-modeline")
  (declare-function nano-modeline-org-capture-mode  "nano-modeline")
  (declare-function nano-modeline-org-agenda-mode   "nano-modeline")

  (defvar ui--nano-modeline-initialized nil
    "Non-nil once nano-modeline hooks have been installed.")

  (defun my/ui--nano-available-p ()
    "Return non-nil if `nano-modeline` can be required."
    (require 'nano-modeline nil 'noerror))

  (defun my/ui--nano-setup ()
    "Install nano-modeline hooks once (idempotent)."
    (when (and (not ui--nano-modeline-initialized)
               (my/ui--nano-available-p))
      (setopt nano-modeline-padding '(0.20 . 0.25))

      (when (fboundp 'nano-modeline-text-mode)
        (nano-modeline-text-mode t))

      (when (fboundp 'nano-modeline-prog-mode)
        (add-hook 'prog-mode-hook            #'nano-modeline-prog-mode))
      (when (fboundp 'nano-modeline-text-mode)
        (add-hook 'text-mode-hook            #'nano-modeline-text-mode))
      (when (fboundp 'nano-modeline-org-mode)
        (add-hook 'org-mode-hook             #'nano-modeline-org-mode))
      (when (fboundp 'nano-modeline-pdf-mode)
        (add-hook 'pdf-view-mode-hook        #'nano-modeline-pdf-mode))
      (when (fboundp 'nano-modeline-mu4e-headers-mode)
        (add-hook 'mu4e-headers-mode-hook    #'nano-modeline-mu4e-headers-mode))
      (when (fboundp 'nano-modeline-mu4e-message-mode)
        (add-hook 'mu4e-view-mode-hook       #'nano-modeline-mu4e-message-mode))
      (when (fboundp 'nano-modeline-elfeed-entry-mode)
        (add-hook 'elfeed-show-mode-hook     #'nano-modeline-elfeed-entry-mode))
      (when (fboundp 'nano-modeline-elfeed-search-mode)
        (add-hook 'elfeed-search-mode-hook   #'nano-modeline-elfeed-search-mode))
      (when (fboundp 'nano-modeline-term-mode)
        (add-hook 'term-mode-hook            #'nano-modeline-term-mode))
      (when (fboundp 'nano-modeline-xwidget-mode)
        (add-hook 'xwidget-webkit-mode-hook  #'nano-modeline-xwidget-mode))
      (when (fboundp 'nano-modeline-message-mode)
        (add-hook 'messages-buffer-mode-hook #'nano-modeline-message-mode))
      (when (fboundp 'nano-modeline-org-capture-mode)
        (add-hook 'org-capture-mode-hook     #'nano-modeline-org-capture-mode))
      (when (fboundp 'nano-modeline-org-agenda-mode)
        (add-hook 'org-agenda-mode-hook      #'nano-modeline-org-agenda-mode))

      (setq ui--nano-modeline-initialized t)
      (message "[ui] nano-modeline initialized.")))

  (leaf nano-modeline
    :straight (nano-modeline :type git :host github :repo "rougier/nano-modeline")
    :after nano-emacs
    :require nil
    :init
    (add-hook 'after-init-hook #'my/ui--nano-setup)
    :hook
    (prog-mode-hook . nano-modeline-prog-mode)
    (text-mode-hook . nano-modeline-text-mode))

  ;;;###autoload
  (defun my/ui-enable-nano ()
    "Enable Nano UI modeline bundle (guarded)."
    (interactive)
    (if (my/ui--nano-available-p)
        (progn
          (my/ui--nano-setup)
          (message "[ui] nano-modeline enabled."))
      (message "[ui] nano-modeline not available; will enable after it is installed.")))

  (provide 'ui/ui-nano-modeline)
  ;;; ui/ui-nano-modeline.el ends here
#+end_src

*** ui/ui-window.el
:PROPERTIES:
:CUSTOM_ID: ui-window
:END:

#+begin_src emacs-lisp :tangle lisp/ui/ui-window.el
  ;;; ui/ui-window.el --- Window management -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: ui
  ;;
  ;;; Code:

  (leaf zoom :straight t
    :hook (after-init-hook . zoom-mode)
    :custom ((zoom-size . '(0.62 . 0.62))
             (zoom-ignored-major-modes . '(dired-mode treemacs-mode))
             (zoom-ignored-buffer-names . '("*Messages*" "*Help*"))))

  (leaf desktop
    :straight nil
    :config
    (let* ((dir (concat no-littering-var-directory "desktop/")))
      (setq desktop-dirname            dir
            desktop-path               (list dir)
            desktop-base-file-name     "desktop"
            desktop-base-lock-name     "lock"
            desktop-restore-eager      10
            desktop-save               t
            desktop-load-locked-desktop nil
            desktop-auto-save-timeout  300)
      (my/ensure-directory-exists dir)
      (desktop-save-mode 1)))

  (leaf winner
    :straight nil
    :global-minor-mode t
    :bind (("M-[" . winner-undo)
           ("M-]" . winner-redo)))

  (defvar my:saved-window-config nil)

  (defun my/save-window-layout () (interactive)
    (setq my:saved-window-config (window-state-get nil t))
    (message "Window configuration saved."))

  (defun my/restore-window-layout () (interactive)
    (if my:saved-window-config
        (window-state-put my:saved-window-config)
      (message "No saved window configuration found.")))

  (provide 'ui/ui-window)
  ;;; ui/ui-window.el ends here
#+end_src

*** ui/ui-utils.el
:PROPERTIES:
:CUSTOM_ID: ui-utils
:END:

#+begin_src emacs-lisp :tangle lisp/ui/ui-utils.el
  ;;; ui/ui-utils.el --- Treemacs configuration & UI utils -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: ui
  ;;
  ;;; Code:

  (leaf minions :straight t
    :custom ((minions-mode-line-lighter . "⚙"))
    :hook (after-init-hook . minions-mode))

  (setq display-time-interval 30
        display-time-day-and-date t
        display-time-24hr-format t)
  (display-time-mode 1)
  (when (fboundp 'display-battery-mode) (display-battery-mode 1))

  (leaf treemacs :straight t
    :if (display-graphic-p)
    :custom ((treemacs-filewatch-mode . t)
             (treemacs-follow-mode . t)
             (treemacs-indentation . 2)
             (treemacs-missing-project-action . 'remove)))

  (leaf nerd-icons-dired :straight t
    :hook (dired-mode . nerd-icons-dired-mode))

  (leaf pbcopy
    :if (memq window-system '(mac ns))
    :straight t
    :config (turn-on-pbcopy))

  (provide 'ui/ui-utils)
  ;;; ui/ui-utils.el ends here
#+end_src

*** completion/completion-core.el
:PROPERTIES:
:CUSTOM_ID: completion-core
:END:

#+begin_src emacs-lisp :tangle lisp/completion/completion-core.el
  ;;; completion-core.el --- Completion core settings -*- lexical-binding: t; -*-
  ;;
  ;; Category: completion
  ;;
  (eval-when-compile (require 'leaf))

  (leaf orderless
    :straight t
    :custom
    ((completion-styles . '(orderless basic))
     (completion-category-overrides
      . '((file (styles . (partial-completion)))
          (symbol (styles . (orderless)))
          (command (styles . (orderless)))))))

  (provide 'completion/completion-core)
#+end_src

*** completion/completion-vertico.el
:PROPERTIES:
:CUSTOM_ID: completion-vertico
:END:

#+begin_src emacs-lisp :tangle lisp/completion/completion-vertico.el
  ;;; completion-vertico.el --- Vertico minibuffer UI -*- lexical-binding: t; -*-
  ;;
  ;; Category: completion
  ;;
  (eval-when-compile (require 'leaf))

  (leaf vertico :straight t
    :global-minor-mode vertico-mode
    :custom ((vertico-count . 15)))

  (leaf vertico-posframe :straight t
    :if (display-graphic-p)
    :after vertico
    :custom ((vertico-posframe-border-width . 2))
    :config (vertico-posframe-mode 1))

  (leaf marginalia :straight t
    :global-minor-mode marginalia-mode)

  (provide 'completion/completion-vertico)
#+end_src

*** completion/completion-consult.el
:PROPERTIES:
:CUSTOM_ID: completion-consult
:END:

#+begin_src emacs-lisp :tangle lisp/completion/completion-consult.el
  ;;; completion-consult.el --- Consult search/navigation -*- lexical-binding: t; -*-
  ;;
  ;; Category: completion
  ;;
  (eval-when-compile (require 'leaf))

  (leaf consult :straight t
    :custom
    ((xref-show-xrefs-function . #'consult-xref)
     (xref-show-definitions-function . #'consult-xref)))

  (provide 'completion/completion-consult)
#+end_src

*** completion/completion-embark.el
:PROPERTIES:
:CUSTOM_ID: completion-embark
:END:

#+begin_src emacs-lisp :tangle lisp/completion/completion-embark.el
  ;;; completion-embark.el --- Embark actions -*- lexical-binding: t; -*-
  ;;
  ;; Category: completion
  ;;
  (eval-when-compile (require 'leaf))

  (leaf embark :straight t
    :custom ((prefix-help-command . #'embark-prefix-help-command)))

  (leaf embark-consult :straight t
    :after (embark consult)
    :hook (embark-collect-mode . consult-preview-at-point-mode))

  (provide 'completion/completion-embark)
#+end_src

*** completion/completion-corfu.el
:PROPERTIES:
:CUSTOM_ID: completion-corfu
:END:

#+begin_src emacs-lisp :tangle lisp/completion/completion-corfu.el
  ;;; completion/completion-corfu.el --- Corfu popup completion module -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: completion
  ;;
  ;;; Commentary:
  ;; Corfu-based in-buffer completion with sensible defaults.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf corfu :straight t
    :init
    (setq tab-always-indent 'complete)
    (global-corfu-mode)
    :custom ((corfu-auto . t)
             (corfu-cycle . t))
    :bind (:corfu-map
           ("TAB"     . corfu-next)
           ([tab]     . corfu-next)
           ("S-TAB"   . corfu-previous)
           ([backtab] . corfu-previous)
           ("RET"     . corfu-insert)))

  (leaf kind-icon :straight t
    :after corfu
    :custom ((kind-icon-default-face . 'corfu-default))
    :config (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))

  (leaf cape :straight t
    :init
    (mapc (lambda (fn) (add-to-list 'completion-at-point-functions fn))
          '(cape-file cape-dabbrev cape-keyword)))

  (provide 'completion/completion-corfu)
  ;;; completion/completion-corfu.el ends here
#+end_src

*** completion/completion-icons.el
:PROPERTIES:
:CUSTOM_ID: completion-corfu
:END:

#+begin_src emacs-lisp :tangle lisp/completion/completion-icons.el
  ;;; completion-icons.el --- Nerd icons for completion -*- lexical-binding: t; -*-
  ;;
  ;; Category: completion
  ;;
  (eval-when-compile (require 'leaf))

  (leaf nerd-icons-ibuffer :straight t
    :hook (ibuffer-mode-hook . nerd-icons-ibuffer-mode))

  (leaf nerd-icons-completion :straight t
    :hook (marginalia-mode-hook . nerd-icons-completion-marginalia-setup)
    :config (nerd-icons-completion-mode))

  (provide 'completion/completion-icons)
#+end_src

*** orgx/org-core.el
:PROPERTIES:
:CUSTOM_ID: orgx-core
:END:

#+begin_src emacs-lisp :tangle lisp/orgx/org-core.el
  ;;; orgx/org-core.el --- Org Mode core configuration -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: org
  ;;
  ;;; Code:

  (eval-when-compile
    (require 'leaf)
    (require 'cl-lib))

  (defvar my:d:org (expand-file-name "org" my:d:var))
  (defvar my:d:org-journal (expand-file-name "journal" my:d:org))
  (defvar my:d:org-roam (expand-file-name "org-roam" my:d:org))
  (defvar my:d:org-pictures (expand-file-name "pictures" my:d:org))
  (defvar my:f:capture-blog-file (expand-file-name "blog.org" my:d:org))

  (my/ensure-directory-exists my:d:org)
  (my/ensure-directory-exists my:d:org-journal)
  (my/ensure-directory-exists my:d:org-roam)
  (my/ensure-directory-exists my:d:org-pictures)

  (defun my/org-buffer-files ()
    "Return a list of *.org files currently visited in live buffers."
    (cl-loop for buf in (buffer-list)
             for file = (buffer-file-name buf)
             when (and file (string-match-p "\\.org\\'" file))
             collect file))

  (leaf org
    :straight nil
    :custom
    ((org-directory . my:d:org)
     (org-default-notes-file . "notes.org")
     (org-log-done . 'time)
     (org-support-shift-select . t)
     (org-return-follows-link . t))
    :config
    (setq org-agenda-files
          (seq-filter (lambda (file)
                        (and (string-match-p "\\.org$" file)
                             (not (string-match-p "archives" file))))
                      (directory-files-recursively org-directory "\\.org$")))
    (unless org-agenda-files
      (setq org-agenda-files (list (expand-file-name "inbox.org" org-directory))))
    (setq org-todo-keywords
          '((sequence "TODO(t)" "SOMEDAY(s)" "WAITING(w)" "|" "DONE(d)" "CANCELED(c@)")))
    (setq org-refile-targets
          '((nil :maxlevel . 3)
            (my/org-buffer-files :maxlevel . 1)
            (org-agenda-files :maxlevel . 3)))
    (setq org-capture-templates
          `(("t" "Todo" entry (file+headline ,(expand-file-name "gtd.org" org-directory) "Inbox")
             "* TODO %?\n %i\n %a")
            ("n" "Note" entry (file+headline ,(expand-file-name "notes.org" org-directory) "Notes")
             "* %?\nEntered on %U\n %i\n %a")
            ("j" "Journal" entry (function org-journal-find-location)
             "* %(format-time-string org-journal-time-format)%^{Title}\n%i%?")
            ("m" "Meeting" entry (file ,(expand-file-name "meetings.org" org-directory))
             "* MEETING with %? :meeting:\n  %U\n  %a"))))

  (with-eval-after-load 'org
    (let* ((central (expand-file-name "archive.org" (or (bound-and-true-p org-directory)
                                                       (expand-file-name "~/org")))))
      (setopt org-archive-location (concat central "::"))))

  (provide 'orgx/org-core)
  ;;; orgx/org-core.el ends here
#+end_src

*** orgx/org-visual.el
:PROPERTIES:
:CUSTOM_ID: orgx-visual
:END:

#+begin_src emacs-lisp :tangle lisp/orgx/org-visual.el
  ;;; orgx/org-visual.el --- Org Mode visual enhancements -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: org
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf org-modern
    :straight t
    :hook (org-mode . org-modern-mode)
    :custom
    ((org-startup-indented . t)
     (org-hide-leading-stars . t)
     (org-auto-align-tags . nil)
     (org-tags-column . 0)
     (org-catch-invisible-edits . 'show-and-error)
     (org-special-ctrl-a/e . t)
     (org-insert-heading-respect-content . t)
     (org-hide-emphasis-markers . t)
     (org-pretty-entities . t)
     ;; TODO faces (variable, not face)
     (org-modern-todo-faces
      . '(("TODO" :background "#673AB7" :foreground "#f8f8f2")
          ("WAIT" :background "#6272a4" :foreground "#f8f8f2")
          ("NEXT" :background "#f1fa8c" :foreground "#1E2029")
          ("DONE" :background "#373844" :foreground "#b0b8d1")))
     ;; Nerd Font icons
     (org-modern-list          . '((?+ . "◦") (?- . "–") (?* . "•")))
     (org-modern-checkbox      . '((?X . "") (?- . "") (?\s . "")))
     (org-modern-priority      . '((?A . "") (?B . "") (?C . "")))
     (org-modern-replace-stars . "")
     ;; Agenda
     (org-agenda-tags-column . 0)
     (org-agenda-block-separator . ?─)
     (org-agenda-time-grid
      . '((daily today require-timed)
          (800 1000 1200 1400 1600 1800 2000)
          " ┄┄┄┄┄ " " ┄┄┄┄┄ "))
     (org-agenda-current-time-string
      . "⭠ now ─────────────────────────────────────────────────")))

  (provide 'orgx/org-visual)
  ;;; org-visual.el ends here
#+end_src

*** orgx/org-extensions.el
:PROPERTIES:
:CUSTOM_ID: orgx-extensions
:END:

#+begin_src emacs-lisp :tangle lisp/orgx/org-extensions.el
  ;;; orgx/org-extensions.el --- Org Mode extensions -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: org
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf org-journal :straight t
    :custom ((org-journal-dir . my:d:org-journal)))

  (leaf org-roam :straight t
    :custom ((org-roam-directory . my:d:org-roam))
    :config
    (setq org-roam-db-location (expand-file-name "org-roam.db" my:d:org-roam))
    (org-roam-db-autosync-mode))

  (leaf org-download :straight t
    :custom ((org-download-image-dir . my:d:org-pictures)))

  (leaf toc-org :straight t
    :hook ((org-mode . toc-org-enable)
           (markdown-mode . toc-org-mode)))

  (leaf org-cliplink :straight t
    :bind ("C-x p i" . org-cliplink))

  (provide 'orgx/org-extensions)
  ;;; orgx/org-extensions.el ends here
#+end_src

*** orgx/org-export.el
:PROPERTIES:
:CUSTOM_ID: orgx-export
:END:

#+begin_src emacs-lisp :tangle lisp/orgx/org-export.el
  ;;; orgx/org-export.el --- Org export configuration -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: org
  ;;
  ;; Commentary:
  ;; Org export configuration: LaTeX, Hugo, Markdown.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf ox-hugo :straight t
    :after ox
    :custom ((org-hugo-front-matter-format . "toml")))

  (leaf *ox-hugo--capture
    :require (org-capture org-id)   ;; ensure both are available
    :preface
    ;; Pure Emacs Lisp, newline-free, portable
    (defun my/ox-hugo-generate-safe-filename ()
      "Return a safe Hugo filename like \"YYYYMMDD-xxxxxxxx\".
  It uses `org-id-uuid' and trims to 8 hex chars for stability."
      (let ((date (format-time-string "%Y%m%d"))
            (short (substring (org-id-uuid) 0 8))) ; no newline, pure Elisp
        (format "%s-%s" date short)))

    ;; Optional: where to capture (declare your path elsewhere if you prefer)
    ;; (defvar my:f:capture-blog-file "~/notes/blog.org")

    :config
    (defvar org-capture-templates) ; silence byte-compiler if needed

    ;; Insert (or replace) the "b" template
    (add-to-list
     'org-capture-templates
     `("b" "Blog (Hugo) - new post" entry
       (file+olp ,(or (bound-and-true-p my:f:capture-blog-file)
                      (user-error "Set `my:f:capture-blog-file' to your blog Org file"))
                 "blog")
       ,(concat
         "** TODO %^{Title}\n"
         ":PROPERTIES:\n"
         "  :EXPORT_FILE_NAME: %(my/ox-hugo-generate-safe-filename)\n"
         "  :EXPORT_DATE: %(format-time-string \"%Y-%m-%dT%T%z\")\n"
         "  :EXPORT_HUGO_TAGS: %^{Tags|emacs}\n"
         "  :EXPORT_HUGO_CATEGORIES: %^{Category|blog}\n"
         "  :EXPORT_HUGO_LASTMOD: %(format-time-string \"%Y-%m-%dT%T%z\")\n"
         "  :EXPORT_HUGO_CUSTOM_FRONT_MATTER: :pin false\n"
         "  :END:\n\n"
         "%?\n")
       :empty-lines 1 :prepend t)))

  (leaf markdown-mode :straight t)
  (leaf markdown-preview-mode :straight t)
  (leaf edit-indirect :straight t)

  (provide 'orgx/org-export)
  ;;; org-export.el ends here
#+end_src

*** vcs/vcs-magit.el
:PROPERTIES:
:CUSTOM_ID: vcs-magit
:END:

#+begin_src emacs-lisp :tangle lisp/vcs/vcs-magit.el
  ;;; vcs/vcs-magit.el --- Git integration with Magit -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: vcs
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf magit :straight t
    :bind (("C-x g" . magit-status)))

  (provide 'vcs/vcs-magit)
  ;;; vcs/vcs-magit.el ends here
#+end_src

*** vcs/vcs-gutter.el
:PROPERTIES:
:CUSTOM_ID: vcs-gutter
:END:

#+begin_src emacs-lisp :tangle lisp/vcs/vcs-gutter.el
  ;;; vcs/vcs-gutter.el --- Show Git changes in fringe -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: vcs
  ;;
  ;;; Commentary:
  ;; Use `diff-hl` as the sole fringe indicator. Integrates with Magit refresh.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf diff-hl
    :straight t
    :hook ((prog-mode . diff-hl-mode)
           (text-mode . diff-hl-mode)
           (dired-mode . diff-hl-dired-mode)
           (magit-post-refresh . diff-hl-magit-post-refresh))
    :custom ((diff-hl-draw-borders . nil)))

  (provide 'vcs/vcs-gutter)
  ;;; vcs-gutter.el ends here
#+end_src

*** vcs/vcs-forge.el
:PROPERTIES:
:CUSTOM_ID: vcs-forge
:END:

#+begin_src emacs-lisp :tangle lisp/vcs/vcs-forge.el
  ;;; vcs/vcs-forge.el --- GitHub/GitLab integration via Forge -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: vcs
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf forge
    :straight t
    :after magit
    :config
    (setq forge-database-file
          (expand-file-name "forge/forge-database.sqlite" no-littering-var-directory))
    (my/ensure-directory-exists (expand-file-name "forge" no-littering-var-directory)))

  (provide 'vcs/vcs-forge)
  ;;; vcs/vcs-forge.el ends here
#+end_src

*** dev/dev-lsp-eglot.el
:PROPERTIES:
:CUSTOM_ID: dev-lsp-eglot
:END:

#+begin_src emacs-lisp :tangle lisp/dev/dev-lsp-eglot.el
;;; dev-lsp-eglot.el --- Eglot setup -*- lexical-binding: t; -*-
;;
;; Copyright (c) 2021-2025
;; Author: YAMASHITA, Takao
;; License: GNU GPL v3 or later
;;
;; Category: dev
;;
;;; Commentary:
;; Eglot baseline with safe auto-enable only when a server is detectable.
;;
;;; Code:

(eval-when-compile (require 'leaf))

(leaf eglot
  :straight t
  :commands (eglot eglot-ensure)
  :custom ((eglot-autoreconnect . t))
  :hook ((prog-mode . (lambda ()
                        (when (fboundp 'eglot--guess-contact)
                          (when (eglot--guess-contact)
                            (eglot-ensure)))))))

;;;###autoload
(defun my/lsp-enable-eglot ()
  "Enable Eglot-based LSP setup."
  (interactive)
  (add-hook 'prog-mode-hook
            (lambda ()
              (when (fboundp 'eglot--guess-contact)
                (when (eglot--guess-contact)
                  (eglot-ensure)))))
  (message "[lsp] Eglot enabled."))

(provide 'dev/dev-lsp-eglot)
;;; dev-lsp-eglot.el ends here
#+end_src

*** dev/dev-lsp-mode.el
:PROPERTIES:
:CUSTOM_ID: dev-lsp-mode
:END:

#+begin_src emacs-lisp :tangle lisp/dev/dev-lsp-mode.el
;;; dev-lsp-mode.el --- lsp-mode setup -*- lexical-binding: t; -*-
;;
;; Copyright (c) 2021-2025
;; Author: YAMASHITA, Takao
;; License: GNU GPL v3 or later
;;
;; Category: dev
;;
;;; Commentary:
;; lsp-mode baseline + lsp-ui. Completion is delegated to Corfu.
;;
;;; Code:

(eval-when-compile (require 'leaf))

(leaf lsp-mode
  :straight t
  :commands (lsp lsp-deferred)
  :custom ((lsp-keymap-prefix . "C-c l")
           (lsp-enable-file-watchers . t)
           (lsp-file-watch-threshold . 5000)
           (lsp-response-timeout . 5)
           (lsp-diagnostics-provider . :auto)
           (lsp-completion-provider . :none))
  :hook ((prog-mode . lsp-deferred)))

(leaf lsp-ui
  :straight t
  :after lsp-mode
  :custom ((lsp-ui-doc-enable . t)
           (lsp-ui-doc-delay . 0.2)
           (lsp-ui-sideline-enable . t)))

;;;###autoload
(defun my/lsp-enable-lspmode ()
  "Enable lsp-mode-based LSP setup."
  (interactive)
  (add-hook 'prog-mode-hook #'lsp-deferred)
  (message "[lsp] lsp-mode enabled."))

(provide 'dev/dev-lsp-mode)
;;; dev-lsp-mode.el ends here
#+end_src

*** dev/dev-ai.el
:PROPERTIES:
:CUSTOM_ID: dev-ai
:END:

#+begin_src emacs-lisp :tangle lisp/dev/dev-ai.el
  ;;; dev/dev-ai.el --- AI-assisted development -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;; AI-assisted coding via Aidermacs.
  ;;
  ;; Purpose: Centralize aider/Aidermacs runtime files into ~/.var/aideremacs
  ;; Notes:
  ;; - Uses official AIDER_* env vars so both CLI and Aidermacs obey.
  ;; - Safe to load early (e.g. in personal/user.el or core/general.el).
  ;; - pip3 install aider-ce
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf)
  		   (require 'subr-x))

  ;; 1) Decide base directory: ~/.var/aideremacs
  (defvar my:d:aider
    (expand-file-name "aideremacs/" (or (bound-and-true-p my:d:var)
                                        (expand-file-name "~/.var/")))
    "Base directory to store aider runtime files.")

  ;; 2) Ensure directory exists
  (my/ensure-directory-exists my:d:aider)

  ;; 3) Point aider history files to this directory via env vars
  ;;    (aider recognizes these officially)
  (setenv "AIDER_INPUT_HISTORY_FILE" (expand-file-name "input.history" my:d:aider))
  (setenv "AIDER_CHAT_HISTORY_FILE"  (expand-file-name "chat.history.md" my:d:aider))
  (setenv "AIDER_LLM_HISTORY_FILE"   (expand-file-name "llm.history" my:d:aider))
  ;; Optional: analytics log location if you enable analytics logging
  (setenv "AIDER_ANALYTICS_LOG"      (expand-file-name "analytics.log" my:d:aider))

  ;; 4) [Optional] Keep a per-user .env here and make aider load it.
  ;;    You can create ~/.var/aideremacs/.env and put API keys / options there.
  ;;    If you use Aidermacs, pass --env-file via its extra args.
  (with-eval-after-load 'aidermacs
    ;; Aidermacs exposes an extra-args variable in recent builds
    ;; (see NonGNU ELPA diffs mentioning `aidermacs-extra-args`).
    ;; If your installed version still uses `aidermacs-args`, switch the name.
    (defvar aidermacs-extra-args nil)
    (let ((env-file (expand-file-name ".env" my:d:aider)))
      (when (file-exists-p env-file)
        (setq aidermacs-extra-args
              (append aidermacs-extra-args
                      (list "--env-file" env-file)))))

    ;; Redundancy is harmless: also force the same files via CLI flags,
    ;; in case your shell environment overrides Emacs' setenv.
    (setq aidermacs-extra-args
          (append aidermacs-extra-args
                  (list "--input-history-file" (getenv "AIDER_INPUT_HISTORY_FILE")
                        "--chat-history-file"  (getenv "AIDER_CHAT_HISTORY_FILE")
                        "--llm-history-file"   (getenv "AIDER_LLM_HISTORY_FILE")))))

  (leaf aidermacs :straight t
    :init
    (cond
     ((getenv "OPENROUTER_API_KEY")
      (setenv "OPENAI_API_BASE" "https://openrouter.ai/api/v1")
      (setenv "OPENAI_API_KEY"  (getenv "OPENROUTER_API_KEY"))
      (setopt aidermacs-default-model "openrouter/anthropic/claude-3.5-sonnet"))
     ((getenv "OPENAI_API_KEY")
      (setenv "OPENAI_API_BASE" "https://api.openai.com/v1")
      (setopt aidermacs-default-model "gpt-4o-mini"))
     (t
      (display-warning 'aidermacs
                       "No API keys set. Set OPENROUTER_API_KEY or OPENAI_API_KEY.")))
    (setopt aidermacs-retry-attempts 3
            aidermacs-retry-delay   2.0
            aidermacs-backend       'vterm))

  (provide 'dev/dev-ai)
  ;;; dev/dev-ai.el ends here
#+end_src

*** dev/dev-term.el
:PROPERTIES:
:CUSTOM_ID: dev-term
:END:

#+begin_src emacs-lisp :tangle lisp/dev/dev-term.el
  ;;; dev/dev-term.el --- Terminal integration -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;; Provides terminal integration with vterm.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf vterm
    :doc "Emacs libvterm integration"
    :url "https://github.com/akermu/emacs-libvterm"
    :straight t
    :bind
    (:vterm-mode-map
     ("C-c C-l"    . vterm-clear)
     ("C-o"        . previous-window-any-frame))
    :custom-face
    (vterm-color-red     . '((t (:foreground "#ff79c6" :background "#ff9ad4"))))
    (vterm-color-magenta . '((t (:foreground "#bd93f9" :background "#a9a1e1")))))

  (leaf vterm-toggle
    :doc "Toggles between the vterm buffer and whatever buffer you are editing."
    :url "https://github.com/jixiuf/vterm-toggle"
    :straight t
    :bind
    ("<f12>"    . vterm-toggle-cd-show)
    ("C-<f12>"  . vterm-toggle-show)
    (:vterm-mode-map
     ("<f12>"      . vterm-toggle-hide)
     ("C-<enter>"  . vterm-toggle-insert-cd)
     ("S-<right>"  . vterm-toggle-forward)
     ("S-<left>"   . vterm-toggle-backward))
    :custom
    (vterm-toggle-cd-auto-create-buffer . t)
    (vterm-toggle-fullscreen-p          . nil)
    (vterm-toggle-scope                 . 'project)
    :config
    (add-to-list 'display-buffer-alist
                 '((lambda (buffer-or-name _)
                     (let ((buffer (get-buffer buffer-or-name)))
                       (with-current-buffer buffer
                         (or (equal major-mode 'vterm-mode)
                             (string-prefix-p vterm-buffer-name (buffer-name buffer))))))
                   (display-buffer-reuse-window display-buffer-in-direction)
                   (direction       . bottom)
                   (dedicated       . t)
                   (reusable-frames . visible)
                   (window-height   . 0.3))))

  (provide 'dev/dev-term)
  ;;; dev/dev-term.el ends here
#+end_src

*** dev/dev-build.el
:PROPERTIES:
:CUSTOM_ID: dev-build
:END:

#+begin_src emacs-lisp :tangle lisp/dev/dev-build.el
  ;;; dev/dev-build.el --- Build & Makefile tools -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;; Build system integration.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf make-mode :straight nil
    :mode (("\\`Makefile\\'"    . makefile-gmake-mode)
           ("\\`GNUmakefile\\'" . makefile-gmake-mode)
           ("\\`makefile\\'"    . makefile-gmake-mode))
    :hook ((makefile-mode . (lambda ()
                              (setq-local indent-tabs-mode t
                                          tab-width 8
                                          show-trailing-whitespace t))))
    :config
    (leaf compile :straight nil
      :bind (("C-c c c" . compile)
             ("C-c c r" . recompile)
             ("C-c c p" . project-compile))
      :custom ((compilation-scroll-output . t)
               (compilation-skip-threshold . 2))
      :init
      (defun my/set-make-compile-command ()
        "Use `make -k` when Makefile is present."
        (when (or (derived-mode-p 'makefile-mode)
                  (locate-dominating-file default-directory "Makefile")
                  (locate-dominating-file default-directory "GNUmakefile"))
          (setq-local compile-command "make -k")))
      (add-hook 'after-change-major-mode-hook #'my/set-make-compile-command))
    (leaf ansi-color :straight nil
      :hook (compilation-filter . (lambda ()
                                    (let ((inhibit-read-only t))
                                      (ansi-color-apply-on-region compilation-filter-start (point-max)))))))

  (provide 'dev/dev-build)
  ;;; dev/dev-build.el ends here
#+end_src

*** dev/dev-docker.el
:PROPERTIES:
:CUSTOM_ID: dev-docker
:END:

#+begin_src emacs-lisp :tangle lisp/dev/dev-docker.el
  ;;; dev/dev-docker.el --- Docker integration -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;; Docker development support.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf dockerfile-mode :straight t
    :mode (("Dockerfile\\(\\..*\\)?\\'" . dockerfile-mode)
           ("\\.dockerfile\\'"         . dockerfile-mode))
    :custom ((dockerfile-mode-command . "docker")))

  (leaf yaml-mode :straight t
    :mode (("\\`docker-compose.*\\.ya?ml\\'" . yaml-mode)
           ("\\.ya?ml\\'"                   . yaml-mode)))

  (leaf docker :straight t
    :commands (docker docker-containers docker-images docker-volumes docker-networks)
    :bind (("C-c d d" . docker)
           ("C-c d c" . docker-containers)
           ("C-c d i" . docker-images)
           ("C-c d v" . docker-volumes)
           ("C-c d n" . docker-networks)))

  (leaf tramp-container :straight nil
    :after tramp
    :init
    (setq tramp-container-method "docker"))

  (leaf tempel :straight t
    :commands (tempel-insert)
    :bind (dockerfile-mode-map
           ("C-c d t" . tempel-insert))
    :init
    (with-eval-after-load 'tempel
      (defvar my:tempel-docker-templates
        '((dockerfile "FROM " p n
                      "WORKDIR /app" n
                      "COPY . /app" n
                      "RUN " p n
                      "CMD [" p "]" n)))
      (add-to-list 'tempel-user-elements my:tempel-docker-templates)))

  (provide 'dev/dev-docker)
  ;;; dev/dev-docker.el ends here
#+end_src

*** dev/web-core.el
:PROPERTIES:
:CUSTOM_ID: dev-web-core
:END:

#+begin_src emacs-lisp :tangle lisp/dev/web-core.el
  ;;; web-core.el --- Treesit & project core -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;; Core configuration for project management and modern syntax highlighting.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf project :straight nil)
  (leaf files   :straight nil
    :custom ((require-final-newline . t)
             (delete-trailing-lines . t))
    :hook ((before-save-hook . delete-trailing-whitespace)))

  (leaf editorconfig
    :straight t
    :global-minor-mode t)

  (leaf treesit
    :straight nil
    :custom ((major-mode-remap-alist
              . '((typescript-mode . typescript-ts-mode)
                  (js-mode         . js-ts-mode)
                  (json-mode       . json-ts-mode)
                  (css-mode        . css-ts-mode)
                  (yaml-mode       . yaml-ts-mode)
                  (sh-mode         . bash-ts-mode)))))

  (add-to-list 'auto-mode-alist '("\\.tsx\\'" . tsx-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.ts\\'"  . typescript-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.json\\'" . json-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.ya?ml\\'" . yaml-ts-mode))

  (provide 'dev/web-core)
  ;;; web-core.el ends here
#+end_src

*** dev/format.el
:PROPERTIES:
:CUSTOM_ID: dev-format
:END:

#+begin_src emacs-lisp :tangle lisp/dev/format.el
  ;;; format.el --- Prettier/ESLint via Apheleia -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf apheleia
    :straight t
    :require t
    :config
    (setf (alist-get 'prettierd apheleia-formatters)
          '("prettierd" filepath))
    (setf (alist-get 'prettier apheleia-formatters)
          '("npx" "prettier" "--stdin-filepath" filepath))
    (dolist (pair '((typescript-ts-mode . prettierd)
                    (tsx-ts-mode        . prettierd)
                    (json-ts-mode       . prettierd)
                    (css-ts-mode        . prettierd)
                    (markdown-mode      . prettierd)))
      (add-to-list 'apheleia-mode-alist pair))
    (apheleia-global-mode +1))

  (provide 'dev/format)
  ;;; format.el ends here
#+end_src

*** dev/infra-modes.el
:PROPERTIES:
:CUSTOM_ID: dev-infra-modes
:END:

#+begin_src emacs-lisp :tangle lisp/dev/infra-modes.el
  ;;; infra-modes.el --- Infra modes (.env / Compose / TOML / Make) -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;; Syntax support for infra files such as Docker Compose, .env, TOML, and Makefiles.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf docker-compose-mode :straight t :mode ("docker-compose\\.*ya?ml\\'"))
  (leaf dotenv-mode :straight t
    :mode (("\\.env\\..*\\'" . dotenv-mode)
           ("\\.env\\'"     . dotenv-mode)))
  (leaf toml-mode :straight t :mode ("\\.toml\\'" . toml-mode))

  (provide 'dev/infra-modes)
  ;;; infra-modes.el ends here
#+end_src

*** dev/sql.el
:PROPERTIES:
:CUSTOM_ID: dev-sql
:END:

#+begin_src emacs-lisp :tangle lisp/dev/sql.el
  ;;; lisp/dev/sql.el --- SQL/PostgreSQL helpers -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  (eval-when-compile (require 'leaf))

  (leaf sql :straight nil
    :custom ((sql-product . 'postgres)))

  (leaf sql-indent :straight t
    :hook (sql-mode . sqlind-minor-mode))

  (leaf sqlformat
    :straight t
    :custom ((sqlformat-command . 'pgformatter)
             (sqlformat-args . '("--nostyle")))
    :hook (sql-mode . sqlformat-on-save-mode)
    :hook (sql-ts-mode . sqlformat-on-save-mode))

  (provide 'dev/sql)
  ;;; sql.el ends here
#+end_src

*** dev/rest.el
:PROPERTIES:
:CUSTOM_ID: dev-rest
:END:

#+begin_src emacs-lisp :tangle lisp/dev/rest.el
  ;;; rest.el --- REST client helpers (Next.js / Ollama / Qdrant) -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: dev
  ;;
  ;; Commentary:
  ;; REST client integration for testing HTTP requests inside Emacs.
  ;; Supports JSON parsing with jq.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf restclient :straight t :mode ("\\.http\\'" . restclient-mode))
  (leaf restclient-jq :straight t :after restclient)

  (provide 'dev/rest)
  ;;; dev/rest.el ends here
#+end_src

*** utils/utils-functions.el
:PROPERTIES:
:CUSTOM_ID: utils-functions
:END:

#+begin_src emacs-lisp :tangle lisp/utils/utils-functions.el
  ;;; utils/utils-functions.el --- General utility functions -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: utils
  ;;
  ;;; Code:

  (defun my/kill-buffer-smart ()
    "Kill buffer and window, or just buffer if only one window."
    (interactive)
    (if (one-window-p)
        (kill-buffer)
      (kill-buffer-and-window)))

  ;;; Sidebar
  ;; This defines an org sidebar using imenu-list.
  (require 'imenu) ;; OK: built-in library

  ;; Do NOT require imenu-list at top-level; keep it lazy-loaded.
  (declare-function imenu-list-minor-mode "imenu-list")
  (declare-function imenu-list-stop-timer "imenu-list")
  (declare-function imenu-list-display-dwim "imenu-list")
  (defvar imenu-list--displayed-buffer nil) ;; Define to avoid unbound-variable errors in eval

  (defun my/nano-headerline (buf subtitle)
    "Return a header-line string. Prefer nano-modeline if available.
  BUF is the buffer to display the name of. SUBTITLE is an extra label."
    (let* ((name (cond
                  ((and buf (buffer-live-p buf)) (buffer-name buf))
                  (t (buffer-name))))
           (extra ""))
      (cond
       ;; Use nano-modeline when present
       ((fboundp 'nano-modeline-render)
        (nano-modeline-render nil name subtitle extra))
       ;; Fallback: a simple textual header-line
       (t
        (concat "  "
                (propertize name 'face 'mode-line-buffer-id)
                "  " subtitle)))))

  (defun my/org-tree-to-indirect-buffer ()
    "Create an indirect buffer for the current subtree, reveal code blocks, and mark blocks as visible."
    (org-tree-to-indirect-buffer)
    (org-show-block-all)
    (setq-local my/org-blocks-hidden nil))

  (defun my/org-sidebar ()
    "Open an imenu-list sidebar on the left for navigation, with safe fallbacks."
    (interactive)
    ;; Hard fail early if imenu-list is not installed, with guidance.
    (unless (locate-library "imenu-list")
      (user-error
       (concat "imenu-list is not installed. "
               "Install it or use `consult-imenu' / `imenu' instead.")))

    ;; Establish autoloads for the necessary imenu-list entry points.
    (autoload 'imenu-list-minor-mode "imenu-list" nil t)
    (autoload 'imenu-list-stop-timer  "imenu-list" nil t)
    (autoload 'imenu-list-display-dwim "imenu-list" nil t)

    (setq imenu-list-after-jump-hook #'my/org-tree-to-indirect-buffer
          imenu-list-position 'left
          imenu-list-size 36
          imenu-list-focus-after-activation t)

    (let ((heading (substring-no-properties (or (and (fboundp 'org-get-heading)
                                                     (org-get-heading t t t t))
                                                ""))))
      ;; If called from an indirect buffer, switch back to the base buffer.
      (when (buffer-base-buffer)
        (switch-to-buffer (buffer-base-buffer)))

      ;; Start the sidebar.
      (imenu-list-minor-mode 1)
      (when (fboundp 'imenu-list-stop-timer) (imenu-list-stop-timer))
      (hl-line-mode 1)

      ;; Only apply nano face when it exists.
      (when (facep 'nano-subtle)
        (face-remap-add-relative 'hl-line :inherit 'nano-subtle))

      ;; Main crash fix: do NOT call nano-modeline-render directly in header-line-format.
      ;; Route through a safe wrapper that checks availability.
      (setq header-line-format
            `(:eval
              (my/nano-headerline
               ,(when (boundp 'imenu-list--displayed-buffer)
                  'imenu-list--displayed-buffer)
               "(outline)")))

      (setq-local cursor-type nil)

      ;; If we captured a heading, try to jump to it and display items.
      (when (> (length heading) 0)
        (goto-char (point-min))
        (when (search-forward heading nil t)
          (when (fboundp 'imenu-list-display-dwim)
            (imenu-list-display-dwim))))))

  ;; This toggles the org-sidebar.
  (defun my/org-sidebar-toggle ()
    "Toggle the org-sidebar"

    (interactive)
    (if (get-buffer-window "*Ilist*")
        (progn
          (quit-window nil (get-buffer-window "*Ilist*"))
          (switch-to-buffer (buffer-base-buffer)))
      (my/org-sidebar)))

  (provide 'utils/utils-functions)
#+end_src

*** utils/utils-scratch.el
:PROPERTIES:
:CUSTOM_ID: utils-scratch
:END:

#+begin_src emacs-lisp :tangle lisp/utils/utils-scratch.el
  ;;; utils/utils-scratch.el --- Scratch buffer helpers -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: utils
  ;;
  ;;; Commentary:
  ;; Automatically recreates *scratch* after it is killed.
  ;; The recreated buffer uses `lisp-interaction-mode` and starts with a small header.
  ;;
  ;;; Code:

  (eval-when-compile (require 'leaf))

  (leaf my:scratch-auto-recreate
    :straight nil
    :init
    (defun my/create-scratch-buffer ()
      "Create or reset a `*scratch*` buffer with `lisp-interaction-mode` and a header."
      (let ((buf (get-buffer-create "*scratch*")))
        (with-current-buffer buf
          (lisp-interaction-mode)
          (erase-buffer)
          (insert ";; This is a new *scratch* buffer\n\n"))
        buf))

    (defun my/kill-scratch-buffer-advice (buf)
      "If BUF is *scratch*, recreate it shortly after kill."
      (when (string= (buffer-name buf) "*scratch*")
        ;; Recreate asynchronously to avoid interfering with the kill flow.
        (run-at-time 0 nil #'my/create-scratch-buffer)))

    ;; When any buffer is killed, if it's *scratch*, recreate it.
    (add-hook 'kill-buffer-hook
              (lambda ()
                (my/kill-scratch-buffer-advice (current-buffer)))))

  (provide 'utils/utils-scratch)
  ;;; utils/utils-scratch.el ends here
#+end_src

*** utils/utils-backup.el
:PROPERTIES:
:CUSTOM_ID: utils-backup
:END:

#+begin_src emacs-lisp :tangle lisp/utils/utils-backup.el
  ;;; utils/utils-backup.el --- Backup and auto-save helpers -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: utils
  ;;
  ;;; Code:

  (defun my/delete-old-backups ()
    "Delete backup files older than 7 days."
    (interactive)
    (let ((backup-dir (concat no-littering-var-directory "backup/"))
          (threshold (- (float-time (current-time)) (* 7 24 60 60))))
      (when (file-directory-p backup-dir)
        (dolist (file (directory-files backup-dir t))
          (when (and (file-regular-p file)
                     (< (float-time (file-attribute-modification-time
                                     (file-attributes file)))
                        threshold))
            (delete-file file))))))

  (add-hook 'emacs-startup-hook #'my/delete-old-backups)

  (provide 'utils/utils-backup)
  ;;; utils/utils-backup.el ends here
#+end_src

*** utils/utils-async.el
:PROPERTIES:
:CUSTOM_ID: utils-async
:END:

#+begin_src emacs-lisp :tangle lisp/utils/utils-async.el
  ;;; utils/utils-async.el --- Async helpers -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: utils
  ;;
  ;;; Code:

  (defun my/safe-run-async (task)
    "Run TASK asynchronously, catching and reporting any errors."
    (run-at-time 0 nil
                 (lambda ()
                   (condition-case err
                       (funcall task)
                     (error (message "[async] error: %s" err))))))

  (provide 'utils/utils-async)
  ;;; utils/utils-async.el ends here
#+end_src

*** utils/search-nav.el
:PROPERTIES:
:CUSTOM_ID: search-nav
:END:

#+begin_src emacs-lisp :tangle lisp/utils/search-nav.el
  ;;; utils/search-nav.el --- Project search & navigation -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; Author: YAMASHITA, Takao
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: utils
  ;;
  ;;; Commentary:
  ;; Modern search/navigation helpers using ripgrep (via consult) and dumb-jump fallback.
  ;;
  ;;; Code:

  (eval-when-compile
    (require 'leaf)
    (require 'subr-x))

  (leaf dumb-jump :straight t
    :hook (xref-backend-functions . dumb-jump-xref-activate)
    :custom ((dumb-jump-force-searcher  . 'rg)
             (dumb-jump-prefer-searcher . 'rg)))

  (leaf multiple-cursors :straight t)

  (leaf eww :straight nil
    :custom ((eww-search-prefix . "https://duckduckgo.com/html/?kl=jp-jp&k1=-1&kc=1&kf=-1&q=")
             (eww-download-directory . "~/Downloads"))
    :init
    (setopt eww-bookmarks-file (expand-file-name "eww-bookmarks" my:d:eww)
  	  eww-cache-directory (expand-file-name "cache/" my:d:eww))
    (my/ensure-directory-exists eww-cache-directory)
    (setq eww-history-limit 200)
    (defvar eww-hl-search-word nil "Word to highlight with isearch after EWW loads.")
    (defun my/eww-search (term)
      "Search TERM with EWW and start isearch."
      (interactive "sSearch terms: ")
      (setq eww-hl-search-word term)
      (eww-browse-url (concat eww-search-prefix term)))
    (add-hook 'eww-after-render-hook
              (lambda ()
                (when eww-hl-search-word
                  (isearch-mode t)
                  (isearch-yank-string eww-hl-search-word)
                  (setq eww-hl-search-word nil))))
    (defun my/eww-toggle-images () (interactive)
      (setq shr-inhibit-images (not shr-inhibit-images))
      (eww-reload)))

  (provide 'utils/search-nav)
  ;;; utils-search-nav.el ends here
#+end_src

** Personal Profile & Device Integrations — `user.el`
:PROPERTIES:
:CUSTOM_ID: personal-profile-and-device-integrations
:END:

*** Overview

**** Purpose
Provide **personal, device-specific overlays** on top of the core configuration:
- Identity (name/email) and safe editor defaults.
- Preferred **fonts/size**, and **global switches** for UI (`nano`) and LSP (`eglot`).
- A portable **Org directory layout** under your cloud path, plus agenda discovery.
- macOS conveniences: **input-method auto-switching** (English⇄Japanese) with `sis`, and **Apple Music** control (Hydra).
- Pointer/scroll tuning for the **Logitech MX Ergo** profile.
- Small QoL touches (cursor color normalization after theme changes, load-path exclusions).

**** What this configuration does
- **Identity & safety**
  - Sets `user-full-name`, `user-mail-address`.
  - Disables compacting font caches on macOS; enables passphrase caching for `plstore`.
- **Look & feel**
  - Declares personal font choices (`my:font-*`) and default size.
  - Chooses UI bundle and LSP backend via `my:use-ui` / `my:use-lsp`.
- **Directories / Org**
  - Defines cloud base at `~/Documents` and derives `my:d:org`, `my:d:blog`.
  - Ensures directories exist; points `org-directory` there.
  - Builds `org-agenda-files` by recursively scanning `.org` files (skips “archives”).
  - Excludes sensitive paths from `load-path`.
- **macOS input method (`sis`)**
  - On startup, configures ABC ⇄ Kotoeri (Romaji) via `macism`.
  - Enables cursor-color, respect, and inline modes globally (when available).
- **Cursor color**
  - After any theme load, re-applies frame cursor color to the `cursor` face.
- **MX Ergo S profile**
  - Smooth wheel, conservative scrolling, margin preservation, tilt scrolling; binds:
    - `mouse-2` → `yank`, `mouse-4/5` → previous/next buffer.
- **Apple Music integration (macOS)**
  - Async/sync `osascript` helpers; commands for play/pause/next/prev.
  - Query current track info; list & play playlists.
  - A **Hydra** with quick keys; wired into **Meow** leader (`SPC M`).

**** Module map (where things live)
| Module file | Role |
|-------------+------|
| =user.el=   | Personal overlay: identity, fonts/UI/LSP switches, Org locations, macOS `sis`, device scroll/mouse, Apple Music Hydra |

**** How it works (flow)
1. **Bootstrap personals**: set identity, fonts, UI/LSP switches; define cloud/Org/blog paths; ensure dirs exist.
2. **Org wiring**: set `org-directory`; compute `org-agenda-files` by filtering non-archive `.org` files.
3. **Hygiene**: remove `my:d:excluded-directories` from `load-path`.
4. **macOS only**:
   - Configure `sis` (ABC ↔ Kotoeri) and enable global modes if functions exist.
   - Hook `after-load-theme` to keep the `cursor` face background in sync with the frame cursor color.
5. **Device profile**: apply scroll & mouse bindings tailored for MX Ergo S.
6. **Apple Music**:
   - Define async/sync `osascript` runners.
   - Provide commands (`play-pause`, `next`, `previous`, `current-track-info`, `play-playlist`).
   - Define `my/hydra-apple-music` and bind it to `C-c M` and **Meow** leader `SPC M`.

**** Key settings (reference)
- =my:font-default= :: `"JetBrains Mono NL"`
- =my:font-alt= :: `"Noto Sans JP"`
- =my:emoji-font= :: `"Apple Color Emoji"`
- =my:font-size= :: `18`
- =my:use-ui= :: `'nano`
- =my:use-lsp= :: `'eglot`
- =org-directory= :: `~/Documents/org` (derived from `my:d:cloud`)
- =org-agenda-files= :: All `*.org` under `org-directory`, excluding paths matching `archives`.
- =MX Ergo wheel/scroll= :: `mouse-wheel-scroll-amount='(1 ((shift) . 5) ((control) . 10))`, `scroll-conservatively=10000`, `scroll-margin=2`, `scroll-preserve-screen-position=t`, tilt scroll on.
- =Cursor color keep-alive= :: Re-sets `(set-face-background 'cursor (frame-parameter nil 'cursor-color))` on `after-load-theme`.

**** Usage tips
- **Switch UI or LSP quickly**: set `my:use-ui` to `'nano`/`'doom`/`'none`, `my:use-lsp` to `'eglot`/`'lsp`.
- **Change fonts**: adjust `my:font-*` and `my:font-size` here; your existing `ui/ui-font.el` will pick them up.
- **Apple Music control**:
  - `C-c M` opens the Hydra; with **Meow**: `SPC M`.
  - Keys: `p` (play/pause), `n` (next), `b` (back), `l` (choose playlist), `i` (track info).
- **Agenda scope**: drop an `archives/` folder or use “archives” in filenames to exclude from `org-agenda-files`.

**** Troubleshooting
- *`sis` not switching on macOS* → Ensure the bundle is installed and input source IDs match:
  `"com.apple.keylayout.ABC"` and `"com.apple.inputmethod.Kotoeri.RomajiTyping.Japanese"`. The code guards with `(fboundp ...)`, so missing functions won’t error.
- *Cursor color looks wrong after theme change* → The `after-load-theme` hook reapplies it; ensure your theme sets the frame’s `'cursor-color` parameter.
- *Hydra key not found* → Confirm `hydra` is installed/loaded; the Hydra is wrapped in `with-eval-after-load 'hydra`.
- *Meow leader binding missing* → Requires both `meow` and `hydra` loaded; binding occurs inside nested `with-eval-after-load`.

**** Related source blocks (already provided as =.el= file)

#+begin_src emacs-lisp :tangle no
;; See:
;; - user.el  (personal overlay: fonts/UI/LSP, Org locations, macOS sis, MX Ergo, Apple Music Hydra)
#+end_src

*** user.el

#+begin_src emacs-lisp :tangle (eval (format "personal/%s.el" (user-login-name)))
  ;;; user.el --- Personal configuration -*- lexical-binding: t; -*-
  ;;
  ;; Copyright (c) 2021-2025
  ;; License: GNU GPL v3 or later
  ;;
  ;; Category: personal
  ;;
  ;;; Commentary:
  ;; Personal settings and device-specific configurations.
  ;;
  ;;; Code:

  (eval-when-compile
    (require 'leaf)
    (require 'subr-x))

  ;;; Personal Information
  (leaf *personals
    :straight nil
    :init
    (setq user-full-name "YAMASHITA, Takao"
          user-mail-address "tjy1965@gmail.com"
          inhibit-compacting-font-caches t
          plstore-cache-passphrase-for-symmetric-encryption t)

    (setq my:font-default "JetBrains Mono NL"
          my:font-alt     "Noto Sans JP"
          my:emoji-font   "Apple Color Emoji"
          my:font-size    18)

    (setq my:use-ui 'nano
          my:use-lsp 'eglot)

    (defvar my:d:cloud
      (expand-file-name "Documents" (getenv "HOME"))
      "Cloud storage base directory.")
    (defvar my:d:org
      (expand-file-name "org" my:d:cloud)
      "Org directory.")
    (defvar my:d:blog
      (expand-file-name "devel/repos/mysite" my:d:cloud)
      "Blog directory.")
    (defvar my:f:capture-blog-file
      (expand-file-name "all-posts.org" my:d:blog)
      "Default Org file for blog captures.")
    (defvar my:d:excluded-directories
      (list (expand-file-name "Library/Accounts" (getenv "HOME")))
      "Directories excluded from `load-path` for safety.")

    (mapc #'my/ensure-directory-exists (list my:d:cloud my:d:org my:d:blog))

    (setq org-directory my:d:org)
    (setq org-agenda-files
          (seq-filter (lambda (file)
                        (and (string-match-p "\\.org\\'" file)
                             (not (string-match-p "archives" file))))
                      (directory-files-recursively org-directory "\\.org\\'")))

    (setq load-path
          (seq-remove (lambda (dir)
                        (member dir my:d:excluded-directories))
                      load-path)))

  ;;; SIS
  (leaf sis
    :if (eq system-type 'darwin)
    :straight t
    :commands (sis-ism-lazyman-config
               sis-global-cursor-color-mode
               sis-global-respect-mode
               sis-global-inline-mode)
    :hook (emacs-startup .
           (lambda ()
             (when (fboundp 'sis-ism-lazyman-config)
               (sis-ism-lazyman-config
                "com.apple.keylayout.ABC"
                "com.apple.inputmethod.Kotoeri.RomajiTyping.Japanese"
                'macism))
             (when (fboundp 'sis-global-cursor-color-mode)
               (sis-global-cursor-color-mode t))
             (when (fboundp 'sis-global-respect-mode)
               (sis-global-respect-mode t))
             (when (fboundp 'sis-global-inline-mode)
               (sis-global-inline-mode t)))))

  (add-hook 'after-load-theme-hook
            (lambda ()
              (set-face-background 'cursor (frame-parameter nil 'cursor-color))))

  ;;; Device: MX_ErgoS
  (leaf *device/MX_ErgoS
    :straight nil
    :init
    (setq mouse-wheel-scroll-amount '(1 ((shift) . 5) ((control) . 10))
          mouse-wheel-progressive-speed nil
          scroll-conservatively 10000
          scroll-margin 2
          scroll-preserve-screen-position t
          mac-mouse-wheel-smooth-scroll t
          mouse-wheel-tilt-scroll t
          mouse-wheel-flip-direction nil)
    (global-set-key [mouse-2] #'yank)
    (global-set-key [mouse-4] #'previous-buffer)
    (global-set-key [mouse-5] #'next-buffer))

  ;;; Apple Music
  (when (eq system-type 'darwin)
    (defun my/apple-music-osascript-async (script &optional callback)
      "Run AppleScript SCRIPT asynchronously, invoking CALLBACK with its output."
      (let* ((proc-name "apple-music-async")
             (buffer-name "*Apple Music Async*")
             (osascript-cmd (list "osascript" "-e" script))
             (proc (apply #'start-process proc-name buffer-name osascript-cmd)))
        (when callback
          (set-process-sentinel
           proc
           (lambda (process event)
             (when (string= event "finished\n")
               (with-current-buffer (process-buffer process)
                 (let ((output (string-trim (buffer-string))))
                   (funcall callback output)))
               (kill-buffer (process-buffer process))))))))

    (defun my/apple-music-osascript-sync (script)
      "Run AppleScript SCRIPT synchronously and return trimmed output."
      (string-trim
       (shell-command-to-string
        (format "osascript -e '%s'" script))))

    ;;;###autoload
    (defun my/apple-music-play-pause () (interactive)
      "Toggle play/pause in Apple Music."
      (my/apple-music-osascript-async "tell application \"Music\" to playpause"))

    ;;;###autoload
    (defun my/apple-music-next-track () (interactive)
      "Skip to the next track in Apple Music."
      (my/apple-music-osascript-async "tell application \"Music\" to next track"))

    ;;;###autoload
    (defun my/apple-music-previous-track () (interactive)
      "Return to the previous track in Apple Music."
      (my/apple-music-osascript-async "tell application \"Music\" to previous track"))

    ;;;###autoload
    (defun my/apple-music-current-track-info () (interactive)
      "Echo current track info: Title — Artist [Album]."
      (let ((info (my/apple-music-osascript-sync
                   "tell application \"Music\" \
  to (get name of current track) & \" — \" & (get artist of current track) & \" [\" & (get album of current track) & \"]\"")))
        (message "%s" info)
        info))

    (defun my/apple-music-get-playlists ()
      "Return a list of playlist names from Apple Music."
      (split-string
       (my/apple-music-osascript-sync
        "tell application \"Music\" to get name of playlists")
       ", "))

    ;;;###autoload
    (defun my/apple-music-play-playlist (playlist)
      "Prompt for PLAYLIST and play it in Apple Music."
      (interactive
       (list (completing-read "Playlist: " (my/apple-music-get-playlists))))
      (my/apple-music-osascript-async
       (format "tell application \"Music\" to play playlist \"%s\"" playlist)))

    (with-eval-after-load 'hydra
      (defhydra my/hydra-apple-music (:hint nil)
        "
  Apple Music:
  _p_: Play/Pause  _n_: Next  _b_: Back  _l_: Playlist  _i_: Info  _q_: Quit
  "
        ("p" my/apple-music-play-pause)
        ("n" my/apple-music-next-track)
        ("b" my/apple-music-previous-track)
        ("l" my/apple-music-play-playlist)
        ("i" my/apple-music-current-track-info)
        ("q" nil "quit"))
      (global-set-key (kbd "C-c M") #'my/hydra-apple-music/body)))

  ;; Meow leader: <leader> m -> Apple Music hydra
  (with-eval-after-load 'meow
    (with-eval-after-load 'hydra
      (when (fboundp 'meow-leader-define-key)
        (meow-leader-define-key
         '("M" . my/hydra-apple-music/body)))))

  (provide 'user)
  ;;; user.el ends here
#+end_src
